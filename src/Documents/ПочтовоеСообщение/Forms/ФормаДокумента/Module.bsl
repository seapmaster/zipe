
////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	МассивСтрок = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПочтовыеСообщенияИсходящие.Отправлено КАК Отправлено,
	|	ПочтовыеСообщенияИсходящие.СообщениеОбОшибке КАК СообщениеОбОшибке,
	|	ПочтовыеСообщенияИсходящие.Дата КАК Дата,
	|	ПочтовыеСообщенияИсходящие.КоличествоПопыток КАК КоличествоПопыток
	|ИЗ
	|	РегистрСведений.ПочтовыеСообщенияИсходящие КАК ПочтовыеСообщенияИсходящие
	|ГДЕ
	|	ПочтовыеСообщенияИсходящие.ПочтовоеСообщение = &ПочтовоеСообщение";
	
	Запрос.УстановитьПараметр("ПочтовоеСообщение", ТекущийОбъект.Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		
		Если ВыборкаДетальныеЗаписи.Отправлено Тогда
			
			Цвет = WebЦвета.ТемноЗеленый;
		    СтрокаСостояния = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Сообщение отправлено ""%1""'"), ВыборкаДетальныеЗаписи.Дата);
			
		Иначе
			
			Цвет = WebЦвета.Красный;
		    СтрокаСостояния = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Сообщение не отправлено! Дата последней попытки ""%1"", количество попыток ""%2"", по причине ""%3""'"), 
			ВыборкаДетальныеЗаписи.Дата, ВыборкаДетальныеЗаписи.КоличествоПопыток, ВыборкаДетальныеЗаписи.СообщениеОбОшибке);
			
		КонецЕсли;
		
		ФССтрокаСостояния = Новый ФорматированнаяСтрока(СтрокаСостояния, , Цвет); 		
		МассивСтрок.Добавить(ФССтрокаСостояния);
		
	Иначе
		
		МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Сообщение не отправлено!'"),,WebЦвета.Красный));

	КонецЕсли;		
	
	ИнформацияОбОтправкеПисьма = Новый ФорматированнаяСтрока(МассивСтрок);	
	
КонецПроцедуры  //ПриЧтенииНаСервере
