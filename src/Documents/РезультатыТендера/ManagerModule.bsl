/////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Функция ПолучитьТаблицуДвиженийЦены(Ссылка) Экспорт 
	Запрос 					= Новый Запрос;
	#Область ТекстЗапроса
	Запрос.Текст 	= "ВЫБРАТЬ
	             	  |	РезультатыТендераЦены.Ссылка.ДатаТендера КАК ДатаТендера,
	             	  |	РезультатыТендераЦены.ПредметСнабжения КАК ПредметСнабжения,
	             	  |	РезультатыТендераЦены.Поставщик КАК Продавец,
	             	  |	РезультатыТендераЦены.Ссылка.Заказчик КАК Покупатель,
	             	  |	РезультатыТендераЦены.Ссылка.Заказ КАК Заказ,
	             	  |	РезультатыТендераЦены.Ссылка КАК Тендер,
	             	  |	РезультатыТендераЦены.Цена КАК Цена,
	             	  |	РезультатыТендераЦены.Количество КАК Количество,
	             	  |	РезультатыТендераЦены.Ссылка.Валюта КАК Валюта
	             	  |ПОМЕСТИТЬ ВТ_ДанныеТендера
	             	  |ИЗ
	             	  |	Документ.РезультатыТендера.Цены КАК РезультатыТендераЦены
	             	  |ГДЕ
	             	  |	РезультатыТендераЦены.Ссылка = &Ссылка
	             	  |;
	             	  |
					  |////////////////////////////////////////////////////////////////////////////////
	             	  |ВЫБРАТЬ
	             	  |	УсловияПоставок.Количество КАК Количество,
					  |	УсловияПоставок.Ссылка КАК УсловиеПоставки
					  |ИЗ
	             	  |	ВТ_ДанныеТендера КАК ВТ_ДанныеТендера
	             	  |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.УсловияПоставок КАК УсловияПоставок
	             	  |		ПО ВТ_ДанныеТендера.Количество = УсловияПоставок.Количество
	             	  |;
	             	  |
	             	  |////////////////////////////////////////////////////////////////////////////////
	             	  |ВЫБРАТЬ
	             	  |	КлючиАналитикиЦен.Продавец КАК Продавец,
	             	  |	КлючиАналитикиЦен.Покупатель КАК Покупатель,
	             	  |	КлючиАналитикиЦен.Заказ КАК Заказ,
	             	  |	КлючиАналитикиЦен.Тендер КАК Тендер,
	             	  |	КлючиАналитикиЦен.Ссылка КАК КлючАналитики
	             	  |ИЗ
	             	  |	ВТ_ДанныеТендера КАК ВТ_ДанныеТендера
	             	  |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиЦен КАК КлючиАналитикиЦен
	             	  |		ПО ВТ_ДанныеТендера.Продавец = КлючиАналитикиЦен.Продавец
	             	  |			И ВТ_ДанныеТендера.Покупатель = КлючиАналитикиЦен.Покупатель
	             	  |			И ВТ_ДанныеТендера.Заказ = КлючиАналитикиЦен.Заказ
	             	  |			И ВТ_ДанныеТендера.Тендер = КлючиАналитикиЦен.Тендер
	             	  |;
	             	  |
	             	  |////////////////////////////////////////////////////////////////////////////////
	             	  |ВЫБРАТЬ
	             	  |	ВТ_ДанныеТендера.ДатаТендера КАК Период,
	             	  |	ВТ_ДанныеТендера.ПредметСнабжения КАК ПредметСнабжения,
	             	  |	ЗНАЧЕНИЕ(Справочник.ВидыЦен.Конкурентная) КАК ВидЦены,
	             	  |	ВТ_ДанныеТендера.Продавец КАК Продавец,
	             	  |	ВТ_ДанныеТендера.Покупатель КАК Покупатель,
	             	  |	ВТ_ДанныеТендера.Заказ КАК Заказ,
	             	  |	ВТ_ДанныеТендера.Тендер КАК Тендер,
	             	  |	ВТ_ДанныеТендера.Цена КАК Цена,
	             	  |	ВТ_ДанныеТендера.Количество КАК Количество,
	             	  |	ВТ_ДанныеТендера.Валюта КАК Валюта,
					  | Неопределено Как КлючАналитики,
					  | Неопределено Как УсловиеПоставки
	             	  |ИЗ
	             	  |	ВТ_ДанныеТендера КАК ВТ_ДанныеТендера";
	#КонецОбласти
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	РезультатЗапроса 		= Запрос.ВыполнитьПакет();
	ТаблицаУсловийПоставок 	= РезультатЗапроса[РезультатЗапроса.Количество() - 3].Выгрузить();
	ТаблицаКлючейАналитики 	= РезультатЗапроса[РезультатЗапроса.Количество() - 2].Выгрузить();
	ТаблицаЦен 				= РезультатЗапроса[РезультатЗапроса.Количество() - 1].Выгрузить();
	// Заполним ключ аналитики
	Для Каждого СтрокаТаблицыЦен Из ТаблицаЦен Цикл
		СтруктураОтбора 	= Новый Структура("Продавец,Покупатель,Заказ,Тендер");
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаТаблицыЦен);
		НайденныеСтроки 	= ТаблицаКлючейАналитики.НайтиСтроки(СтруктураОтбора);
		Если НайденныеСтроки.Количество() = 0 Тогда
			СтрокаТаблицыЦен.КлючАналитики = Справочники.КлючиАналитикиЦен.СоздатьКлюч(СтруктураОтбора);
		Иначе
			СтрокаТаблицыЦен.КлючАналитики = НайденныеСтроки[0].КлючАналитики;
		КонецЕсли; // Если НайденныеСтроки.Количество() = 0 Тогда
		
		СтруктураОтбораУсловияПоставки 	= Новый Структура("Количество");
		ЗаполнитьЗначенияСвойств(СтруктураОтбораУсловияПоставки, СтрокаТаблицыЦен);
		НайденныеСтроки 	= ТаблицаУсловийПоставок.НайтиСтроки(СтруктураОтбораУсловияПоставки);
		Если НайденныеСтроки.Количество() = 0 Тогда
			СтрокаТаблицыЦен.УсловиеПоставки = Справочники.УсловияПоставок.СоздатьУсловиеПоставки(СтруктураОтбораУсловияПоставки);
		Иначе
			СтрокаТаблицыЦен.УсловиеПоставки = НайденныеСтроки[0].УсловиеПоставки;
		КонецЕсли; // Если НайденныеСтроки.Количество() = 0 Тогда

	КонецЦикла; // Для Каждого СтрокаТаблицыЦен Из ТаблицаЦен Цикл	
	Возврат ТаблицаЦен;
КонецФункции // ПолучитьТаблицуДвиженийЦены
