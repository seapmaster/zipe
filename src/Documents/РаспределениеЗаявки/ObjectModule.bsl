/////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Функция ЕстьОшибки()
	Результат = Ложь;
	
	// Проверка на превышение количества распределения
	Запрос 			= Новый Запрос;
	Запрос.Текст 	= "ВЫБРАТЬ
	             	  |	МАКСИМУМ(РаспределениеЗаявкиПредметыСнабжения.НомерСтроки) КАК НомерСтроки,
	             	  |	РаспределениеЗаявкиПредметыСнабжения.ПредметСнабжения КАК ПредметСнабжения,
	             	  |	СУММА(РаспределениеЗаявкиПредметыСнабжения.Количество) КАК Количество
	             	  |ПОМЕСТИТЬ ВТ_ПредметыСнабжения
	             	  |ИЗ
	             	  |	Документ.РаспределениеЗаявки.ПредметыСнабжения КАК РаспределениеЗаявкиПредметыСнабжения
	             	  |ГДЕ
	             	  |	РаспределениеЗаявкиПредметыСнабжения.Ссылка = &Ссылка
	             	  |
	             	  |СГРУППИРОВАТЬ ПО
	             	  |	РаспределениеЗаявкиПредметыСнабжения.ПредметСнабжения
	             	  |;
	             	  |
	             	  |////////////////////////////////////////////////////////////////////////////////
	             	  |ВЫБРАТЬ
	             	  |	ВТ_ПредметыСнабжения.НомерСтроки КАК НомерСтроки,
	             	  |	ВТ_ПредметыСнабжения.ПредметСнабжения КАК ПредметСнабжения,
	             	  |	ВТ_ПредметыСнабжения.Количество КАК Количество,
	             	  |	ЕСТЬNULL(РаспределениеЗаявокОбороты.КоличествоОборот, 0) + ВТ_ПредметыСнабжения.Количество - ЕСТЬNULL(ЗаявкиФСВТСОбороты.КоличествоОборот, 0) КАК Превышение
	             	  |ИЗ
	             	  |	ВТ_ПредметыСнабжения КАК ВТ_ПредметыСнабжения
	             	  |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗаявкиФСВТС.Обороты(, , , Заявка = &Заявка) КАК ЗаявкиФСВТСОбороты
	             	  |		ПО ВТ_ПредметыСнабжения.ПредметСнабжения = ЗаявкиФСВТСОбороты.ПредметСнабжения
	             	  |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РаспределениеЗаявок.Обороты(, , , Заявка = &Заявка) КАК РаспределениеЗаявокОбороты
	             	  |		ПО ВТ_ПредметыСнабжения.ПредметСнабжения = РаспределениеЗаявокОбороты.ПредметСнабжения
	             	  |ГДЕ
	             	  |	ЕСТЬNULL(РаспределениеЗаявокОбороты.КоличествоОборот, 0) + ВТ_ПредметыСнабжения.Количество > ЕСТЬNULL(ЗаявкиФСВТСОбороты.КоличествоОборот, 0)";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Заявка", Ссылка.Заявка);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Строка №" + Выборка.НомерСтроки + ". Превышение - " + Выборка.Превышение,,
							"Объект.ПредметыСнабжения[" + (Выборка.НомерСтроки - 1) + "].Количество");
		Результат = Истина;
	КонецЦикла; // Пока Выборка.Следующий() Цикл
	
	Возврат Результат;
КонецФункции // ЕстьОшибки

/////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ               

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	Если ЕстьОшибки() Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли; // Если ЕстьОшибки() Тогда
	
	// РН РаспределениеЗаявок
	ТаблицаДвижений = Документы.РаспределениеЗаявки.СформироватьТаблицуДвиженийРаспределениеЗаявок(Ссылка);
	Движения.РаспределениеЗаявок.Загрузить(ТаблицаДвижений);
КонецПроцедуры // ОбработкаПроведения