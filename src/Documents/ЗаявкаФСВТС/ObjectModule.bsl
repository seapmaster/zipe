///////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Функция ЕстьОшибки()
	Результат = Ложь;
	
	// Проверка на вхождение изделия в корабли
	МассивКораблей = Корабли.ВыгрузитьКолонку("Корабль");
	Для Сч = 0 По ПредметыСнабжения.Количество() - 1 Цикл
		Строка = ПредметыСнабжения[Сч];
		Если МассивКораблей.Найти(Строка.Изделие.Владелец) = Неопределено Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Строка №" 	+ (Сч + 1) 
																			+ ". Изделие принадлежит кораблю, не указанному в списке кораблей",,
																			"Объект.ПредметыСнабжения[" + Сч + "].Изделие");
			Результат = Истина;
		КонецЕсли; // Если МассивКораблей.Найти(Строка.Изделие.Владелец) = Неопределено Тогда		
	КонецЦикла; // Для Сч = 0 По ПредметыСнабжения.Количество() - 1 Цикл
	
	Возврат Результат;
КонецФункции // ЕстьОшибки

///////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Функция ПолучитьПроекты() Экспорт
	МассивПроектов 	= Новый Массив;
	Для Каждого Строка Из Корабли Цикл
		Проект 	= Строка.Корабль.Владелец;
		Если МассивПроектов.Найти(Проект) = Неопределено Тогда
			МассивПроектов.Добавить(Проект);
		КонецЕсли; // Если Проекты.Найти(Строка.Корабль.Владелец) = Неопределено Тогда		
	КонецЦикла; // Для Каждого Строка Из Корабли Цикл	
	Возврат МассивПроектов;
КонецФункции // ПолучитьПроекты

Процедура ЗаполнитьНеидентифицированныеПредметыСнабжения() Экспорт
	Для Каждого Строка Из ПредметыСнабжения Цикл
		Если ЗначениеЗаполнено(Строка.ПредметСнабжения) Тогда
			Продолжить;
		КонецЕсли; // Если ЗначениеЗаполнено(Строка.ПредметСнабжения) Тогда
		
		НеидентицифированныйПС = Справочники.НеиденитифицированныеПредметыСнабжения.НайтиПоОбозначению(Строка.ОбозначениеПС);
		Если НеидентицифированныйПС = Неопределено Тогда
			НеидентицифированныйПС = Справочники.НеиденитифицированныеПредметыСнабжения.Создать(Строка.НаименованиеПС, Строка.ОбозначениеПС);
		КонецЕсли; // Если НеидентицифированныйПС = Неопределено Тогда
		
		Строка.ПредметСнабжения = НеидентицифированныйПС;
	КонецЦикла; // Для Каждого Строка Из ПредметыСнабжения Цикл	
КонецПроцедуры // ЗаполнитьНеидентифицированныеПредметыСнабжения

///////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	//Если ЕстьОшибки() Тогда
	//	Отказ = Истина;
	//	Возврат
	//КонецЕсли; // Если ЕстьОшибки() Тогда
	
	// РН ЗаявкиФСВТС
	ТаблицаДвижений = Документы.ЗаявкаФСВТС.СформироватьТаблицуДвиженийЗаявкиФСВТС(Ссылка);
	Движения.ЗаявкиФСВТС.Загрузить(ТаблицаДвижений);
КонецПроцедуры // ОбработкаПроведения

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)	
	МассивПроектов 					= ПолучитьПроекты();
	Если МассивПроектов.Количество() > 0 Тогда
		Проекты = СтрСоединить(МассивПроектов, ", ");
	КонецЕсли; // Если МассивПроектов.Количество() > 0 Тогда	
КонецПроцедуры // ПередЗаписью
