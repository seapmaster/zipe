
//////////////////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

#Область Печать

Функция СформироватьПечатнуюФормуЗапросНаКоммерческоеПредложение(МассивОбъектов, ОбъектыПечати) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗапросНаКоммерческоеПредложениеПредметыСнабжения.Ссылка КАК Ссылка,
	|	ЗапросНаКоммерческоеПредложениеПредметыСнабжения.ПредметСнабжения КАК Наименование,
	|	ЗапросНаКоммерческоеПредложениеПредметыСнабжения.ПредметСнабжения.UID КАК Идентификатор,
	|	ЗапросНаКоммерческоеПредложениеПредметыСнабжения.ПредметСнабжения.ДокументНаПоставку КАК ОбозначениеТУ,
	|	ЗапросНаКоммерческоеПредложениеПредметыСнабжения.Количество КАК Количество,
	|	ЗапросНаКоммерческоеПредложениеПредметыСнабжения.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ЗаявкаСпецификация.Изделие.Родитель КАК Входимость
	|ИЗ
	|	Документ.ЗапросНаКоммерческоеПредложение.ПредметыСнабжения КАК ЗапросНаКоммерческоеПредложениеПредметыСнабжения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Заявка.Спецификация КАК ЗаявкаСпецификация
	|		ПО ЗапросНаКоммерческоеПредложениеПредметыСнабжения.ИдентификаторПозиции = ЗаявкаСпецификация.ИдентификаторПозиции
	|ГДЕ
	|	ЗапросНаКоммерческоеПредложениеПредметыСнабжения.Ссылка В(&МассивДокументов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|ИТОГИ ПО
	|	Ссылка");
		
	Запрос.УстановитьПараметр("МассивДокументов", МассивОбъектов);
		
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ЗапросНаКоммерческоеПредложение_ЗапросНаКоммерческоеПредложение";
	
	Макет = Документы.ЗапросНаКоммерческоеПредложение.ПолучитьМакет("ЗапросНаКоммерческоеПредложение");
	
	ДанныеПечатиВыборкаСсылка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ПервыйДокумент = Истина;
	Пока ДанныеПечатиВыборкаСсылка.Следующий() Цикл
	
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		// Выводим общие реквизиты шапки.
		ЗаголовокТаблицы = Макет.ПолучитьОбласть("Шапка");
		
		Валюта = ДанныеПечатиВыборкаСсылка.Ссылка.Валюта;
		
		ЗаголовокТаблицы.Параметры.Валюта = ?((ЗначениеЗаполнено(Валюта) И Валюта.Код = "643"), "руб.", Валюта.Наименование);
		
		ТабличныйДокумент.Вывести(ЗаголовокТаблицы);
			
		// Выводим таблицу.
		ВыборкаДетальныеЗаписи = ДанныеПечатиВыборкаСсылка.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	        ОбластьМакета.Параметры.Заполнить(ВыборкаДетальныеЗаписи);
		    ТабличныйДокумент.Вывести(ОбластьМакета);	
		
		КонецЦикла;
		
	КонецЦикла;
		
	Возврат ТабличныйДокумент;

КонецФункции // СформироватьПечатнуюФормуЗапросНаКоммерческоеПредложение

#КонецОбласти