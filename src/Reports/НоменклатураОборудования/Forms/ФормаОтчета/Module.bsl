&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьСписокПроектов();
	УстановитьЗначенияПараметров();
	
КонецПроцедуры

&НаКлиенте
Процедура ПараметрПриИзменении(Элемент)
	
	Если Элемент.Имя = "Страна" Тогда
	
		УстановитьСписокПроектов();
		Корабль = ЗаказыПустаяСсылка;
		ЗаводскойНомер = "";
		Система = СтруктураЗаказаПустаяСсылка;
		Оборудование = СтруктураЗаказаПустаяСсылка;
	
	КонецЕсли;
	
	УстановитьЗначенияПараметров();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСписокПроектов()
	
	Элементы.Проект.СписокВыбора.Очистить();
	Проект = Справочники.ПроектыКораблей.ПустаяСсылка();
	
	Если Не ЗначениеЗаполнено(Страна) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Заказы.Владелец КАК Проект
	|ИЗ
	|	Справочник.Заказы КАК Заказы
	|ГДЕ
	|	Заказы.Заказчик В
	|			(ВЫБРАТЬ
	|				Флоты.Ссылка КАК Ссылка
	|			ИЗ
	|				Справочник.Флоты КАК Флоты
	|			ГДЕ
	|				Флоты.Страна = &Страна)";
	Запрос.УстановитьПараметр("Страна", Страна);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Элементы.Проект.СписокВыбора.ЗагрузитьЗначения(РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Проект"));
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗначенияПараметров()
	
	ПараметрыОтчета = Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы;
	
	УстановитьЗначениеПараметра(ПараметрыОтчета, "Страна");
	УстановитьЗначениеПараметра(ПараметрыОтчета, "Проект");
	УстановитьЗначениеПараметра(ПараметрыОтчета, "Корабль");
	УстановитьЗначениеПараметра(ПараметрыОтчета, "ЗаводскойНомер");
	УстановитьЗначениеПараметра(ПараметрыОтчета, "Система");
	УстановитьЗначениеПараметра(ПараметрыОтчета, "Изготовитель");
	УстановитьЗначениеПараметра(ПараметрыОтчета, "Оборудование");
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗначениеПараметра(ПараметрыОтчета, ИмяПараметра)
	
	ПараметрОтчета = ПараметрыОтчета.Найти(ИмяПараметра);
	ПараметрОтчета.Использование = Истина;
	ПараметрОтчета.Значение = ЭтаФорма[ИмяПараметра];
	
КонецПроцедуры