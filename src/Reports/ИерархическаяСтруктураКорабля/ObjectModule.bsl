///////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Функция ПолучитьЭлементыПоОтборам(Корабль, СписокПоставщиков, СписокРазработчиков, СписокИзготовителей, ТолькоИзделия) Экспорт
	
	Если СписокПоставщиков.Количество() = 0 И СписокРазработчиков.Количество() = 0 И СписокИзготовителей.Количество() = 0 И Не ТолькоИзделия Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	//собираю текст запроса
	ТекстЗапроса = 	ПолучитьНеизменнуюЧастьЗапроса();
	
	Если ТолькоИзделия Тогда
		ДобавитьТаблицуСпецификацийВЗапрос(ТекстЗапроса);
	КонецЕсли;
	
	ЧастьВывод = ПолучитьЧастьЗапросаВывод();

	Если ТолькоИзделия Тогда
		ДобавитьСвязьСТаблицейСпецификаций(ЧастьВывод);
	КонецЕсли;
		
	ДобавитьУсловияЗапросаДляВывода(СписокПоставщиков, СписокРазработчиков, СписокИзготовителей,ТолькоИзделия,  ЧастьВывод);
	
	ТекстЗапроса = ТекстЗапроса + ЧастьВывод;
	//
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.УстановитьПараметр("Изготовители", 	СписокИзготовителей);
	Запрос.УстановитьПараметр("Поставщики", 	СписокПоставщиков);
	Запрос.УстановитьПараметр("Разработчики", 	СписокРазработчиков);
	Запрос.УстановитьПараметр("Корабль", 		Корабль);
	Запрос.УстановитьПараметр("ТолькоИзделия", 	ТолькоИзделия);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Элемент");
	
КонецФункции // ПолучитьЭлементыПоОтборам

Функция ПолучитьВсеЭлементыСтруктурыКорабля(МассивЭлементовПоОтбору, Корабль) Экспорт
	
	Если МассивЭлементовПоОтбору = Неопределено Тогда
		
		Условие = "СтруктураЗаказаПоКомплектующимИзделиямИЗИП.Владелец = &Корабль 
		| И НЕ СтруктураЗаказаПоКомплектующимИзделиямИЗИП.ПометкаУдаления";
		//| И НЕ СтруктураЗаказаПоКомплектующимИзделиямИЗИП.Наименование = ""Архив""";
		
	Иначе
		
		Условие = "СтруктураЗаказаПоКомплектующимИзделиямИЗИП.Ссылка В (&Ссылка)";
		
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ 
	|	Источник.Ссылка КАК Ссылка
	|Поместить ВТ_Архив
	|Из
	|	Справочник.СтруктураЗаказаПоКомплектующимИзделиямИЗИП КАК Источник
	|ГДЕ
	|	Источник.Наименование = ""Архив""
	|	И Источник.Владелец = &Корабль 
	|;
	|
	|
	|ВЫБРАТЬ
	|	СтруктураЗаказаПоКомплектующимИзделиямИЗИП.Ссылка КАК Ссылка,
	|	СтруктураЗаказаПоКомплектующимИзделиямИЗИП.Представление КАК Представление,
	|	СтруктураЗаказаПоКомплектующимИзделиямИЗИП.ПредметСнабжения КАК ПредметСнабжения
	|ИЗ
	|	Справочник.СтруктураЗаказаПоКомплектующимИзделиямИЗИП КАК СтруктураЗаказаПоКомплектующимИзделиямИЗИП
	|ГДЕ
	|	" + Условие + "
	|	И НЕ СтруктураЗаказаПоКомплектующимИзделиямИЗИП.Ссылка В ИЕРАРХИИ (ВЫБРАТЬ ВТ_Архив.Ссылка Из ВТ_Архив)	
	|ИТОГИ ПО
	|	Ссылка ИЕРАРХИЯ
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("Корабль", 	Корабль);
	Запрос.УстановитьПараметр("Ссылка", 	МассивЭлементовПоОтбору);
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса;
	
КонецФункции

Функция ПолучитьТаблицуДанных(МассивПС, МассивЭлементов) Экспорт
	Запрос = Новый Запрос;
	
	#Область ТекстЗапроса
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СпецификацииПС.ПредметСнабжения КАК ПредметСнабжения,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СпецификацииПС.СоставляющаяЧасть.Код) КАК Количество
	|ПОМЕСТИТЬ ВТСпецификации
	|ИЗ
	|	РегистрСведений.СпецификацииПС КАК СпецификацииПС
	|ГДЕ
	|	СпецификацииПС.ПредметСнабжения В(&МассивПС)
	|
	|СГРУППИРОВАТЬ ПО
	|	СпецификацииПС.ПредметСнабжения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПредметСнабжения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДополнительныеНаименования.Владелец КАК Владелец,
	|	ДополнительныеНаименования.Перевод КАК Перевод
	|ПОМЕСТИТЬ ВТПереводыПредметыСнабжения
	|ИЗ
	|	РегистрСведений.ДополнительныеНаименования КАК ДополнительныеНаименования
	|ГДЕ
	|	ДополнительныеНаименования.Владелец В(&МассивПС)
	|	И ДополнительныеНаименования.ИмяРеквизита = ""Наименование"" 
	|	И ДополнительныеНаименования.Язык = &ЯзыкОтбор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДополнительныеНаименования.Владелец КАК Владелец,
	|	ДополнительныеНаименования.Перевод КАК Перевод
	|ПОМЕСТИТЬ ВТПереводыЭлементыСтруктуры
	|ИЗ
	|	РегистрСведений.ДополнительныеНаименования КАК ДополнительныеНаименования
	|ГДЕ
	|	ДополнительныеНаименования.Владелец В(&МассивЭлементов)
	|	И ДополнительныеНаименования.ИмяРеквизита = ""Наименование"" 
	|	И ДополнительныеНаименования.Язык = &ЯзыкОтбор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТПереводыПредметыСнабжения.Владелец КАК Владелец,
	|	ВТПереводыПредметыСнабжения.Перевод КАК Перевод
	|ПОМЕСТИТЬ ВТПереводы
	|ИЗ
	|	ВТПереводыПредметыСнабжения КАК ВТПереводыПредметыСнабжения
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТПереводыЭлементыСтруктуры.Владелец,
	|	ВТПереводыЭлементыСтруктуры.Перевод
	|ИЗ
	|	ВТПереводыЭлементыСтруктуры КАК ВТПереводыЭлементыСтруктуры
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Владелец
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КодыINCAT.ПредметСнабжения КАК ПредметСнабжения,
	|	КодыINCAT.КодINCAT КАК КодINCAT
	|ПОМЕСТИТЬ ВТКодыINCAT
	|ИЗ
	|	РегистрСведений.КодыINCAT КАК КодыINCAT
	|ГДЕ
	|	КодыINCAT.ПредметСнабжения В(&МассивПС)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПредметСнабжения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтруктураЗаказаПоКомплектующимИзделиямИЗИП.Ссылка КАК ЭлементСтруктуры,
	|	СтруктураЗаказаПоКомплектующимИзделиямИЗИП.Представление КАК Представление,
	|	ВЫБОР
	|		КОГДА ВТСпецификации.Количество ЕСТЬ NULL
	|				И НЕ СтруктураЗаказаПоКомплектующимИзделиямИЗИП.Тип = ЗНАЧЕНИЕ(Справочник.ТипыУзловЭлектроннойСтруктурыКомплектующихИзделийИЗИПКорабля.Группа)
	|			ТОГДА ""ЗИП""
	|		ИНАЧЕ СтруктураЗаказаПоКомплектующимИзделиямИЗИП.Тип
	|	КОНЕЦ КАК Тип,
	|	ВТПереводы.Перевод КАК ИмяНаАнгл,
	|	СтруктураЗаказаПоКомплектующимИзделиямИЗИП.ПредметСнабжения.Обозначение КАК Обозначение,
	|	СтруктураЗаказаПоКомплектующимИзделиямИЗИП.ПредметСнабжения.ОбозначениеТранслитированное КАК ОбозначениеТранслитированное,
	|	СтруктураЗаказаПоКомплектующимИзделиямИЗИП.ПредметСнабжения.NSN КАК NSN,
	|	ВТКодыINCAT.КодINCAT КАК КодINCAT
	|ИЗ
	|	Справочник.СтруктураЗаказаПоКомплектующимИзделиямИЗИП КАК СтруктураЗаказаПоКомплектующимИзделиямИЗИП
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСпецификации КАК ВТСпецификации
	|		ПО СтруктураЗаказаПоКомплектующимИзделиямИЗИП.ПредметСнабжения = ВТСпецификации.ПредметСнабжения
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПереводы КАК ВТПереводы
	|		ПО (СтруктураЗаказаПоКомплектующимИзделиямИЗИП.Ссылка = ВТПереводы.Владелец
	|				ИЛИ СтруктураЗаказаПоКомплектующимИзделиямИЗИП.ПредметСнабжения = ВТПереводы.Владелец)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКодыINCAT КАК ВТКодыINCAT
	|		ПО СтруктураЗаказаПоКомплектующимИзделиямИЗИП.ПредметСнабжения = ВТКодыINCAT.ПредметСнабжения
	|ГДЕ
	|	СтруктураЗаказаПоКомплектующимИзделиямИЗИП.Ссылка В(&МассивЭлементов)";
	#КонецОбласти
	
	Запрос.УстановитьПараметр("МассивПС", 			МассивПС);
	Запрос.УстановитьПараметр("МассивЭлементов", 	МассивЭлементов);
	Запрос.УстановитьПараметр("ЯзыкОтбор", 			Справочники.Языки.Английский);
	
	
	тзРезультат = Запрос.Выполнить().Выгрузить();
	тзРезультат.Индексы.Добавить("ЭлементСтруктуры");
	
	Возврат тзРезультат;
	
КонецФункции // ПолучитьТаблицуДанных

Функция ПолучитьТабИзготовителейИПоставщиков(МассивЭлементов, СписокПоставщиков, СписокРазработчиков, СписокИзготовителей) Экспорт

	Ответ = Новый ТаблицаЗначений;
	Ответ.Колонки.Добавить("ПредметСнабжения");
	Ответ.Колонки.Добавить("Поставщики");
	Ответ.Колонки.Добавить("Разработчики");
	Ответ.Колонки.Добавить("Изготовители");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	#Область ТекстЗапрос
		"ВЫБРАТЬ
		|	СтруктураЗаказаПоКомплектующимИзделиямИЗИП.ПредметСнабжения КАК ПредметСнабжения
		|ПОМЕСТИТЬ ВТ_ЭлементыСтруктуры
		|ИЗ
		|	Справочник.СтруктураЗаказаПоКомплектующимИзделиямИЗИП КАК СтруктураЗаказаПоКомплектующимИзделиямИЗИП
		|ГДЕ
		|	СтруктураЗаказаПоКомплектующимИзделиямИЗИП.Ссылка В(&МассивЭлементов)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КаталогПредметовСнабженияИзготовителиИПоставщики.Ссылка КАК Ссылка,
		|	ВЫБОР
		|		КОГДА КаталогПредметовСнабженияИзготовителиИПоставщики.Поставщик
		|			ТОГДА ПРЕДСТАВЛЕНИЕ(КаталогПредметовСнабженияИзготовителиИПоставщики.Контрагент)
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК Поставщик,
		|	ВЫБОР
		|		КОГДА КаталогПредметовСнабженияИзготовителиИПоставщики.Разработчик
		|			ТОГДА ПРЕДСТАВЛЕНИЕ(КаталогПредметовСнабженияИзготовителиИПоставщики.Контрагент)
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК Разработчик,
		|	ВЫБОР
		|		КОГДА КаталогПредметовСнабженияИзготовителиИПоставщики.Изготовитель
		|			ТОГДА ПРЕДСТАВЛЕНИЕ(КаталогПредметовСнабженияИзготовителиИПоставщики.Контрагент)
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК Изготовитель
		|ИЗ
		|	Справочник.КаталогПредметовСнабжения.ИзготовителиИПоставщики КАК КаталогПредметовСнабженияИзготовителиИПоставщики
		|ГДЕ
		|	КаталогПредметовСнабженияИзготовителиИПоставщики.Ссылка В
		|			(ВЫБРАТЬ
		|				ВТ_ЭлементыСтруктуры.ПредметСнабжения
		|			ИЗ
		|				ВТ_ЭлементыСтруктуры)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка";
	#КонецОбласти	
	Запрос.УстановитьПараметр("МассивЭлементов", МассивЭлементов);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	ПредыдущийПС 		= Справочники.КаталогПредметовСнабжения.ПустаяСсылка();
	СтрокаПоставщик 	= "";
	СтрокаРазработчик 	= "";
	СтрокаИзготовитель 	= "";
	
	СписокПоставщиковСтрокой 	= ПолучитьСписокСтрокой(СписокПоставщиков);
	СписокРазработчиковСтрокой 	= ПолучитьСписокСтрокой(СписокРазработчиков);
	СписокИзготовителейСтрокой	= ПолучитьСписокСтрокой(СписокИзготовителей);
	ПроверятьПоставщиков		= ЗначениеЗаполнено(СписокПоставщиковСтрокой);
	ПроверятьРазработчиков		= ЗначениеЗаполнено(СписокРазработчиковСтрокой);
	ПроверятьИзготовителей		= ЗначениеЗаполнено(СписокИзготовителейСтрокой);
	
	Пока Выборка.Следующий() Цикл
		ТекПС = Выборка.Ссылка;
		
		Если ТекПС = ПредыдущийПС Тогда
			ДополнитьСтрокуПоиставщикаИзготовителя(Выборка.Поставщик, 	 СтрокаПоставщик,	СписокПоставщиковСтрокой,	ПроверятьПоставщиков);  
			ДополнитьСтрокуПоиставщикаИзготовителя(Выборка.Разработчик,  СтрокаРазработчик,	СписокРазработчиковСтрокой,	ПроверятьРазработчиков);  
			ДополнитьСтрокуПоиставщикаИзготовителя(Выборка.Изготовитель, СтрокаИзготовитель,СписокИзготовителейСтрокой,	ПроверятьИзготовителей);  
			
		Иначе
			нСтрока = Ответ.Добавить();
			нСтрока.ПредметСнабжения 	= ПредыдущийПС;
			нСтрока.Поставщики 			= СтрокаПоставщик;
			нСтрока.Разработчики 		= СтрокаРазработчик;
			нСтрока.Изготовители 		= СтрокаИзготовитель;
						
			СтрокаПоставщик		= ПолучитьЗначениеЭлементаПоПараметрам(ПроверятьПоставщиков, 	Выборка.Поставщик, 		СписокПоставщиковСтрокой); 
			СтрокаРазработчик	= ПолучитьЗначениеЭлементаПоПараметрам(ПроверятьРазработчиков, 	Выборка.Разработчик,	СписокРазработчиков); 
			СтрокаИзготовитель	= ПолучитьЗначениеЭлементаПоПараметрам(ПроверятьИзготовителей, 	Выборка.Изготовитель, 	СписокИзготовителей); 
			
			ПредыдущийПС		= ТекПС;
		КонецЕсли;
	КонецЦикла;
	
	нСтрока = Ответ.Добавить();
	нСтрока.ПредметСнабжения 	= ПредыдущийПС;
	нСтрока.Поставщики 			= СтрокаПоставщик;
	нСтрока.Разработчики 		= СтрокаРазработчик;
	нСтрока.Изготовители 		= СтрокаИзготовитель;
	
	Ответ.Удалить(Ответ[0]);	//пустая строка
	
	Ответ.Индексы.Добавить("ПредметСнабжения");
	
	Возврат Ответ;
	
КонецФункции	//ПолучитьТабИзготовителейИПоставщиков

///////////////////////////////////////
//ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

#Область ТекстЗапроса_ПолучитьЭлементыПоОтборам
Функция ПолучитьНеизменнуюЧастьЗапроса()
	
	ТекстЗапроса = 	"
	|ВЫБРАТЬ
	|	СтруктураЗаказаПоКомплектующимИзделиямИЗИП.Ссылка КАК Элемент,
	|	СтруктураЗаказаПоКомплектующимИзделиямИЗИП.ПредметСнабжения КАК ПредметСнабжения
	|ПОМЕСТИТЬ ВТ_СтруктураКорабля
	|ИЗ
	|	Справочник.СтруктураЗаказаПоКомплектующимИзделиямИЗИП КАК СтруктураЗаказаПоКомплектующимИзделиямИЗИП
	|ГДЕ
	|	СтруктураЗаказаПоКомплектующимИзделиямИЗИП.Владелец = &Корабль
	|	И НЕ СтруктураЗаказаПоКомплектующимИзделиямИЗИП.ПометкаУдаления
	|	И НЕ СтруктураЗаказаПоКомплектующимИзделиямИЗИП.Наименование = ""Архив""
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПредметСнабжения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_СтруктураКорабля.ПредметСнабжения КАК ПредметСнабжения,
	|	ВЫБОР
	|		КОГДА КаталогПредметовСнабженияИзготовителиИПоставщики.Поставщик
	|			ТОГДА КаталогПредметовСнабженияИзготовителиИПоставщики.Контрагент
	|		ИНАЧЕ ЗНАЧЕНИЕ(СПРАВОЧНИК.Организации.ПустаяСсылка)
	|	КОНЕЦ КАК Поставщики,
	|	ВЫБОР
	|		КОГДА КаталогПредметовСнабженияИзготовителиИПоставщики.Разработчик
	|			ТОГДА КаталогПредметовСнабженияИзготовителиИПоставщики.Контрагент
	|		ИНАЧЕ ЗНАЧЕНИЕ(СПРАВОЧНИК.Организации.ПустаяСсылка)
	|	КОНЕЦ КАК Разработчики,
	|	ВЫБОР
	|		КОГДА КаталогПредметовСнабженияИзготовителиИПоставщики.Изготовитель
	|			ТОГДА КаталогПредметовСнабженияИзготовителиИПоставщики.Контрагент
	|		ИНАЧЕ ЗНАЧЕНИЕ(СПРАВОЧНИК.Организации.ПустаяСсылка)
	|	КОНЕЦ КАК Изготовители
	|ПОМЕСТИТЬ ВТ_ИзготовителиИПоставщики
	|ИЗ
	|	ВТ_СтруктураКорабля КАК ВТ_СтруктураКорабля
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КаталогПредметовСнабжения.ИзготовителиИПоставщики КАК КаталогПредметовСнабженияИзготовителиИПоставщики
	|		ПО ВТ_СтруктураКорабля.ПредметСнабжения = КаталогПредметовСнабженияИзготовителиИПоставщики.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПредметСнабжения
	|;
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции	//ПолучитьНеизменнуюЧастьЗапроса

Процедура ДобавитьТаблицуСпецификацийВЗапрос(ТекстЗапроса)
	
	ТекстЗапроса = ТекстЗапроса + "
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СпецификацииПС.ПредметСнабжения КАК ПредметСнабжения,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СпецификацииПС.СоставляющаяЧасть) КАК КоличествоСостЧастей
	|ПОМЕСТИТЬ ВТНаличиеСпецификации
	|ИЗ
	|	РегистрСведений.СпецификацииПС КАК СпецификацииПС
	|ГДЕ
	|	СпецификацииПС.ПредметСнабжения В
	|		(ВЫБРАТЬ
	|			ВТ_СтруктураКорабля.ПредметСнабжения
	|		ИЗ
	|			ВТ_СтруктураКорабля КАК ВТ_СтруктураКорабля)
	|
	|СГРУППИРОВАТЬ ПО
	|	СпецификацииПС.ПредметСнабжения
	|;
	|";
	
КонецПроцедуры	//ДобавитьТаблицуСпецификацийВЗапрос

Функция ПолучитьЧастьЗапросаВывод()
	
	ЧастьВывод = "
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_СтруктураКорабля.Элемент КАК Элемент
	|ИЗ
	|	ВТ_СтруктураКорабля КАК ВТ_СтруктураКорабля
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИзготовителиИПоставщики КАК ВТ_Поставщики
	|		ПО ВТ_СтруктураКорабля.ПредметСнабжения = ВТ_Поставщики.ПредметСнабжения
	|		И (ВТ_Поставщики.Поставщики В (&Поставщики))
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИзготовителиИПоставщики КАК ВТ_Разработчики
	|		ПО ВТ_СтруктураКорабля.ПредметСнабжения = ВТ_Разработчики.ПредметСнабжения
	|		И (ВТ_Разработчики.Разработчики В (&Разработчики))
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИзготовителиИПоставщики КАК ВТ_Изготовители
	|		ПО ВТ_СтруктураКорабля.ПредметСнабжения = ВТ_Изготовители.ПредметСнабжения
	|		И (ВТ_Изготовители.Изготовители В (&Изготовители))
	|";
	
	Возврат ЧастьВывод;
	
КонецФункции	//ПолучитьЧастьЗапросаВывод

Процедура ДобавитьСвязьСТаблицейСпецификаций(ЧастьВывод)
	
	ЧастьВывод = ЧастьВывод + "
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТНаличиеСпецификации КАК ВТНаличиеСпецификации
	|		ПО ВТ_СтруктураКорабля.ПредметСнабжения = ВТНаличиеСпецификации.ПредметСнабжения";
	
КонецПроцедуры	//ДобавитьСвязьСТаблицейСпецификаций

Процедура ДобавитьУсловияЗапросаДляВывода(СписокПоставщиков, СписокРазработчиков, СписокИзготовителей,ТолькоИзделия,  ЧастьВывод)
	
	ЧастьВывод = ЧастьВывод + "
	|
	|ГДЕ
	|	&УсловиеПоставщики
	|	И &УсловиеРазработчики
	|	И &УсловиеИзготовители
	|	И &УсловиеТолькоИзделия";
	
	Если СписокПоставщиков.Количество() > 0 Тогда
		ЧастьВывод = СтрЗаменить(ЧастьВывод, "&УсловиеПоставщики","НЕ ВТ_Поставщики.Поставщики ЕСТЬ NULL");	
	Иначе
		ЧастьВывод = СтрЗаменить(ЧастьВывод, "&УсловиеПоставщики","ИСТИНА");	
	КонецЕсли;
	
	Если СписокРазработчиков.Количество() > 0 Тогда
		ЧастьВывод = СтрЗаменить(ЧастьВывод, "&УсловиеРазработчики","НЕ ВТ_Разработчики.Разработчики ЕСТЬ NULL");		
	Иначе
		ЧастьВывод = СтрЗаменить(ЧастьВывод, "&УсловиеРазработчики","ИСТИНА");		
	КонецЕсли;
	
	Если СписокИзготовителей.Количество() > 0 Тогда
		ЧастьВывод = СтрЗаменить(ЧастьВывод, "&УсловиеИзготовители","НЕ ВТ_Изготовители.Изготовители ЕСТЬ NULL");		
	Иначе
		ЧастьВывод = СтрЗаменить(ЧастьВывод, "&УсловиеИзготовители","ИСТИНА");		
	КонецЕсли;
	Если ТолькоИзделия Тогда
		ЧастьВывод = СтрЗаменить(ЧастьВывод, "&УсловиеТолькоИзделия","ВТНаличиеСпецификации.КоличествоСостЧастей > 0");			
	Иначе
		ЧастьВывод = СтрЗаменить(ЧастьВывод, "&УсловиеТолькоИзделия","ИСТИНА");			
	КонецЕсли;

КонецПроцедуры // ДобавитьУсловияЗапросаДляВывода()
#КонецОбласти

#Область Сервисное
Процедура ДополнитьСтрокуПоиставщикаИзготовителя(текИсточник, стрИсточник, стрВхождения, ПроверятьВхождение = Ложь)
	
	Если ЗначениеЗаполнено(текИсточник) Тогда
		Если СтрНайти(стрИсточник, текИсточник) = 0 И (Не ПроверятьВхождение Или СтрНайти(стрВхождения, текИсточник) > 0 ) Тогда
			стрИсточник = стрИсточник + ?(ЗначениеЗаполнено(стрИсточник), ";" + Символы.ПС, "") + текИсточник;	
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры	//ДополнитьСтрокуПоиставщикаИзготовителя

Функция ПолучитьЗначениеЭлементаПоПараметрам(ПроверятьПоставщиков, ЭлементИсточник, СписокПоставщиковСтрокой)
	
	Ответ = "";	
	Если Не ПроверятьПоставщиков Тогда
		Ответ = ЭлементИсточник;
	ИначеЕсли СтрНайти(СписокПоставщиковСтрокой, ЭлементИсточник) > 0 Тогда 
		Ответ = ЭлементИсточник;
	КонецЕсли;
	Возврат Ответ;
	
КонецФункции	//ПолучитьЗначениеЭлементаПоПараметрам

Функция ПолучитьСписокСтрокой(СписокИсточник)
	
	Ответ = "";
	Для Каждого эСписка Из СписокИсточник Цикл
		Ответ = Ответ + эСписка.Значение.Наименование;
	КонецЦикла;
	
	Возврат Ответ;
	
КонецФункции	//ПолучитьСписокСтрокой
#КонецОбласти