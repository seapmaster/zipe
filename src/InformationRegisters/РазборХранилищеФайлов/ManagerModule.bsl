////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура ДобавитьЗапись(Объект) Экспорт
	
	Запись = РегистрыСведений.РазборХранилищеФайлов.СоздатьМенеджерЗаписи();
	Запись.Объект 					= Объект;
	Запись.Автор					= ПараметрыСеанса.ТекущийПользователь;
	Запись.ДатаДобавленияВОчередь 	= ТекущаяДатаСеанса();
	Запись.Записать();
	
КонецПроцедуры // ДобавитьЗапись

Процедура УдалитьЗапись(Объект) Экспорт
	
	Запись 				= РегистрыСведений.РазборХранилищеФайлов.СоздатьМенеджерЗаписи();
	Запись.Объект 		= Объект;
	Запись.Прочитать();
	
	Если Запись.Выбран() Тогда
		Запись.Удалить();
	КонецЕсли; // Если Запись.Выбран() Тогда
	
КонецПроцедуры // УдалитьЗапись

Процедура УвеличитьСчетчик(Объект, ОписаниеОшибки) Экспорт
	
	Запись 				= РегистрыСведений.РазборХранилищеФайлов.СоздатьМенеджерЗаписи();
	Запись.Объект 		= Объект;
	Запись.Прочитать();
	
	Если Запись.Выбран() Тогда
		Запись.КоличествоПопыток = Запись.КоличествоПопыток + 1;	
	Иначе
		Запись.КоличествоПопыток = 1;
	КонецЕсли; // Если Запись.Выбран() Тогда
	
	Запись.ОписаниеОшибки 		= ОписаниеОшибки;
	Запись.ДатаПоследнейПопытки = ТекущаяДатаСеанса();
	Запись.Записать();
	
КонецПроцедуры // УвеличитьСчетчик

Процедура ОбнулитьСчетчикиПопыток() Экспорт
	
	Запрос 			= Новый Запрос;
	Запрос.Текст 	= "ВЫБРАТЬ
	             	  |	РазборХранилищеФайлов.Объект КАК Объект
	             	  |ИЗ
	             	  |	РегистрСведений.РазборХранилищеФайлов КАК РазборХранилищеФайлов
	             	  |ГДЕ
	             	  |	РазборХранилищеФайлов.КоличествоПопыток > 0";
	РезультатЗапроса 	= Запрос.Выполнить();
	ВыборкаЗапроса		= РезультатЗапроса.Выбрать();
	
	Пока ВыборкаЗапроса.Следующий() Цикл
		
		Запись 				= РегистрыСведений.РазборХранилищеФайлов.СоздатьМенеджерЗаписи();
		Запись.Объект 		= ВыборкаЗапроса.Объект;
		Запись.Прочитать();
		
		Если Запись.Выбран() Тогда
			Запись.КоличествоПопыток = 0;	
			Запись.Записать();
		КонецЕсли; // Если Запись.Выбран() Тогда
		
	КонецЦикла; // Пока ВыборкаЗапроса.Следующий() Цикл
		
КонецПроцедуры // ОбнулитьСчетчикиПопыток
