
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("Дата", Дата);
	Параметры.Свойство("Экземпляр", Экземпляр);
	
	ЗаполнитьНаработку();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаработку()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РесурсыИСрокиСлужбыПредметовСнабжения.ПараметрУчета КАК ПараметрУчета,
	|	МАКСИМУМ(РесурсыИСрокиСлужбыПредметовСнабжения.ЕдиницаИзмерения) КАК ЕдиницаИзмерения,
	|	&Экземпляр КАК Экземпляр,
	|	&Период КАК Период
	|ПОМЕСТИТЬ втНаработка
	|ИЗ
	|	РегистрСведений.РесурсыИСрокиСлужбыПредметовСнабжения КАК РесурсыИСрокиСлужбыПредметовСнабжения
	|ГДЕ
	|	РесурсыИСрокиСлужбыПредметовСнабжения.ПредметСнабжения = &ПредметСнабжения
	|
	|СГРУППИРОВАТЬ ПО
	|	РесурсыИСрокиСлужбыПредметовСнабжения.ПараметрУчета
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПараметрУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втНаработка.ПараметрУчета КАК ПараметрУчета,
	|	втНаработка.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	втНаработка.Экземпляр КАК Экземпляр,
	|	втНаработка.Период КАК Период,
	|	МАКСИМУМ(ЕСТЬNULL(НаработкаЭкземляровКомплектующихИзделийКорабляСрезПоследних.ЗначениеСНачалаЭксплуатации, 0)) КАК ТекущееЗначение
	|ИЗ
	|	втНаработка КАК втНаработка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НаработкаЭкземляровКомплектующихИзделийКорабля.СрезПоследних(&Период, Экземпляр = &Экземпляр) КАК НаработкаЭкземляровКомплектующихИзделийКорабляСрезПоследних
	|		ПО втНаработка.ПараметрУчета = НаработкаЭкземляровКомплектующихИзделийКорабляСрезПоследних.ПараметрУчета
	|
	|СГРУППИРОВАТЬ ПО
	|	втНаработка.ПараметрУчета,
	|	втНаработка.ЕдиницаИзмерения,
	|	втНаработка.Экземпляр,
	|	втНаработка.Период";
	Запрос.УстановитьПараметр("Экземпляр", Экземпляр);
	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("ПредметСнабжения", Экземпляр.Владелец.ПредметСнабжения);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		
		Сообщить("Данное изделие не имеет параметров учета!");
		Возврат;
		
	КонецЕсли;
	
	Наработка.Загрузить(РезультатЗапроса.Выгрузить());
		
КонецПроцедуры

&НаКлиенте
Процедура НаработкаЗначениеСНачалаЭксплуатацииПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Наработка.ТекущиеДанные;
	
	ТекущиеДанные.НачалоЦикла = (ТекущиеДанные.ЗначениеСНачалаЭксплуатации > 0);
	
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда)
	
	ПоказатьВопрос(Новый ОписаниеОповещения("ЗаписатьПродолжение", ЭтаФорма), "В базу данных будут внесены изменения. Продолжить?", РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьПродолжение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		ЗаписатьНаработку();
		Закрыть();
		
	Иначе
		
		ПоказатьПредупреждение(, "Действие отменено");
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаписатьНаработку()
	
	НаработкаМенеджер = РегистрыСведений.НаработкаЭкземляровКомплектующихИзделийКорабля;	
	
	Для каждого СтрокаНаработка Из Наработка Цикл
		
		Если СтрокаНаработка.НачалоЦикла Тогда
			
			НаработкаМенеджерЗаписи = НаработкаМенеджер.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(НаработкаМенеджерЗаписи, СтрокаНаработка);
			НаработкаМенеджерЗаписи.Записать();
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры
