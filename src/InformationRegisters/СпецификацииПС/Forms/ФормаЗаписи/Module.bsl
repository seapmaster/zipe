
////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПредметСнабжения = Запись.ПредметСнабжения;
	СоставляющаяЧасть = Запись.СоставляющаяЧасть;

	РезультатПроверки = СпецификацииПредметовСнабжения.ПроверитьВозможностьУдаленияСоставнойЧастиИзСпецификацииПредметаСнабжения(Запись.ПредметСнабжения, Запись.СоставляющаяЧасть); 
	Элементы.СоставляющаяЧасть.ТолькоПросмотр = РезультатПроверки.Отказ;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ПредметСнабжения") Тогда
		Запись.ПредметСнабжения = Параметры.ПредметСнабжения;
		Элементы.ПредметСнабжения.Видимость = НЕ ЗначениеЗаполнено(Запись.ПредметСнабжения);
	КонецЕсли;
		
	ВидимостьДоступность(ЭтаФорма);
	
КонецПроцедуры //ПриСозданииНаСервере

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Ошибки = Неопределено;

	Если НЕ ЗначениеЗаполнено(Запись.СоставляющаяЧасть) Тогда
		
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "СоставляющаяЧасть", НСтр("ru = 'Необходимо заполнить составляющую часть.'"), Неопределено);
				
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(Запись.Тип) Тогда
		
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "Тип", НСтр("ru = 'Необходимо заполнить группировку.'"), Неопределено);
		
	КонецЕсли;
			
	Если НЕ ЗначениеЗаполнено(Запись.Количество) Тогда
		
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "Количество", НСтр("ru = 'Необходимо заполнить количество.'"), Неопределено);
		
	КонецЕсли;
	
	Если ЗаписьСуществует() Тогда
		
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "СоставляющаяЧасть", НСтр("ru = 'Запись уже существует.'"), Неопределено);
		
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки, Отказ); 
	
	Если НЕ Отказ Тогда
		
		ПараметрыЗаписи.Вставить("ДобавитьПредметСнабженияВОчередьИзменений");	
		
	КонецЕсли;
		
КонецПроцедуры //ПередЗаписьюНаСервере

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПредметСнабжения = Запись.ПредметСнабжения;
	СоставляющаяЧасть = Запись.СоставляющаяЧасть;
	
КонецПроцедуры //ПослеЗаписи

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	//Вставить содержимое обработчика
КонецПроцедуры //ПриЗаписиНаСервере

&НаКлиенте
Процедура ЗИППриИзменении(Элемент)
	
	 ВидимостьДоступность(ЭтаФорма);

КонецПроцедуры //ЗИППриИзменении

////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиентеНаСервереБезКонтекста
Процедура ВидимостьДоступность(Форма)
	
	ОтображатьКоличествоЗИП = Форма.Запись.ЗИП;
	
	Форма.Элементы.КоличествоВЗИПБаза.Видимость = ОтображатьКоличествоЗИП;
	Форма.Элементы.КоличествоВЗИПБорт.Видимость = ОтображатьКоличествоЗИП;
	Форма.Элементы.КоличествоВЗИПДЭ.Видимость = ОтображатьКоличествоЗИП;
	Форма.Элементы.КоличествоВЗИПР.Видимость = ОтображатьКоличествоЗИП;	

КонецПроцедуры //ВидимостьДоступность

&НаСервере
Функция ЗаписьСуществует()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СпецификацииПС.ПредметСнабжения КАК ПредметСнабжения
	|ИЗ
	|	РегистрСведений.СпецификацииПС КАК СпецификацииПС
	|ГДЕ
	|	СпецификацииПС.ПредметСнабжения = &ПредметСнабжения
	|	И СпецификацииПС.СоставляющаяЧасть = &СоставляющаяЧасть
	|";
	
	Запрос.УстановитьПараметр("ПредметСнабжения", Запись.ПредметСнабжения);
	Запрос.УстановитьПараметр("СоставляющаяЧасть", Запись.СоставляющаяЧасть);
	
	Возврат (НЕ Запрос.Выполнить().Пустой()) И ((ПредметСнабжения <> Запись.ПредметСнабжения) ИЛИ (СоставляющаяЧасть <> Запись.СоставляющаяЧасть));
	
КонецФункции  //ЗаписьСуществует





