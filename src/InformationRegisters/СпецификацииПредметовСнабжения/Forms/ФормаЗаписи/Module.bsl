
////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ПредметСнабжения") Тогда
		Запись.ПредметСнабжения = Параметры.ПредметСнабжения;
	КонецЕсли;
	
	Элементы.ПредметСнабжения.Видимость = НЕ ЗначениеЗаполнено(Запись.ПредметСнабжения);
	
	Если НЕ Запись.ИсходныйКлючЗаписи.Пустой() Тогда
		ПредметСнабжения = Запись.ПредметСнабжения;
		СоставляющаяЧасть = Запись.СоставляющаяЧасть;
		ЗИП = Запись.ЗИП;
	КонецЕсли;
	
КонецПроцедуры //ПриСозданииНаСервере

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Ошибки = Неопределено;

	Если НЕ ЗначениеЗаполнено(Запись.СоставляющаяЧасть) Тогда
		
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "СоставляющаяЧасть", НСтр("ru = 'Необходимо заполнить составляющую часть.'"), Неопределено);
				
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(Запись.Тип) Тогда
		
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "Тип", НСтр("ru = 'Необходимо заполнить группировку.'"), Неопределено);
		
	КонецЕсли;
			
	Если НЕ ЗначениеЗаполнено(Запись.Количество) Тогда
		
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "Количество", НСтр("ru = 'Необходимо заполнить количество.'"), Неопределено);
		
	КонецЕсли;
	
	Если ЗаписьСуществует() Тогда
		
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "Количество", НСтр("ru = 'Запись уже существует.'"), Неопределено);
		
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки, Отказ); 
	
	Если НЕ Отказ Тогда
		
		ПараметрыЗаписи.Вставить("ДобавитьПредметСнабженияВОчередьИзменений");	
		
	КонецЕсли;
		
КонецПроцедуры //ПередЗаписьюНаСервере

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПредметСнабжения = Запись.ПредметСнабжения;
	СоставляющаяЧасть = Запись.СоставляющаяЧасть;
	ЗИП = Запись.ЗИП;
	
КонецПроцедуры //ПослеЗаписи

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	//Вставить содержимое обработчика
КонецПроцедуры //ПриЗаписиНаСервере

&НаКлиенте
Процедура ЗИППриИзменении(Элемент)
	
	//если установлен флаг ЗИП, но предмет снабжения имеет спецификацию - запрашиваем подтверждение действия

	Если Запись.ЗИП И ИмеетсяСпецификация(Запись.СоставляющаяЧасть) И НЕ Запись.ИсходныйКлючЗаписи.Пустой() Тогда
		
		ПоказатьВопрос(Новый ОписаниеОповещения("ЗИППриИзмененииДалее", ЭтаФорма), 
			НСтр("ru = 'Спецификация элемента будет удалена из структуры заказа (включая данные о количестве). Продолжить?'"), РежимДиалогаВопрос.ДаНет);
		
	КонецЕсли;

КонецПроцедуры //ЗИППриИзменении

////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервереБезКонтекста
Функция ИмеетсяСпецификация(ПредметСнабжения)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СпецификацииПредметовСнабжения.СоставляющаяЧасть КАК СоставляющаяЧасть
	|ИЗ
	|	РегистрСведений.СпецификацииПредметовСнабжения КАК СпецификацииПредметовСнабжения
	|ГДЕ
	|	СпецификацииПредметовСнабжения.ПредметСнабжения = &ПредметСнабжения";
	Запрос.УстановитьПараметр("ПредметСнабжения", ПредметСнабжения);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат Не РезультатЗапроса.Пустой();
	
КонецФункции  //ИмеетсяСпецификация

&НаКлиенте
Процедура ЗИППриИзмененииДалее(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		
		Запись.ЗИП = Ложь;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Действие отменено'"));
		
	КонецЕсли;	
	
КонецПроцедуры //ЗИППриИзмененииДалее

&НаСервере
Функция ЗаписьСуществует()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СпецификацииПредметовСнабжения.ПредметСнабжения КАК ПредметСнабжения
	|ИЗ
	|	РегистрСведений.СпецификацииПредметовСнабжения КАК СпецификацииПредметовСнабжения
	|ГДЕ
	|	СпецификацииПредметовСнабжения.ПредметСнабжения = &ПредметСнабжения
	|	И СпецификацииПредметовСнабжения.СоставляющаяЧасть = &СоставляющаяЧасть
	|	И СпецификацииПредметовСнабжения.ЗИП = &ЗИП";
	
	Запрос.УстановитьПараметр("ПредметСнабжения", Запись.ПредметСнабжения);
	Запрос.УстановитьПараметр("СоставляющаяЧасть", Запись.СоставляющаяЧасть);
	Запрос.УстановитьПараметр("ЗИП", Запись.ЗИП);
	
	Возврат (НЕ Запрос.Выполнить().Пустой()) И ((ПредметСнабжения <> Запись.ПредметСнабжения) ИЛИ (СоставляющаяЧасть <> Запись.СоставляющаяЧасть) ИЛИ (ЗИП <> Запись.ЗИП));
	
КонецФункции  //ЗаписьСуществует




