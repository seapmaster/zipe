#Область ОбменДаннымиСПоставщиками

Процедура ЗаполнитьПродолжение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		ЗаполнитьТабличныеЧасти(ДополнительныеПараметры);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьТабличныеЧасти(Параметры)
	
	Если Параметры.ВариантЗаполнения = 0 Тогда
		
		ЗаполнитьТабличныеЧастиПродолжение(0, Параметры);
		
	Иначе      
		
		Если Параметры.ВариантЗаполнения = 1 Тогда
			
			ФормаВыбора = "Справочник.КаталогПредметовСнабжения.Форма.ФормаСписка";
			
		ИначеЕсли Параметры.ВариантЗаполнения = 2 Тогда
			
			ФормаВыбора = "Документ.Заявка.Форма.ФормаСписка";
			
		ИначеЕсли Параметры.ВариантЗаполнения = 3 Тогда
			
			ФормаВыбора = "Документ.Контракт.Форма.ФормаСписка";
			
		ИначеЕсли Параметры.ВариантЗаполнения = 4 Тогда
			
			ФормаВыбора = "БизнесПроцесс.ЗапросНаАктуализациюКаталогаПоставщика.Форма.ФормаВыбораИзКаталога";
			
		Иначе
			
			Возврат;
			
		КонецЕсли;
			
			ОткрытьФорму(ФормаВыбора, Новый Структура("РежимВыбора, МножественныйВыбор, ПоставщикПараметр", Истина, Истина, Параметры.Поставщик), 
				ЭтотОбъект, , , , Новый ОписаниеОповещения("ЗаполнитьТабличныеЧастиПродолжение", ЭтотОбъект, Параметры));


	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьТабличныеЧастиПродолжение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	
	Форма = ДополнительныеПараметры.Форма;
	
	ДополнительныеПараметры.Удалить("Форма");
	
	Если РезультатЗакрытия = 0 Или ТипЗнч(РезультатЗакрытия) = Тип("Массив") Тогда
		
		Если РезультатЗакрытия = 0 Тогда
			
			РезультатЗакрытия = Неопределено;
			
		КонецЕсли;
		
		Параметры = КорректировкаДанныхСправочников.ЗаполнитьПредметыСнабженияИХарактеристики(РезультатЗакрытия, ДополнительныеПараметры);
		
		КопироватьДанныеФормы(Параметры.ПредметыСнабжения, Форма.Объект.ПредметыСнабжения);
		КопироватьДанныеФормы(Параметры.Характеристики, Форма.Объект.Характеристики);
		
		Если ДополнительныеПараметры.Свойство("Аналоги") Тогда
			
			Форма.Объект.Аналоги.Очистить();
			
		КонецЕсли;
		
	КонецЕсли; 
	
КонецПроцедуры

Функция ВыгрузитьФайлПоставщику(ПредметыСнабжения, Характеристики, ИмяФайла) Экспорт
	
	Если ПредметыСнабжения.Количество() = 0 Тогда
		
		ПоказатьПредупреждение(, "Нет данных для выгрузки!");
		Возврат Неопределено;
		
	КонецЕсли;
	
	//создание файла
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ДиалогВыбораФайла.ПолноеИмяФайла = ИмяФайла;
	ДиалогВыбораФайла.МножественныйВыбор = Ложь;
	ДиалогВыбораФайла.Заголовок = "Выберите файл для выгрузки:";
	ДиалогВыбораФайла.Фильтр = "Excel файлы(*.xls;*.xlsx)|*.xls;*.xlsx";
	Если ДиалогВыбораФайла.Выбрать() Тогда
		
	    ПутьКФайлу = ДиалогВыбораФайла.ПолноеИмяФайла; 
		
	Иначе
		
		Возврат Неопределено;
		
	КонецЕсли;
	
	Шаблон = КорректировкаДанныхСправочников.ПолучитьШаблонФайлаОбменаСПоставщиками();
	
	Попытка
		
		Шаблон.Записать(ПутьКФайлу);
		
		Excel = Новый COMОбъект("Excel.Application");
		Книга = Excel.WorkBooks.Open(ПутьКФайлу);
		
	Исключение
		
		Сообщить(ОписаниеОшибки());
		Возврат Неопределено;
		
	КонецПопытки;
	
	//выгрузка данных
	ЛистПредметовСнабжения = Книга.WorkSheets(1);
	Сч = 3;
	
	ДатаНачала = ТекущаяДата();
	ВсегоСтрок = ПредметыСнабжения.Количество();
	
	Для каждого СтрокаПредметСнабжения Из ПредметыСнабжения Цикл
		
		УправлениеИнтерфейсом.ВывестиТекущееСостояние("Запись изделий в файл...", ДатаНачала, Сч - 2, ВсегоСтрок);
		
		ЛистПредметовСнабжения.Cells(Сч, 1).Value = ?(ЗначениеЗаполнено(СтрокаПредметСнабжения.ПредметСнабжения),
			Строка(СтрокаПредметСнабжения.ПредметСнабжения.УникальныйИдентификатор()), "");
		ЛистПредметовСнабжения.Cells(Сч, 2).Value = Строка(СтрокаПредметСнабжения.Входимость) + " " + СтрокаПредметСнабжения.ВходимостьОбозначение;
		ЛистПредметовСнабжения.Cells(Сч, 3).Value = СтрокаПредметСнабжения.Наименование;
		ЛистПредметовСнабжения.Cells(Сч, 4).Value = СтрокаПредметСнабжения.Обозначение;
		ЛистПредметовСнабжения.Cells(Сч, 5).Value = СтрокаПредметСнабжения.ДокументНаПоставку;
		ЛистПредметовСнабжения.Cells(Сч, 7).Value = ?(СтрокаПредметСнабжения.СрокИзготовления > 0, СтрокаПредметСнабжения.СрокИзготовления, "");
		ЛистПредметовСнабжения.Cells(Сч, 10).Value = СтрокаПредметСнабжения.ПравилаУпаковкиТранспортировкиХранения;
		
		Сч = Сч + 1;
		
	КонецЦикла;
	
	ЛистХарактеристик = Книга.WorkSheets(2);
	Сч = 3;
	
	ДатаНачала = ТекущаяДата();
	ВсегоСтрок = Характеристики.Количество();
	
	Для каждого СтрокаХарактеристика Из Характеристики Цикл
		
		УправлениеИнтерфейсом.ВывестиТекущееСостояние("Запись характеристик в файл...", ДатаНачала, Сч - 2, ВсегоСтрок);
		
		ЛистХарактеристик.Cells(Сч, 1).Value = ?(ЗначениеЗаполнено(СтрокаХарактеристика.ПредметСнабжения),
			Строка(СтрокаХарактеристика.ПредметСнабжения.УникальныйИдентификатор()), "");
		ЛистХарактеристик.Cells(Сч, 2).Value = Строка(СтрокаХарактеристика.Характеристика);
		ЛистХарактеристик.Cells(Сч, 3).Value = Строка(СтрокаХарактеристика.ЕдиницаИзмерения);
		ЛистХарактеристик.Cells(Сч, 4).Value = СтрокаХарактеристика.ЕдиницаИзмеренияКод;
		ЛистХарактеристик.Cells(Сч, 5).Value = СтрокаХарактеристика.Значение;
		ЛистХарактеристик.Cells(Сч, 6).Value = ?(СтрокаХарактеристика.Основная, "Да", "");
		
		Сч = Сч + 1;
		
	КонецЦикла;
		
	Попытка
		
		Excel.Application.DisplayAlerts = False;
		Книга.SaveAs(ПутьКФайлу);
		Excel.Application.Quit();
		
	Исключение
		
	    ОписаниеОшибки = ОписаниеОшибки();
		Сообщить("Документ Excel не записан: " + ОписаниеОшибки);
		Возврат Неопределено;
		
	КонецПопытки;
	
	Excel = "";
	
	Возврат ПутьКФайлу;
	
КонецФункции

Функция ЗагрузитьФайлОтПоставщика(Форма, ТаблицыВОбъекте) Экспорт
	
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогОткрытияФайла.ПолноеИмяФайла = "";
	ДиалогОткрытияФайла.Фильтр = "Документ MS Excel 2010 (*.xlsx)|*.xlsx|Документ MS Excel(*.xls)|*.xls";
	ДиалогОткрытияФайла.МножественныйВыбор = Ложь;
	ДиалогОткрытияФайла.Заголовок = "Выберите файл поставщика:";
	
	Если ДиалогОткрытияФайла.Выбрать() Тогда
		
		РезультатЗагрузки = ВыполнитьЗагрузкуФайлаОтПоставщика(Форма, ТаблицыВОбъекте, ДиалогОткрытияФайла.ПолноеИмяФайла);
		Возврат РезультатЗагрузки;
		
	Иначе
		
		Возврат Неопределено;
		
	КонецЕсли;
	
КонецФункции

Функция ВыполнитьЗагрузкуФайлаОтПоставщика(Форма, ЭтоОбработка, ПутьКФайлу) Экспорт
	
	Если ЭтоОбработка Тогда
			
		Форма.Объект.ПредметыСнабжения.Очистить();
		Форма.Объект.Характеристики.Очистить();
		Форма.Объект.Аналоги.Очистить();
		
	Иначе
		
		Форма.ПредметыСнабжения.Очистить();
		Форма.Характеристики.Очистить();
		Форма.Аналоги.Очистить();
		
	КонецЕсли;
		
	//чтение данных из файла
	Попытка
	
		Excel = Новый COMОбъект("Excel.Application");
		Книга = Excel.WorkBooks.Open(ПутьКФайлу);
		
	Исключение
		
		Сообщить(ОписаниеОшибки());
		Возврат Неопределено;
		
	КонецПопытки;
	
	//предметы снабжения	
	ЛистПредметовСнабжения = Книга.WorkSheets(1);
	
	Если Не (ЛистПредметовСнабжения.Cells(1, 1).Value = "Идентификатор" И ЛистПредметовСнабжения.Cells(1, 2).Value = "Входимость") Тогда
		
		Сообщить("Некорректный формат файла!");
		Возврат Неопределено;
		
	КонецЕсли;
	
	ДатаНачала = ТекущаяДата();
	ВсегоСтрок = ЛистПредметовСнабжения.Cells.SpecialCells(11).Row;
	
	Для Сч = 3 По ВсегоСтрок Цикл
		
		УправлениеИнтерфейсом.ВывестиТекущееСостояние("Чтение предметов снабжения...", ДатаНачала, Сч - 2, ВсегоСтрок - 2);
		
		ГУИД = Формат(ЛистПредметовСнабжения.Cells(Сч, 1).Value, "ЧГ=0");
		Наименование = Формат(ЛистПредметовСнабжения.Cells(Сч, 3).Value, "ЧГ=0");
		
		Если Не ЗначениеЗаполнено(Наименование) Тогда
			
			Продолжить;
			
		КонецЕсли;
			
		ПредметСнабжения = КорректировкаДанныхСправочников.ПолучитьСсылкуПоГУИД(ГУИД, "КаталогПредметовСнабжения");
				
		СтрокаПредметСнабжения = ?(ЭтоОбработка, Форма.Объект.ПредметыСнабжения.Добавить(), Форма.ПредметыСнабжения.Добавить());
		СтрокаПредметСнабжения.ПредметСнабжения = ПредметСнабжения;
		СтрокаПредметСнабжения.Наименование = Наименование;
		СтрокаПредметСнабжения.Обозначение = Формат(ЛистПредметовСнабжения.Cells(Сч, 4).Value, "ЧГ=0");
		СтрокаПредметСнабжения.ДокументНаПоставку = Формат(ЛистПредметовСнабжения.Cells(Сч, 5).Value, "ЧГ=0");
		СтрокаПредметСнабжения.ВозможностьИзготовления = (ЛистПредметовСнабжения.Cells(Сч, 6).Value = "Да");
		СтрокаПредметСнабжения.СрокИзготовления = ПолучитьЧисло(ЛистПредметовСнабжения.Cells(Сч, 7).Value);
		СтрокаПредметСнабжения.Цена = ПолучитьЧисло(ЛистПредметовСнабжения.Cells(Сч, 8).Value);
		СтрокаПредметСнабжения.ДатаЦены = ПолучитьДату(ЛистПредметовСнабжения.Cells(Сч, 9).Value);
		СтрокаПредметСнабжения.ПравилаУпаковкиТранспортировкиХранения = ЛистПредметовСнабжения.Cells(Сч, 10).Value;
		СтрокаПредметСнабжения.Идентификатор = ?(ЗначениеЗаполнено(ГУИД), ГУИД, Новый УникальныйИдентификатор());
		СтрокаПредметСнабжения.Согласовано = Истина;
		СтрокаПредметСнабжения.ПорядковыйНомер = Сч - 2;
		
		ИмяФайлаКартинки = СокрЛП(ЛистПредметовСнабжения.Cells(Сч, 11).Value);
		Если НЕ ИмяФайлаКартинки = "" Тогда
			
			ПолныйФайл = ОбщегоНазначенияКлиентСервер.РазложитьПолноеИмяФайла(ПутьКФайлу);
			Каталог = ПолныйФайл.Путь;
			
			Попытка
				ПолныйПутьКартинки = Каталог + ИмяФайлаКартинки;
				ДвоичныеДанные = Новый ДвоичныеДанные(ПолныйПутьКартинки);
			Исключение
				ДвоичныеДанные = Неопределено;
			КонецПопытки;
			
			Если НЕ ДвоичныеДанные = Неопределено Тогда
				
				УИД = новый УникальныйИдентификатор;
				
				Адрес = ПоместитьВоВременноеХранилище(ДвоичныеДанные, УИД);
				
				Если НЕ Адрес = "" Тогда
					
					Если ЭтоОбработка Тогда
						СтрокаПредметСнабжения.Картинка = ПолучитьИзВременногоХранилища(Адрес);
					Иначе
						СтрокаПредметСнабжения.ХранилищеКартинкиВременное = Адрес;
					КонецЕсли; 
					
					СтрокаПредметСнабжения.ИмяФайлаКартинки = ИмяФайлаКартинки;
					
				КонецЕсли;
				
			КонецЕсли; 
			
		КонецЕсли;
		
	КонецЦикла;
	
	//характеристики
	ЛистХарактеристик = Книга.WorkSheets(2);
	
	ДатаНачала = ТекущаяДата();
	ВсегоСтрок = ЛистХарактеристик.Cells.SpecialCells(11).Row;
	
	Для Сч = 3 По ВсегоСтрок Цикл
		
		УправлениеИнтерфейсом.ВывестиТекущееСостояние("Чтение характеристик...", ДатаНачала, Сч - 2, ВсегоСтрок - 2);
		
		ГУИД = 	ЛистХарактеристик.Cells(Сч, 1).Value;
		Наименование = ЛистХарактеристик.Cells(Сч, 2).Value;
		
		Если Не ЗначениеЗаполнено(ГУИД) Или Не ЗначениеЗаполнено(Наименование) Тогда
			
			Продолжить;
			
		КонецЕсли;
			
		ПредметСнабжения = КорректировкаДанныхСправочников.ПолучитьСсылкуПоГУИД(ГУИД, "КаталогПредметовСнабжения");
		
		Характеристика = КорректировкаДанныхСправочников.ПолучитьХарактеристикуПоНаименованию(Наименование, ПредметСнабжения);
		
		СтрокаХарактеристика = ?(ЭтоОбработка, Форма.Объект.Характеристики.Добавить(), Форма.Характеристики.Добавить());
		СтрокаХарактеристика.ПредметСнабжения = ПредметСнабжения;
		СтрокаХарактеристика.Характеристика = Характеристика;
		СтрокаХарактеристика.ЕдиницаИзмерения = 
			КорректировкаДанныхСправочников.ПолучитьЕдиницуИзмерения(ЛистХарактеристик.Cells(Сч, 4).Value, ЛистХарактеристик.Cells(Сч, 3).Value);
		СтрокаХарактеристика.Основная = (ЛистХарактеристик.Cells(Сч, 6).Value = "Да");
		СтрокаХарактеристика.Значение = Формат(ЛистХарактеристик.Cells(Сч, 5).Value, "ЧГ=0");
		СтрокаХарактеристика.Идентификатор = ГУИД;
		СтрокаХарактеристика.Наименование = Наименование;
		СтрокаХарактеристика.ПорядковыйНомер = Сч - 2;
				
		Если Не Наименование = "Код ОКП" Тогда //исключения
			
			Попытка
				
				СтрокаХарактеристика.Значение = Число(СтрокаХарактеристика.Значение);
				
			Исключение
				
			КонецПопытки;
			
		КонецЕсли;
					
	КонецЦикла;
	
	//аналоги
	ЛистАналогов = Книга.WorkSheets(3);
	
	ДатаНачала = ТекущаяДата();
	ВсегоСтрок = ЛистАналогов.Cells.SpecialCells(11).Row;
	
	Для Сч = 3 По ВсегоСтрок Цикл
		
		УправлениеИнтерфейсом.ВывестиТекущееСостояние("Чтение аналогов...", ДатаНачала, Сч - 2, ВсегоСтрок - 2);
		
		ГУИД = ЛистАналогов.Cells(Сч, 1).Value;
		ГУИДАналога = ЛистАналогов.Cells(Сч, 2).Value;
		
		Если Не ЗначениеЗаполнено(ГУИД) Или Не ЗначениеЗаполнено(ГУИДАналога) Тогда
			
			Продолжить;
			
		КонецЕсли;
			
		ПредметСнабжения = КорректировкаДанныхСправочников.ПолучитьСсылкуПоГУИД(ГУИД, "КаталогПредметовСнабжения");
		Аналог = КорректировкаДанныхСправочников.ПолучитьСсылкуПоГУИД(ГУИДАналога, "КаталогПредметовСнабжения");
		
		СтрокаАналог = ?(ЭтоОбработка, Форма.Объект.Аналоги.Добавить(), Форма.Аналоги.Добавить());
		СтрокаАналог.ПредметСнабжения = ПредметСнабжения;
		СтрокаАналог.Аналог = Аналог;
		СтрокаАналог.Идентификатор = ГУИД;
		СтрокаАналог.ИдентификаторАналога = ГУИДАналога;
					
	КонецЦикла;
	
	Попытка
		
		Excel.Application.DisplayAlerts = False;
		Excel.Application.Quit();
		
	Исключение
		
	    ОписаниеОшибки = ОписаниеОшибки();
		Возврат Неопределено;
		
	КонецПопытки;
	
	Excel = "";
	
	Возврат ПутьКФайлу;
	
КонецФункции

Функция ПолучитьЧисло(Значение)
	
	Попытка
		
		Результат = Число(Значение);
		
	Исключение
		
		Результат = 0;
		
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьДату(Значение)
	
	Попытка
		
		Результат = Дата(Сред(Значение, 7, 4) + Сред(Значение, 4, 2) + Лев(Значение, 2));
		
	Исключение
		
		Результат = Дата("00010101");
		
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти