////////////////////////////////////////////////////////////////////////////////
// Модуль предназначен для процедур и функций работы с СУБД MS SQL Server.
// Савинов А.А. 21.04.2017
////////////////////////////////////////////////////////////////////////////////

#Область СУБД

// Функция предназначена для установки соединения с источником данных типа MSSQL
// Параметры:
// ИсточникДанных - СправочникСсылка.ИсточникиДанных
//
Функция УстановитьСоединение(ИсточникДанных) Экспорт

	Если Не ЗначениеЗаполнено(ИсточникДанных) Тогда
	
		Возврат Неопределено;	
	
	КонецЕсли;
	
	РеквизитыИсточника = ОбщиеФункцииСервер.ПолучитьЗначенияРеквизитов(ИсточникДанных, "ИмяСервера,ИмяБазы,Пользователь,Пароль");
	
	Соединение = Новый COMОбъект("ADODB.Connection");
	
	Соединение.ConnectionString = "Provider=SQLOLEDB; Server=" + РеквизитыИсточника.ИмяСервера + ";" + "Database=" + РеквизитыИсточника.ИмяБазы +";"+"User Id=" + РеквизитыИсточника.Пользователь + ";" + "Password=" + РеквизитыИсточника.Пароль + ";";

	Попытка
	
		Соединение.Open();	
	
	Исключение
		
		ОбщиеФункцииСервер.ЗарегистрироватьИнформациюОбОшибке("УстановитьСоединение(). " + ОписаниеОшибки(), Перечисления.ПриоритетыОшибок.Критический);
		Возврат Неопределено;
	
	КонецПопытки;
	
	Возврат Соединение;
	
КонецФункции // УстановитьСоединение()

// Функция предназначена для выполнения прямых запросов к СУБД и получения их результатов
// Параметры:
// Соединение - COMОбъект - Результат функции УстановитьСоединение()
// ТекстЗапроса - Строка
//
Функция ПолучитьРезультатЗапроса(Соединение, ТекстЗапроса, ЗакрытьСоединение = Истина) Экспорт
	
	Результат = Неопределено;

	Попытка
		
		Recordset = Новый COMОбъект("ADODB.Recordset");			
		Recordset.ActiveConnection = Соединение;
		Recordset.CursorType = 0;
		Recordset.LockType = 3;
		Recordset.CursorLocation = 3;
		Recordset.Open(ТекстЗапроса);
		
		ТЧ = Новый ТаблицаЗначений;
		
		Для iCount = 0 По RecordSet.Fields.Count - 1 Цикл
			
			ТЧ.Колонки.Добавить(RecordSet.Fields.Item(iCount).Name);
			
		КонецЦикла;
		
		Если ТЧ.Колонки.Количество() = 0 Тогда
			
			Перейти ~Конец;
			
		КонецЕсли;
		
		Если НЕ (RecordSet.EOF() И RecordSet.BOF()) Тогда 
			
			RecordSet.MoveFirst();                 
			
			Пока RecordSet.EOF() = 0 Цикл
				
				НовСтрока = ТЧ.Добавить();
				
				Для i = 0 По RecordSet.Fields.Count - 1 Цикл
					
					НовСтрока.Установить(i, RecordSet.Fields(i).Value);
					
				КонецЦикла;
				
				RecordSet.MoveNext();
				
			КонецЦикла;
			
			Результат = ТЧ;
			Перейти ~Конец;
			
		Иначе
			
			Перейти ~Конец;	
			
		КонецЕсли;
		
	Исключение
		
		Перейти ~Конец;
	
	КонецПопытки;
	
	~Конец:
	
	Если ЗакрытьСоединение Тогда
	
		Соединение.Close();	
	
	КонецЕсли;
	
	Возврат Результат;

КонецФункции // ПолучитьРезультатЗапроса()

#КонецОбласти

#Область Приложение

// Функция предназначена для получения значения СУБД MS SQL Server ссылки
// Параметры:
// Ссылка - ЛюбаяСсылка
// UID - УникальныйИдентификатор - Уникальный идентификатор ссылки
//
Функция ПолучитьЗначениеСУБДСсылки(Знач Ссылка = Неопределено, Знач UID = Неопределено) Экспорт
	
	Если Ссылка = Неопределено И UID = Неопределено Тогда
	
		Возврат Неопределено;	
	
	КонецЕсли;
	
	Если UID = Неопределено Тогда
	
		UID = Ссылка.УникальныйИдентификатор();	
	
	КонецЕсли;
	
	Секция1 = Сред(UID,20,4);
	Секция2 = Сред(UID,25,12);
	Секция3 = Сред(UID,15,4);
	Секция4 = Сред(UID,10,4);
	Секция5 = Сред(UID,1,8);
	
	Возврат "0x" + Секция1 + Секция2 + Секция3 + Секция4 + Секция5;
	
КонецФункции // ПолучитьЗначениеСУБДСсылки()

#КонецОбласти