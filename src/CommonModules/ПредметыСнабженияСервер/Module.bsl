////////////////////////////////////////////////////////////////////////////////
// Модуль предназначен для процедур и функций работы с предметами снабжения.
// Савинов А.А. 20.03.2017
////////////////////////////////////////////////////////////////////////////////

#Область РесурсыИСрокиСлужбы

// Функция предназначена для записи ресурсов и сроков службы предмета снабжения.
// Параметры:
// ПредметСнабжения - Справочники.КаталогПредметовСнабжения
// ТаблицаРесурсовИСроковСлужбы - ТаблицаЗначений - Таблица ресурсов и сроков службы предмета снабжения из формы элемента
//
Функция ЗаписатьРесурсыИСрокиСлужбы(ПредметСнабжения, ТаблицаРесурсовИСроковСлужбы) Экспорт

	Результат = Новый Структура("Выполнено, Описание", Ложь, "");
	
	НаборЗаписей = РегистрыСведений.РесурсыИСрокиСлужбыПредметовСнабжения.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ПредметСнабжения.Установить(ПредметСнабжения);
	НаборЗаписей.Прочитать();
	
	ЕстьЗаписи = НаборЗаписей.Количество() > 0;
	
	Если ЕстьЗаписи Тогда
	
		НаборЗаписей.Очистить();	
	
	КонецЕсли;
	
	Для каждого Строка Из ТаблицаРесурсовИСроковСлужбы Цикл
	
		НоваяЗапись = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, Строка);
		НоваяЗапись.ПредметСнабжения = ПредметСнабжения;
	
	КонецЦикла;
	
	Попытка
	
		НаборЗаписей.Записать(ЕстьЗаписи);	
	
	Исключение
		
		Результат.Описание = "Не удалось записать ресурсы и сроки службы." + Символы.ПС + ОписаниеОшибки();
		Возврат Результат;
	
	КонецПопытки;
	
	Результат.Выполнено = Истина;
	Возврат Результат;	

КонецФункции // ЗаписатьРесурсыИСрокиСлужбы()

#КонецОбласти

#Область ТО

// Функция предназначена для записи регламента ТО предмета снабжения.
// Параметры:
// ПредметСнабжения - Справочники.КаталогПредметовСнабжения
// ТаблицаРегламентаТО - ТаблицаЗначений - Таблица регламента ТО предмета снабжения из формы элемента
//
Функция ЗаписатьРегламентТО(ПредметСнабжения, ТаблицаРегламентаТО) Экспорт

	Результат = Новый Структура("Выполнено, Описание", Ложь, "");
	
	НаборЗаписей = РегистрыСведений.РегламентТОПредметовСнабжения.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ПредметСнабжения.Установить(ПредметСнабжения);
	НаборЗаписей.Прочитать();
	
	ЕстьЗаписи = НаборЗаписей.Количество() > 0;
	
	Если ЕстьЗаписи Тогда
	
		НаборЗаписей.Очистить();	
	
	КонецЕсли;
	
	Для каждого Строка Из ТаблицаРегламентаТО Цикл
	
		НоваяЗапись = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, Строка);
		НоваяЗапись.ПредметСнабжения = ПредметСнабжения;
	
	КонецЦикла;
	
	Попытка
	
		НаборЗаписей.Записать(ЕстьЗаписи);	
	
	Исключение
		
		Результат.Описание = "Не удалось записать регламент ТО." + Символы.ПС + ОписаниеОшибки();
		Возврат Результат;
	
	КонецПопытки;
	
	Результат.Выполнено = Истина;
	Возврат Результат;	

КонецФункции // ЗаписатьРегламентТО()

// Функция предназначена для записи ЗИП, требуемых для ТО предмета снабжения.
// Параметры:
// ПредметСнабжения - Справочники.КаталогПредметовСнабжения
// ТаблицаЗИПТребуемыхДляТО - ТаблицаЗначений - Таблица ЗИП, требуемых для ТО предмета снабжения из формы элемента
//
Функция ЗаписатьЗИПТребуемыеДляТО(ПредметСнабжения, ТаблицаЗИПТребуемыхДляТО) Экспорт

	Результат = Новый Структура("Выполнено, Описание", Ложь, "");
	
	НаборЗаписей = РегистрыСведений.ЗИПТребуемыеДляТОПредметовСнабжения.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ПредметСнабжения.Установить(ПредметСнабжения);
	НаборЗаписей.Прочитать();
	
	ЕстьЗаписи = НаборЗаписей.Количество() > 0;
	
	Если ЕстьЗаписи Тогда
	
		НаборЗаписей.Очистить();	
	
	КонецЕсли;
	
	Для каждого Строка Из ТаблицаЗИПТребуемыхДляТО Цикл
	
		НоваяЗапись = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, Строка);
		НоваяЗапись.ПредметСнабжения = ПредметСнабжения;
	
	КонецЦикла;
	
	Попытка
	
		НаборЗаписей.Записать(ЕстьЗаписи);	
	
	Исключение
		
		Результат.Описание = "Не удалось записать ЗИП, требуемые для ТО." + Символы.ПС + ОписаниеОшибки();
		Возврат Результат;
	
	КонецПопытки;
	
	Результат.Выполнено = Истина;
	Возврат Результат;	

КонецФункции // ЗаписатьЗИПТребуемыеДляТО()

#КонецОбласти

#Область КодыINCAT

Функция ЗаписатьПредставлениеКодовINCAT(ПредметСнабжения, СтрокаПредставление) Экспорт
	
	Результат = Новый Структура("Выполнено, Описание", Ложь, "");
	
	// КодыINCAT Представление
	НаборЗаписей = РегистрыСведений.КодыINCATПредставление.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ПредметСнабжения.Установить(ПредметСнабжения);
	НаборЗаписей.Прочитать();
	
	Если НаборЗаписей.Количество() > 0 Тогда
		НаборЗаписей.Очистить();
	КонецЕсли;
	
	НовСтр = НаборЗаписей.Добавить();
	НовСтр.ПредметСнабжения = ПредметСнабжения;
	НовСтр.Представление = СтрокаПредставление;
	
	Попытка
		НаборЗаписей.Записать();	
	Исключение
		Результат.Описание = "Не удалось записать КодыINCAT. " + Символы.ПС + ОписаниеОшибки();
		Возврат Результат;
	КонецПопытки;
	
	Результат.Выполнено = Истина;
	Возврат Результат;
	
КонецФункции
 
Функция ЗаписатьКодыINCAT(ПредметСнабжения, ТаблицаКодовINCAT) Экспорт
	
	Результат = Новый Структура("Выполнено, Описание", Ложь, "");

	// КодыINCAT
	НаборЗаписей = РегистрыСведений.КодыINCAT.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ПредметСнабжения.Установить(ПредметСнабжения);
	НаборЗаписей.Прочитать();
	
	Если НаборЗаписей.Количество() > 0 Тогда
		НаборЗаписей.Очистить();
	КонецЕсли;
	
	СтрокаПредставление = "";
	
	Для каждого ТекСтрока Из ТаблицаКодовINCAT Цикл
		
		НовСтр = НаборЗаписей.Добавить();
		НовСтр.ПредметСнабжения = ПредметСнабжения;
		НовСтр.КодINCAT = ТекСтрока.КодINCAT;
		
		СтрокаПредставление = СтрокаПредставление + ?(СтрокаПредставление="", "", "; ") +ТекСтрока.КодINCAT;
		
	КонецЦикла; 
	
	Попытка
		НаборЗаписей.Записать();	
	Исключение
		Результат.Описание = "Не удалось записать КодыINCAT. " + Символы.ПС + ОписаниеОшибки();
		Возврат Результат;
	КонецПопытки;
	
	РезультатЗаписьПредставления = ЗаписатьПредставлениеКодовINCAT(ПредметСнабжения, СтрокаПредставление);
	
	Если НЕ РезультатЗаписьПредставления.Выполнено Тогда
		Результат.Описание = "Не удалось записать КодыINCAT. " + Символы.ПС + РезультатЗаписьПредставления.Описание;
		Возврат Результат;
	КонецЕсли; 
	
	Результат.Выполнено = Истина;
	Возврат Результат;	
	
КонецФункции
 

#КонецОбласти 

#Область Свертка

// Функция предназначена для получения данных для объединения значений реквизитов у предметов снабжения после удаления дублей
// Параметры:
// ТаблицаПредметовСнабжения - ТаблицаЗначений
//
Функция ПолучитьДанныеДляСверткиПредметовСнабжения(ТаблицаПредметовСнабжения) Экспорт

	МВТ = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МВТ;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаПредметовСнабжения.Замененный КАК Замененный,
	|	ТаблицаПредметовСнабжения.Заменивший КАК Заменивший
	|ПОМЕСТИТЬ ВТ_ТаблицаПредметовСнабжения
	|ИЗ
	|	&ТаблицаПредметовСнабжения КАК ТаблицаПредметовСнабжения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Замененный
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_ТаблицаПредметовСнабжения.Заменивший КАК Заменивший
	|ПОМЕСТИТЬ ВТ_Основные
	|ИЗ
	|	ВТ_ТаблицаПредметовСнабжения КАК ВТ_ТаблицаПредметовСнабжения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Заменивший
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Основные.Заменивший КАК Заменивший,
	|	ВЫБОР
	|		КОГДА КаталогПредметовСнабжения.Обозначение = """"
	|				ИЛИ КаталогПредметовСнабжения.Обозначение = """"""""""""
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ОбозначениеЗаполнено,
	|	ВЫБОР
	|		КОГДА КаталогПредметовСнабжения.ОбозначениеТранслитированное = """"
	|				ИЛИ КаталогПредметовСнабжения.ОбозначениеТранслитированное = """"""""""""
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ОбозначениеТранслитированноеЗаполнено,
	|	ВЫБОР
	|		КОГДА КаталогПредметовСнабжения.ДокументНаПоставку = """"
	|				ИЛИ КаталогПредметовСнабжения.ДокументНаПоставку = """"""""""""
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ДокументНаПоставкуЗаполнен,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(КодыINCATПредставление.Представление, """") = """"
	|				ИЛИ ЕСТЬNULL(КодыINCATПредставление.Представление, """""""""""") = """"""""""""
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК INCATЗаполнен,
	|	ВЫБОР
	|		КОГДА КаталогПредметовСнабжения.НомерОСК = """"
	|				ИЛИ КаталогПредметовСнабжения.НомерОСК = """"""""""""
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК НомерОСКЗаполнен,
	|	КаталогПредметовСнабжения.Обозначение КАК Обозначение,
	|	КаталогПредметовСнабжения.ОбозначениеТранслитированное КАК ОбозначениеТранслитированное,
	|	КаталогПредметовСнабжения.ДокументНаПоставку КАК ДокументНаПоставку,
	|	ЕСТЬNULL(КодыINCATПредставление.Представление, """") КАК INCAT,
	|	КаталогПредметовСнабжения.НомерОСК КАК НомерОСК
	|ПОМЕСТИТЬ ВТ_ЗаполненностьЗначенийРеквизитовОсновных
	|ИЗ
	|	ВТ_Основные КАК ВТ_Основные
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КаталогПредметовСнабжения КАК КаталогПредметовСнабжения
	|		ПО ВТ_Основные.Заменивший = КаталогПредметовСнабжения.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КодыINCATПредставление КАК КодыINCATПредставление
	|		ПО ВТ_Основные.Заменивший = КодыINCATПредставление.ПредметСнабжения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаПредметовСнабжения.Заменивший КАК Заменивший,
	|	МАКСИМУМ(КаталогПредметовСнабжения.Обозначение) КАК Обозначение,
	|	МАКСИМУМ(КаталогПредметовСнабжения.ОбозначениеТранслитированное) КАК ОбозначениеТранслитированное,
	|	МАКСИМУМ(КаталогПредметовСнабжения.ДокументНаПоставку) КАК ДокументНаПоставку,
	|	МАКСИМУМ(ЕСТЬNULL(КодыINCATПредставление.Представление, """")) КАК INCAT,
	|	МАКСИМУМ(КаталогПредметовСнабжения.НомерОСК) КАК НомерОСК
	|ПОМЕСТИТЬ ВТ_ЗначенияРеквизитовДублей
	|ИЗ
	|	ВТ_ТаблицаПредметовСнабжения КАК ВТ_ТаблицаПредметовСнабжения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КаталогПредметовСнабжения КАК КаталогПредметовСнабжения
	|		ПО ВТ_ТаблицаПредметовСнабжения.Замененный = КаталогПредметовСнабжения.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КодыINCATПредставление КАК КодыINCATПредставление
	|		ПО ВТ_ТаблицаПредметовСнабжения.Замененный = КодыINCATПредставление.Представление
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ТаблицаПредметовСнабжения.Заменивший
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Заменивший
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ЗначенияРеквизитовДублей.Заменивший КАК Заменивший,
	|	ВЫБОР
	|		КОГДА ВТ_ЗначенияРеквизитовДублей.Обозначение = """"
	|				ИЛИ ВТ_ЗначенияРеквизитовДублей.Обозначение = """"""""""""
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ОбозначениеЗаполнено,
	|	ВЫБОР
	|		КОГДА ВТ_ЗначенияРеквизитовДублей.ОбозначениеТранслитированное = """"
	|				ИЛИ ВТ_ЗначенияРеквизитовДублей.ОбозначениеТранслитированное = """"""""""""
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ОбозначениеТранслитированноеЗаполнено,
	|	ВЫБОР
	|		КОГДА ВТ_ЗначенияРеквизитовДублей.ДокументНаПоставку = """"
	|				ИЛИ ВТ_ЗначенияРеквизитовДублей.ДокументНаПоставку = """"""""""""
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ДокументНаПоставкуЗаполнен,
	|	ВЫБОР
	|		КОГДА ВТ_ЗначенияРеквизитовДублей.INCAT = """"
	|				ИЛИ ВТ_ЗначенияРеквизитовДублей.INCAT = """"""""""""
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК INCATЗаполнен,
	|	ВЫБОР
	|		КОГДА ВТ_ЗначенияРеквизитовДублей.НомерОСК = """"
	|				ИЛИ ВТ_ЗначенияРеквизитовДублей.НомерОСК = """"""""""""
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК НомерОСКЗаполнен
	|ПОМЕСТИТЬ ВТ_ЗаполненностьЗначенийРеквизитовДублей
	|ИЗ
	|	ВТ_ЗначенияРеквизитовДублей КАК ВТ_ЗначенияРеквизитовДублей
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ЗначенияРеквизитовДублей.Заменивший КАК Заменивший,
	|	ВТ_ЗначенияРеквизитовДублей.Обозначение КАК Обозначение
	|ПОМЕСТИТЬ ВТ_Обозначение
	|ИЗ
	|	ВТ_ЗаполненностьЗначенийРеквизитовОсновных КАК ВТ_ЗаполненностьЗначенийРеквизитовОсновных
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ЗаполненностьЗначенийРеквизитовДублей КАК ВТ_ЗаполненностьЗначенийРеквизитовДублей
	|		ПО ВТ_ЗаполненностьЗначенийРеквизитовОсновных.Заменивший = ВТ_ЗаполненностьЗначенийРеквизитовДублей.Заменивший
	|			И (НЕ ВТ_ЗаполненностьЗначенийРеквизитовОсновных.ОбозначениеЗаполнено)
	|			И (ВТ_ЗаполненностьЗначенийРеквизитовДублей.ОбозначениеЗаполнено)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ЗначенияРеквизитовДублей КАК ВТ_ЗначенияРеквизитовДублей
	|		ПО ВТ_ЗаполненностьЗначенийРеквизитовОсновных.Заменивший = ВТ_ЗначенияРеквизитовДублей.Заменивший
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Заменивший
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ЗначенияРеквизитовДублей.Заменивший КАК Заменивший,
	|	ВТ_ЗначенияРеквизитовДублей.ОбозначениеТранслитированное КАК ОбозначениеТранслитированное
	|ПОМЕСТИТЬ ВТ_ОбозначениеТранслитированное
	|ИЗ
	|	ВТ_ЗаполненностьЗначенийРеквизитовОсновных КАК ВТ_ЗаполненностьЗначенийРеквизитовОсновных
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ЗаполненностьЗначенийРеквизитовДублей КАК ВТ_ЗаполненностьЗначенийРеквизитовДублей
	|		ПО ВТ_ЗаполненностьЗначенийРеквизитовОсновных.Заменивший = ВТ_ЗаполненностьЗначенийРеквизитовДублей.Заменивший
	|			И (НЕ ВТ_ЗаполненностьЗначенийРеквизитовОсновных.ОбозначениеТранслитированноеЗаполнено)
	|			И (ВТ_ЗаполненностьЗначенийРеквизитовДублей.ОбозначениеТранслитированноеЗаполнено)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ЗначенияРеквизитовДублей КАК ВТ_ЗначенияРеквизитовДублей
	|		ПО ВТ_ЗаполненностьЗначенийРеквизитовОсновных.Заменивший = ВТ_ЗначенияРеквизитовДублей.Заменивший
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Заменивший
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ЗначенияРеквизитовДублей.Заменивший КАК Заменивший,
	|	ВТ_ЗначенияРеквизитовДублей.ДокументНаПоставку КАК ДокументНаПоставку
	|ПОМЕСТИТЬ ВТ_ДокументНаПоставку
	|ИЗ
	|	ВТ_ЗаполненностьЗначенийРеквизитовОсновных КАК ВТ_ЗаполненностьЗначенийРеквизитовОсновных
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ЗаполненностьЗначенийРеквизитовДублей КАК ВТ_ЗаполненностьЗначенийРеквизитовДублей
	|		ПО ВТ_ЗаполненностьЗначенийРеквизитовОсновных.Заменивший = ВТ_ЗаполненностьЗначенийРеквизитовДублей.Заменивший
	|			И (НЕ ВТ_ЗаполненностьЗначенийРеквизитовОсновных.ДокументНаПоставкуЗаполнен)
	|			И (ВТ_ЗаполненностьЗначенийРеквизитовДублей.ДокументНаПоставкуЗаполнен)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ЗначенияРеквизитовДублей КАК ВТ_ЗначенияРеквизитовДублей
	|		ПО ВТ_ЗаполненностьЗначенийРеквизитовОсновных.Заменивший = ВТ_ЗначенияРеквизитовДублей.Заменивший
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Заменивший
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ЗначенияРеквизитовДублей.Заменивший КАК Заменивший,
	|	ВТ_ЗначенияРеквизитовДублей.INCAT КАК INCAT
	|ПОМЕСТИТЬ ВТ_INCAT
	|ИЗ
	|	ВТ_ЗаполненностьЗначенийРеквизитовОсновных КАК ВТ_ЗаполненностьЗначенийРеквизитовОсновных
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ЗаполненностьЗначенийРеквизитовДублей КАК ВТ_ЗаполненностьЗначенийРеквизитовДублей
	|		ПО ВТ_ЗаполненностьЗначенийРеквизитовОсновных.Заменивший = ВТ_ЗаполненностьЗначенийРеквизитовДублей.Заменивший
	|			И (НЕ ВТ_ЗаполненностьЗначенийРеквизитовОсновных.INCATЗаполнен)
	|			И (ВТ_ЗаполненностьЗначенийРеквизитовДублей.INCATЗаполнен)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ЗначенияРеквизитовДублей КАК ВТ_ЗначенияРеквизитовДублей
	|		ПО ВТ_ЗаполненностьЗначенийРеквизитовОсновных.Заменивший = ВТ_ЗначенияРеквизитовДублей.Заменивший
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Заменивший
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ЗначенияРеквизитовДублей.Заменивший КАК Заменивший,
	|	ВТ_ЗначенияРеквизитовДублей.НомерОСК КАК НомерОСК
	|ПОМЕСТИТЬ ВТ_НомерОСК
	|ИЗ
	|	ВТ_ЗаполненностьЗначенийРеквизитовОсновных КАК ВТ_ЗаполненностьЗначенийРеквизитовОсновных
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ЗаполненностьЗначенийРеквизитовДублей КАК ВТ_ЗаполненностьЗначенийРеквизитовДублей
	|		ПО ВТ_ЗаполненностьЗначенийРеквизитовОсновных.Заменивший = ВТ_ЗаполненностьЗначенийРеквизитовДублей.Заменивший
	|			И (НЕ ВТ_ЗаполненностьЗначенийРеквизитовОсновных.НомерОСКЗаполнен)
	|			И (ВТ_ЗаполненностьЗначенийРеквизитовДублей.НомерОСКЗаполнен)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ЗначенияРеквизитовДублей КАК ВТ_ЗначенияРеквизитовДублей
	|		ПО ВТ_ЗаполненностьЗначенийРеквизитовОсновных.Заменивший = ВТ_ЗначенияРеквизитовДублей.Заменивший
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Заменивший
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ЗаполненностьЗначенийРеквизитовОсновных.Заменивший КАК Заменивший,
	|	ЕСТЬNULL(ВТ_Обозначение.Обозначение, ВТ_ЗаполненностьЗначенийРеквизитовОсновных.Обозначение) КАК Обозначение,
	|	ЕСТЬNULL(ВТ_ОбозначениеТранслитированное.ОбозначениеТранслитированное, ВТ_ЗаполненностьЗначенийРеквизитовОсновных.ОбозначениеТранслитированное) КАК ОбозначениеТранслитированное,
	|	ЕСТЬNULL(ВТ_ДокументНаПоставку.ДокументНаПоставку, ВТ_ЗаполненностьЗначенийРеквизитовОсновных.ДокументНаПоставку) КАК ДокументНаПоставку,
	|	ЕСТЬNULL(ВТ_НомерОСК.НомерОСК, ВТ_ЗаполненностьЗначенийРеквизитовОсновных.НомерОСК) КАК НомерОСК
	|ИЗ
	|	ВТ_ЗаполненностьЗначенийРеквизитовОсновных КАК ВТ_ЗаполненностьЗначенийРеквизитовОсновных
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Обозначение КАК ВТ_Обозначение
	|		ПО ВТ_ЗаполненностьЗначенийРеквизитовОсновных.Заменивший = ВТ_Обозначение.Заменивший
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОбозначениеТранслитированное КАК ВТ_ОбозначениеТранслитированное
	|		ПО ВТ_ЗаполненностьЗначенийРеквизитовОсновных.Заменивший = ВТ_ОбозначениеТранслитированное.Заменивший
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДокументНаПоставку КАК ВТ_ДокументНаПоставку
	|		ПО ВТ_ЗаполненностьЗначенийРеквизитовОсновных.Заменивший = ВТ_ДокументНаПоставку.Заменивший
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_INCAT КАК ВТ_INCAT
	|		ПО ВТ_ЗаполненностьЗначенийРеквизитовОсновных.Заменивший = ВТ_INCAT.Заменивший
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_НомерОСК КАК ВТ_НомерОСК
	|		ПО ВТ_ЗаполненностьЗначенийРеквизитовОсновных.Заменивший = ВТ_НомерОСК.Заменивший
	|ГДЕ
	|	ВТ_ЗаполненностьЗначенийРеквизитовОсновных.Заменивший В
	|			(ВЫБРАТЬ
	|				ВТ_Обозначение.Заменивший
	|			ИЗ
	|				ВТ_Обозначение КАК ВТ_Обозначение
	|		
	|			ОБЪЕДИНИТЬ ВСЕ
	|		
	|			ВЫБРАТЬ
	|				ВТ_ОбозначениеТранслитированное.Заменивший
	|			ИЗ
	|				ВТ_ОбозначениеТранслитированное КАК ВТ_ОбозначениеТранслитированное
	|		
	|			ОБЪЕДИНИТЬ ВСЕ
	|		
	|			ВЫБРАТЬ
	|				ВТ_ДокументНаПоставку.Заменивший
	|			ИЗ
	|				ВТ_ДокументНаПоставку КАК ВТ_ДокументНаПоставку
	|		
	|			ОБЪЕДИНИТЬ ВСЕ
	|		
	|			ВЫБРАТЬ
	|				ВТ_INCAT.Заменивший
	|			ИЗ
	|				ВТ_INCAT КАК ВТ_INCAT
	|		
	|			ОБЪЕДИНИТЬ ВСЕ
	|		
	|			ВЫБРАТЬ
	|				ВТ_НомерОСК.Заменивший
	|			ИЗ
	|				ВТ_НомерОСК КАК ВТ_НомерОСК)";
	
	Запрос.УстановитьПараметр("ТаблицаПредметовСнабжения", ТаблицаПредметовСнабжения);
	
	Возврат Запрос.Выполнить();	

КонецФункции // ПолучитьДанныеДляСверткиПредметовСнабжения()

// Функция предназначена для получения данных для объединения изготовителей и поставщиков у предметов снабжения после удаления дублей
// Параметры:
// ТаблицаПредметовСнабжения - ТаблицаЗначений
//
Функция ПолучитьДанныеДляСверткиИзготовителейИПоставщиковПредметовСнабжения(ТаблицаПредметовСнабжения) Экспорт
	
	МВТ = Новый МенеджерВременныхТаблиц;

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МВТ;
	Запрос.Текст = "ВЫБРАТЬ
	|	ТаблицаПредметовСнабжения.Замененный КАК Замененный,
	|	ТаблицаПредметовСнабжения.Заменивший
	|ПОМЕСТИТЬ ВТ_ТаблицаПредметовСнабжения
	|ИЗ
	|	&ТаблицаПредметовСнабжения КАК ТаблицаПредметовСнабжения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_ТаблицаПредметовСнабжения.Заменивший КАК Заменивший
	|ПОМЕСТИТЬ ВТ_Основные
	|ИЗ
	|	ВТ_ТаблицаПредметовСнабжения КАК ВТ_ТаблицаПредметовСнабжения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Заменивший
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Основные.Заменивший
	|ПОМЕСТИТЬ ВТ_ОсновныеБезПоставщиков
	|ИЗ
	|	ВТ_Основные КАК ВТ_Основные
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КаталогПредметовСнабжения.ИзготовителиИПоставщики КАК КаталогПредметовСнабженияИзготовителиИПоставщики
	|		ПО ВТ_Основные.Заменивший = КаталогПредметовСнабженияИзготовителиИПоставщики.Ссылка
	|ГДЕ
	|	КаталогПредметовСнабженияИзготовителиИПоставщики.Ссылка ЕСТЬ NULL ";
	
	Запрос.УстановитьПараметр("ТаблицаПредметовСнабжения", ТаблицаПредметовСнабжения);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ПоследнийРезультат = МВТ.Таблицы.Получить(2).ПолучитьДанные();
	
	Если ПоследнийРезультат.Пустой() Тогда
	
		Возврат ПоследнийРезультат;	
	
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МВТ;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВТ_ТаблицаПредметовСнабжения.Замененный КАК Замененный,
	|	ВТ_ТаблицаПредметовСнабжения.Заменивший
	|ПОМЕСТИТЬ ВТ_Замененные
	|ИЗ
	|	ВТ_ТаблицаПредметовСнабжения КАК ВТ_ТаблицаПредметовСнабжения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОсновныеБезПоставщиков КАК ВТ_ОсновныеБезПоставщиков
	|		ПО ВТ_ТаблицаПредметовСнабжения.Заменивший = ВТ_ОсновныеБезПоставщиков.Заменивший
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Замененный
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ВТ_Замененные.Замененный) КАК Замененный,
	|	ВТ_Замененные.Заменивший
	|ПОМЕСТИТЬ ВТ_ЗамененныеСПоставщиками
	|ИЗ
	|	ВТ_Замененные КАК ВТ_Замененные
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КаталогПредметовСнабжения.ИзготовителиИПоставщики КАК КаталогПредметовСнабженияИзготовителиИПоставщики
	|		ПО ВТ_Замененные.Замененный = КаталогПредметовСнабженияИзготовителиИПоставщики.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Замененные.Заменивший
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Замененный
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ЗамененныеСПоставщиками.Заменивший,
	|	КаталогПредметовСнабженияИзготовителиИПоставщики.НомерСтроки,
	|	КаталогПредметовСнабженияИзготовителиИПоставщики.Контрагент,
	|	КаталогПредметовСнабженияИзготовителиИПоставщики.Поставщик,
	|	КаталогПредметовСнабженияИзготовителиИПоставщики.Разработчик,
	|	КаталогПредметовСнабженияИзготовителиИПоставщики.Изготовитель
	|ИЗ
	|	ВТ_ЗамененныеСПоставщиками КАК ВТ_ЗамененныеСПоставщиками
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КаталогПредметовСнабжения.ИзготовителиИПоставщики КАК КаталогПредметовСнабженияИзготовителиИПоставщики
	|		ПО ВТ_ЗамененныеСПоставщиками.Замененный = КаталогПредметовСнабженияИзготовителиИПоставщики.Ссылка";
	
	Возврат Запрос.Выполнить();

КонецФункции // ПолучитьДанныеДляСверткиИзготовителейИПоставщиковПредметовСнабжения()

// Функция предназначена для получения данных для объединения переводов наименований у предметов снабжения после удаления дублей
// Параметры:
// ТаблицаПредметовСнабжения - ТаблицаЗначений
//
Функция ПолучитьДанныеДляСверткиПереводовНаименованийПредметовСнабжения(ТаблицаПредметовСнабжения) Экспорт
	
	МВТ = Новый МенеджерВременныхТаблиц;

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МВТ;
	Запрос.Текст = "ВЫБРАТЬ
	|	ТаблицаПредметовСнабжения.Замененный КАК Замененный,
	|	ТаблицаПредметовСнабжения.Заменивший
	|ПОМЕСТИТЬ ВТ_ТаблицаПредметовСнабжения
	|ИЗ
	|	&ТаблицаПредметовСнабжения КАК ТаблицаПредметовСнабжения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_ТаблицаПредметовСнабжения.Заменивший КАК Заменивший
	|ПОМЕСТИТЬ ВТ_Основные
	|ИЗ
	|	ВТ_ТаблицаПредметовСнабжения КАК ВТ_ТаблицаПредметовСнабжения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Заменивший
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Основные.Заменивший
	|ПОМЕСТИТЬ ВТ_ОсновныеБезПереводов
	|ИЗ
	|	ВТ_Основные КАК ВТ_Основные
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеНаименования КАК РегистрСведенийДополнительныеНаименования
	|		ПО ВТ_Основные.Заменивший = РегистрСведенийДополнительныеНаименования.Владелец
	|			И (РегистрСведенийДополнительныеНаименования.ИмяРеквизита = ""Наименование"")
	|			И (РегистрСведенийДополнительныеНаименования.Язык = ЗНАЧЕНИЕ(Справочник.Языки.Английский))
	|ГДЕ
	|	РегистрСведенийДополнительныеНаименования.Владелец ЕСТЬ NULL ";
	
	Запрос.УстановитьПараметр("ТаблицаПредметовСнабжения", ТаблицаПредметовСнабжения);
	
	РезультатЗапроса = Запрос.Выполнить();	
	
	ПоследнийРезультат = МВТ.Таблицы.Получить(2).ПолучитьДанные();
	
	Если ПоследнийРезультат.Пустой() Тогда
	
		Возврат ПоследнийРезультат;	
	
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МВТ;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВТ_ТаблицаПредметовСнабжения.Замененный КАК Замененный,
	|	ВТ_ТаблицаПредметовСнабжения.Заменивший
	|ПОМЕСТИТЬ ВТ_Замененные
	|ИЗ
	|	ВТ_ТаблицаПредметовСнабжения КАК ВТ_ТаблицаПредметовСнабжения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОсновныеБезПереводов КАК ВТ_ОсновныеБезПереводов
	|		ПО ВТ_ТаблицаПредметовСнабжения.Заменивший = ВТ_ОсновныеБезПереводов.Заменивший
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Замененный
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ВТ_Замененные.Замененный) КАК Замененный,
	|	ВТ_Замененные.Заменивший
	|ПОМЕСТИТЬ ВТ_ЗамененныеСПереводами
	|ИЗ
	|	ВТ_Замененные КАК ВТ_Замененные
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеНаименования КАК РегистрСведенийДополнительныеНаименования
	|		ПО ВТ_Замененные.Замененный = РегистрСведенийДополнительныеНаименования.Владелец
	|			И (РегистрСведенийДополнительныеНаименования.ИмяРеквизита = ""Наименование"")
	|			И (РегистрСведенийДополнительныеНаименования.Язык = ЗНАЧЕНИЕ(Справочник.Языки.Английский))
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Замененные.Заменивший
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Замененный
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ЗамененныеСПереводами.Заменивший,
	|	РегистрСведенийДополнительныеНаименования.Перевод
	|ИЗ
	|	ВТ_ЗамененныеСПереводами КАК ВТ_ЗамененныеСПереводами
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеНаименования КАК РегистрСведенийДополнительныеНаименования
	|		ПО ВТ_ЗамененныеСПереводами.Замененный = РегистрСведенийДополнительныеНаименования.Владелец
	|			И (РегистрСведенийДополнительныеНаименования.ИмяРеквизита = ""Наименование"")
	|			И (РегистрСведенийДополнительныеНаименования.Язык = ЗНАЧЕНИЕ(Справочник.Языки.Английский))";
	
	Возврат Запрос.Выполнить();

КонецФункции // ПолучитьДанныеДляСверткиИзготовителейИПоставщиковПредметовСнабжения()

#КонецОбласти