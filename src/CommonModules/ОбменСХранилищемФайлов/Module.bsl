//////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

#Область HTTP

Функция ПолучитьПараметрыСоединения(Объект, ЭтоРазбор)
	
	Результат = Неопределено;
	
	ОсновнаяНастройка = Справочники.НастройкиОбменаСХранилищемФайлов.Основная;
	Если Не ПустаяСтрока(ОсновнаяНастройка.Сервер) 
		И Не ПустаяСтрока(ОсновнаяНастройка.Логин) 
		И Не ПустаяСтрока(ОсновнаяНастройка.Пароль) 
		И Не ПустаяСтрока(ОсновнаяНастройка.Ресурс) Тогда
		
		Результат = Новый Структура;
		Результат.Вставить("Сервер",		ОсновнаяНастройка.Сервер);
		Результат.Вставить("Порт",			8008);
		Результат.Вставить("Пользователь",	ОсновнаяНастройка.Логин);
		Результат.Вставить("Пароль",		ОсновнаяНастройка.Пароль);
		Результат.Вставить("Ресурс",		ОсновнаяНастройка.Ресурс);
	Иначе
		ЗафиксироватьОшибкиОбмена(Объект,"Основная настройка соединения некорректна", ЭтоРазбор);
	КонецЕсли; // Если Не ПустаяСтрока(ОсновнаяНастройка.Адрес) Тогда
	
	Возврат	Результат;
	
КонецФункции // ПолучитьПараметрыСоединения

Функция ПолучитьСоединение(ПараметрыСоединения, Объект, ЭтоРазбор)
	
	Результат = Неопределено;
	
	Попытка
		Результат = Новый HTTPСоединение(
				ПараметрыСоединения.Сервер,
				ПараметрыСоединения.Порт,
				ПараметрыСоединения.Пользователь, 
				ПараметрыСоединения.Пароль,,15); 	
	Исключение
		ТекстОшибки = СтрШаблон("Неудачная попытка соединения [%1]",ОписаниеОшибки());
		ЗафиксироватьОшибкиОбмена(Объект, ТекстОшибки, ЭтоРазбор);
	КонецПопытки;
	
	Возврат	Результат;

КонецФункции // ПолучитьСоединение

Функция СформироватьЗапрос(АдресРесурса, Заголовки, ТелоЗапроса = Неопределено)
	
	Результат = Новый HTTPЗапрос(АдресРесурса, Заголовки);
	
	Если Не ТелоЗапроса = Неопределено Тогда
		Результат.УстановитьТелоИзСтроки(ТелоЗапроса);
	КонецЕсли; // Если Не ТелоЗапроса = Неопределено Тогда
	
	Возврат	Результат;
	
КонецФункции // СформироватьЗапрос

Функция ЗаголовкиПоУмолчанию()
	
	Результат = Новый Соответствие();
	Результат.Вставить("Content-Type","text/json; charset=utf-8");
	Возврат	Результат;
	
КонецФункции // ЗаголовкиПоУмолчанию

Функция ВыполнитьМетодСоединения(ТекущееСоединение, Метод, Запрос, Объект, ЭтоРазбор = Истина)
	
	Результат = Неопределено;
	
	Попытка
		HTTPОтвет = ТекущееСоединение.ВызватьHTTPМетод(Метод, Запрос);
		Результат = HTTPОтвет;		
	Исключение
		ЗафиксироватьОшибкиОбмена(Объект, "Произошла сетевая ошибка", ЭтоРазбор); 
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции // ВыполнитьМетодСоединения

Функция ПодготовитьСтруктуруСоединения(Объект, МетодСервиса, ТелоЗапроса = Неопределено, ПараметрыЗапроса = Неопределено, ЭтоРазбор = Истина)

	Результат = Новый Структура;
	
	// Соединение
	ПараметрыСоединения = ПолучитьПараметрыСоединения(Объект, ЭтоРазбор);
	Если ПараметрыСоединения = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли; // Если ПараметрыСоединения = Неопределено Тогда
		
	ТекущееСоединение = ПолучитьСоединение(ПараметрыСоединения, Объект, ЭтоРазбор);
	Если ТекущееСоединение = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли; // Если ТекущееСоединение = Неопределено Тогда
	Результат.Вставить("ТекущееСоединение", ТекущееСоединение);	
	
	// Формирование адреса ресурса
	АдресРесурса 	= СтрШаблон("/%1/hs/FileProcess/V1/%2", ПараметрыСоединения.Ресурс, МетодСервиса);
						
	ПараметрыМассив = Новый Массив;
	Если Не ПараметрыЗапроса = Неопределено Тогда
		Для Каждого ОписаниеПараметра Из ПараметрыЗапроса Цикл
			ПараметрыМассив.Добавить(СтрШаблон("%1=%2", ОписаниеПараметра.Ключ, ОписаниеПараметра.Значение));
		КонецЦикла; // Для Каждого ОписаниеПараметра Из ПараметрыЗапроса Цикл
	КонецЕсли; // Если Не ПараметрыЗапроса = Неопределено Тогда					
	
	Если ПараметрыМассив.Количество() > 0 Тогда
		АдресРесурса = АдресРесурса + "?" + СтрСоединить(ПараметрыМассив, "&");
	КонецЕсли; // Если ПараметрыМассив.Количество() > 0 Тогда
		
	// Формирование запроса						
	Результат.Вставить("Запрос", СформироватьЗапрос(АдресРесурса, ЗаголовкиПоУмолчанию(),ТелоЗапроса));
		
	Возврат Результат;
		
КонецФункции // ПодготовитьСтруктуруСоединения

#КонецОбласти

#Область Служебные

Процедура ЗафиксироватьОшибкиОбмена(СсылкаНаФайл, Ошибки, ЭтоРазбор = Истина)
	
	Если ТипЗнч(Ошибки) = Тип("Массив") Тогда
		ОписаниеОшибки = СтрСоединить(Ошибки, "; ");
	Иначе
		ОписаниеОшибки = Ошибки;	
	КонецЕсли; // Если ТипЗнч(Ошибки) = Тип("Массив") Тогда
	
	Если ЭтоРазбор Тогда 
		Если ПустаяСтрока(ОписаниеОшибки) Тогда
			УвеличитьСчетчик(СсылкаНаФайл, "Обмен завершен ошибкой");
		Иначе
			УвеличитьСчетчик(СсылкаНаФайл, "Ошибки обмена: " + ОписаниеОшибки);
		КонецЕсли; // Если ПустаяСтрока(ОписаниеОшибки) Тогда
	Иначе
		СообщитьПользователю(ОписаниеОшибки());
	КонецЕсли; // Если ЭтоРазбор Тогда 
	
КонецПроцедуры // ЗафиксироватьОшибкиОбмена

Функция ПреобразоватьИзСтрокиJSON(СтрокаИсточник,СсылкаНаФайл,ЭтоРазбор = Истина)
	
	Результат = Неопределено;
	
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(СтрокаИсточник);
	
	Попытка
		Результат = ПрочитатьJSON(ЧтениеJSON);
	Исключение
		ЗафиксироватьОшибкиОбмена(СсылкаНаФайл, "Ошибка преобразования ответа", ЭтоРазбор);
	КонецПопытки;

	Возврат Результат;
	
КонецФункции // ПреобразоватьИзСтрокиJSON

Функция ПолучитьСтрокуJSON(СтруктураДанные)
	РезультатJSON 			= Новый ЗаписьJSON;
	РезультатJSON.УстановитьСтроку();
	ЗаписатьJSON(РезультатJSON, СтруктураДанные);
	СтрокаОтвета 			= РезультатJSON.Закрыть();
	Возврат СтрокаОтвета;
КонецФункции // ПолучитьСтрокуОтвета

Функция ПолучитьТелоЗапросаОтправкиФайла(СсылкаНаФайл)

	СтруктураДанные = Новый Структура;
	
	ДанныеФайла = ПрисоединенныеФайлы.ПолучитьДанныеФайла(СсылкаНаФайл);
	СтруктураДанные.Вставить("Extension", ДанныеФайла.Расширение);
	
	ДанныеСтрокой = СеарилизоватьДвоичныеДанные(ПолучитьИзВременногоХранилища(ДанныеФайла.СсылкаНаДвоичныеДанныеФайла), СсылкаНаФайл);
	Если ДанныеСтрокой = Неопределено ТОгда
		Возврат Неопределено;
	КонецЕсли; // Если ДанныеСтрокой = Неопределено ТОгда
	СтруктураДанные.Вставить("Data", ДанныеСтрокой);
	
	Возврат ПолучитьСтрокуJSON(СтруктураДанные);
	
КонецФункции // ПолучитьТелоЗапросаОтправкиФайла

Функция СеарилизоватьДвоичныеДанные(ДвоичныеДанные, СсылкаНаФайл)

	ЗаписьJSON = Новый ЗаписьJSON();
	ЗаписьJSON.УстановитьСтроку();
	
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("BinaryData", ДвоичныеДанные);
	
	Попытка
		СериализаторXDTO.ЗаписатьJSON(ЗаписьJSON, СтруктураДанные);
		Результат = ЗаписьJSON.Закрыть();
	Исключение
		Результат = Неопределено;
		ЗафиксироватьОшибкиОбмена(СсылкаНаФайл, "Не удалось сеарилизовать данные");
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции // СеарилизоватьДвоичныеДанные

Функция ДесериализоватьДвоичныеДанные(ДанныеСтрокой, СсылкаНаФайл)
	
	ЧтениеJSON = Новый ЧтениеJSON();
	ЧтениеJSON.УстановитьСтроку(ДанныеСтрокой);
	
	Попытка
		Результат = СериализаторXDTO.ПрочитатьJSON(ЧтениеJSON, Тип("Структура"));
	Исключение
	    Результат = Неопределено;
		ЗафиксироватьОшибкиОбмена(СсылкаНаФайл, "Не удалось прочитать данные файла", Ложь);
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции // ДесериализоватьДвоичныеДанные

Функция УдалитьДвоичныеДанные(СсылкаНаФайл, ЭтоРазбор = Истина)
	
	Результат = Истина;
	
	ПотокВПамяти = Новый ПотокВПамяти;
	ПустыеДанные = ПотокВПамяти.ЗакрытьИПолучитьДвоичныеДанные();

	ДанныеФайла	= Новый Структура;
	ДанныеФайла.Вставить("АдресФайлаВоВременномХранилище", ПоместитьВоВременноеХранилище(ПустыеДанные));
	ДанныеФайла.Вставить("АдресВременногоХранилищаТекста", ПоместитьВоВременноеХранилище(ПустыеДанные));
	
	Попытка
		ПрисоединенныеФайлы.ОбновитьПрисоединенныйФайл(СсылкаНаФайл, ДанныеФайла);
	Исключение
		ЗафиксироватьОшибкиОбмена(СсылкаНаФайл, "Не удалось удалить двоичные данные файла", ЭтоРазбор); 
		Результат = Ложь;
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции // УдалитьДвоичныеДанные

Функция ОбновитьДанныеФайла(СтруктураДанных, СсылкаНаФайл)
	
	Результат = Истина;
	
	ПотокВПамяти = Новый ПотокВПамяти();
	ПустыеДанные = ПотокВПамяти.ЗакрытьИПолучитьДвоичныеДанные();
	
	ДанныеФайла	= Новый Структура;
	ДанныеФайла.Вставить("АдресФайлаВоВременномХранилище", 	ПоместитьВоВременноеХранилище(СтруктураДанных.BinaryData));	
	ДанныеФайла.Вставить("АдресВременногоХранилищаТекста",	ПоместитьВоВременноеХранилище(ПустыеДанные));
	
	Попытка
		ПрисоединенныеФайлы.ОбновитьПрисоединенныйФайл(СсылкаНаФайл, ДанныеФайла);
	Исключение
		Результат = Ложь;
	КонецПопытки;
		
	Возврат Результат;
		
КонецФункции // ОбновитьДанныеФайла

Процедура УдалитьЗапись(Объект) Экспорт
	РегистрыСведений.РазборХранилищеФайлов.УдалитьЗапись(Объект);
КонецПроцедуры // УдалитьЗапись

Процедура УвеличитьСчетчик(Объект, ОписаниеОшибки) Экспорт
	РегистрыСведений.РазборХранилищеФайлов.УвеличитьСчетчик(Объект, ОписаниеОшибки);	
КонецПроцедуры // УвеличитьСчетчик

Процедура СообщитьПользователю(ТекстОшибки)
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);	
КонецПроцедуры // СообщитьОшибку

#КонецОбласти

#Область Проверки

Функция ПроверкаКодОтвета(РезультатЗапроса, СсылкаНаФайл, ЭтоРазбор = Истина)
	
	Результат = Истина;
	
	Если РезультатЗапроса.КодСостояния < 200
		Или РезультатЗапроса.КодСостояния > 299 Тогда
		
		ЗафиксироватьОшибкиОбмена(СсылкаНаФайл, СтрШаблон("Ошибка запроса к серверу, код состояния [%1]",РезультатЗапроса.КодСостояния), ЭтоРазбор);
		Результат = Ложь;
	КонецЕсли; // Если РезультатЗапроса.КодСостояния < 200

	Возврат Результат;
	
КонецФункции // ПроверкаКодОтвета

#КонецОбласти

#Область ОчередьХранилищаФайлов

Функция ПолучитьОчередьНаОбработку(МаксимумПопыток, ВремяХраннения)
	
	Запрос 			= Новый Запрос;
	Запрос.Текст 	= "ВЫБРАТЬ
						|	РазборХранилищеФайлов.Объект
						|ИЗ
						|	РегистрСведений.РазборХранилищеФайлов КАК РазборХранилищеФайлов
						|ГДЕ
						|	РазборХранилищеФайлов.КоличествоПопыток <= &МаксимумПопыток
						|	И РазборХранилищеФайлов.ДатаДобавленияВОчередь < ДобавитьКДате(&ТекущаяДата, ЧАС, -&ВремяХраннения)";
	Запрос.УстановитьПараметр("МаксимумПопыток", 	МаксимумПопыток);
	Запрос.УстановитьПараметр("ВремяХраннения", 	ВремяХраннения);
	Запрос.УстановитьПараметр("ТекущаяДата", 		ТекущаяДатаСеанса());
	
	РезультатЗапроса = Запрос.Выполнить();
	ТаблицаРезультат = РезультатЗапроса.Выгрузить();
	
	Возврат ТаблицаРезультат.ВыгрузитьКолонку("Объект");

КонецФункции // ПолучитьОчередьНаОбработку

#КонецОбласти
 
Процедура ОбработатьЭлементРазбора(СсылкаНаФайл)
			
	// Отправить файл в хранилище
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("FileID", Строка(СсылкаНаФайл.УникальныйИдентификатор()));
	
	ТелоЗапроса = ПолучитьТелоЗапросаОтправкиФайла(СсылкаНаФайл);
	Если ТелоЗапроса = Неопределено Тогда
		Возврат;
	КонецЕсли; // Если ТелоЗапроса = Неопределено Тогда
	
	СтруктураСоединения = ПодготовитьСтруктуруСоединения(СсылкаНаФайл,"PutFile", ТелоЗапроса, ПараметрыЗапроса);
	Если СтруктураСоединения = Неопределено Тогда
		Возврат;
	КонецЕсли; // Если ТекущееСоединение = Неопределено Тогда
	
	// Выполенение запроса
	РезультатЗапроса = ВыполнитьМетодСоединения(СтруктураСоединения.ТекущееСоединение, "POST", СтруктураСоединения.Запрос, СсылкаНаФайл);
	Если РезультатЗапроса = Неопределено Тогда
		Возврат;
	КонецЕсли; // Если Результат = Неопределено Тогда
	
	// Разбор ответа
	Если Не ПроверкаКодОтвета(РезультатЗапроса, СсылкаНаФайл) Тогда
		Возврат;
	КонецЕсли; // Если Не ПроверкаКодОтвета(РезультатЗапроса, СсылкаНаФайл) Тогда
	
	// Получение тела ответа
	ТелоСтрокой = РезультатЗапроса.ПолучитьТелоКакСтроку();

	// Преобразование ответа
	Результат = ПреобразоватьИзСтрокиJSON(ТелоСтрокой, СсылкаНаФайл);	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли; // Если Результат = Неопределено Тогда
	
	// Проверка наличия свойства
	Если Не Результат.Свойство("ResultCode") Тогда
		ЗафиксироватьОшибкиОбмена(СсылкаНаФайл,"В ответе отсутствует поле [ResultCode]"); 
		Возврат;
	КонецЕсли; // Если Не Результат.Свойство("ResultCode") Тогда
	
	// Проверка ResultCode	
	Если Не Результат.ResultCode = "0301" Тогда
		Если Результат.Свойство("Error") Тогда
			Ошибки = Результат.Error;
		Иначе
			Ошибки = "Обмен завершен ошибкой";
		КонецЕсли; // Если Результат.Свойство("Error") Тогда
		ЗафиксироватьОшибкиОбмена(СсылкаНаФайл, Ошибки);
		Возврат;
	КонецЕсли; // Если Результат.ResultCode = "0301" Тогда 
		
	// Удалить двоичные данные из кеша
	Если Не УдалитьДвоичныеДанные(СсылкаНаФайл) Тогда
		Возврат;
	КонецЕсли; // Если Не УдалитьДвоичныеДанные(СсылкаНаФайл) Тогда
	
	УдалитьЗапись(СсылкаНаФайл);
	
КонецПроцедуры // ОбработатьЭлементРазбора
 
///////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура ОбработатьОчередьХранилищаФайлов() Экспорт
	
	Если Не Константы.ВыгружатьФайлыВоВнешнееХранилище.Получить() Тогда
		Возврат;
	КонецЕсли; // Если Не Константы.ВыгружатьФайлыВоВнешнееХранилище.Получить() Тогда
	
	МаксимумПопыток = 5;
	ВремяХраннения	= Справочники.НастройкиОбменаСХранилищемФайлов.ВремяХраненияВКеше();
		
	Для Каждого ЭлементОчереди Из ПолучитьОчередьНаОбработку(МаксимумПопыток, ВремяХраннения) Цикл
		ОбработатьЭлементРазбора(ЭлементОчереди);
	КонецЦикла; // Для Каждого ЭлементОчереди Из ПолучитьОчередьНаОбработку(МаксимумПопыток, ВремяХраннения) Цикл
	
КонецПроцедуры // ОбработатьОчередьХранилищаФайлов

Процедура ПоместитьДвоичныеДанныеИзХранилищаФайловВКеш(СсылкаНаФайл) Экспорт
	
	Если Не Константы.ВыгружатьФайлыВоВнешнееХранилище.Получить() Тогда
		Возврат;
	КонецЕсли; // Если Не Константы.ВыгружатьФайлыВоВнешнееХранилище.Получить() Тогда
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("FileID", Строка(СсылкаНаФайл.УникальныйИдентификатор()));
	
	СтруктураСоединения = ПодготовитьСтруктуруСоединения(СсылкаНаФайл, "GetFile",,ПараметрыЗапроса);
	Если СтруктураСоединения = Неопределено Тогда
		Возврат;
	КонецЕсли; // Если ТекущееСоединение = Неопределено Тогда
	
	// Выполенение запроса
	РезультатЗапроса = ВыполнитьМетодСоединения(СтруктураСоединения.ТекущееСоединение, "GET", СтруктураСоединения.Запрос, СсылкаНаФайл, Ложь);
	Если РезультатЗапроса = Неопределено Тогда
		Возврат;
	КонецЕсли; // Если Результат = Неопределено Тогда
	
	// Разбор ответа
	Если Не ПроверкаКодОтвета(РезультатЗапроса, СсылкаНаФайл, Ложь) Тогда
		Возврат;
	КонецЕсли; // Если Не ПроверкаКодОтвета(РезультатЗапроса, СсылкаНаФайл, Ложб) Тогда
	
	// Получение тела ответа
	ТелоСтрокой = РезультатЗапроса.ПолучитьТелоКакСтроку();

	// Преобразование ответа
	Результат = ПреобразоватьИзСтрокиJSON(ТелоСтрокой, СсылкаНаФайл, Ложь);	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли; // Если Результат = Неопределено Тогда
	
	// Проверка наличия свойства
	Если Не Результат.Свойство("ResultCode") Тогда
		ЗафиксироватьОшибкиОбмена(СсылкаНаФайл,"В ответе отсутствует поле [ResultCode]"); 
		Возврат;
	КонецЕсли; // Если Не Результат.Свойство("ResultCode") Тогда
	
	Если Не Результат.ResultCode = "0201" Тогда
		ЗафиксироватьОшибкиОбмена(СсылкаНаФайл, "Обмен завершен ошибкой", Ложь);
		Возврат;
	КонецЕсли; // Если Результат.ResultCode = "0201" Тогда 
	
	// Data
	Если Не Результат.Свойство("Data") Тогда
		ЗафиксироватьОшибкиОбмена(СсылкаНаФайл, "В ответе отсутствует поле [Data]", Ложь);
		Возврат;
	КонецЕсли; // Если Не Результат.Свойство("Data") Тогда
	
	// Extension
	Если Не Результат.Свойство("Extension") Тогда
		ЗафиксироватьОшибкиОбмена(СсылкаНаФайл, "В ответе отсутствует поле [Extension]", Ложь);
		Возврат;
	КонецЕсли; // Если Не Результат.Свойство("Extension") Тогда
	
	// Чтение данных
	СтруктураДанных = ДесериализоватьДвоичныеДанные(Результат.Data, СсылкаНаФайл); 
	Если СтруктураДанных = Неопределено Тогда
		Возврат;
	КонецЕсли; // Если СтруктураДанных = Неопределено Тогда
	
	// BinaryData
	Если Не СтруктураДанных.Свойство("BinaryData") Тогда
		ЗафиксироватьОшибкиОбмена(СсылкаНаФайл, "В ответе отсутствует поле [BinaryData]", Ложь);
		Возврат;
	КонецЕсли; // Если Не Результат.Data.Свойство("BinaryData") Тогда	
		
	// Обновим двоичные данные в кеше
	Если Не ОбновитьДанныеФайла(СтруктураДанных, СсылкаНаФайл) Тогда
		ЗафиксироватьОшибкиОбмена(СсылкаНаФайл, "Не удалось обновить данные файла", Ложь);
		Возврат;
	КонецЕсли; // Если Не ОбновитьДанныеФайла(СтруктураДанных, СсылкаНаФайл) Тогда
	
	РегистрыСведений.РазборХранилищеФайлов.ДобавитьЗапись(СсылкаНаФайл);
	
КонецПроцедуры // ПоместитьДвоичныеДанныеИзХранилищаФайловВКеш


