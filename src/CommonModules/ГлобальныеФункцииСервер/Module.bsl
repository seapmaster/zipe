////////////////////////////////////////////////////////////////////////////////
// Модуль предназначен для процедур и функций, включаемых в глобальный контекст
// Веденеев П.А. 25.12.2017
////////////////////////////////////////////////////////////////////////////////

//функция возвращает перевод из регистра переводов по заданным параметрам
//при отсутствии перевода возвращает представление владельца
Функция ПолучитьПеревод(Владелец, ИмяРеквизита, Язык) Экспорт
	
	Если Язык = Справочники.Языки.Русский Тогда
		
		Возврат Строка(Владелец);
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДополнительныеНаименования.Перевод КАК Перевод
	|ИЗ
	|	РегистрСведений.ДополнительныеНаименования КАК ДополнительныеНаименования
	|ГДЕ
	|	ДополнительныеНаименования.Владелец = &Владелец
	|	И ДополнительныеНаименования.ИмяРеквизита = &ИмяРеквизита
	|	И ДополнительныеНаименования.Язык = &Язык";
	Запрос.УстановитьПараметр("Владелец", Владелец);
	Запрос.УстановитьПараметр("ИмяРеквизита", ИмяРеквизита);
	Запрос.УстановитьПараметр("Язык", Язык);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		
		Возврат Строка(Владелец);
		
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.Перевод;
	
КонецФункции

//функция возвращает значение свойства владельца
//если владелец не имеет свойства с таким именем - возвращает Неопределено
Функция ПолучитьЗначениеСвойства(ВладелецСвойства, ИмяСвойства) Экспорт
	
	Структура = Новый Структура(ИмяСвойства, Неопределено);
	ЗаполнитьЗначенияСвойств(Структура, ВладелецСвойства);
	
	Возврат Структура[ИмяСвойства];
	
КонецФункции

//функция предоставляет возможность использовать метод СокрЛП() в СКД
Функция УдалитьПробелы(Строка) Экспорт
	
	Возврат СокрЛП(Строка);
	
КонецФункции