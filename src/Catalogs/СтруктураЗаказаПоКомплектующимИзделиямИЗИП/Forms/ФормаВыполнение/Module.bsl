
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ВыбраннаяКоманда", ВыбраннаяКоманда);
	
	Если ВыбраннаяКоманда = "Скопировать" Тогда
	
		Заголовок = "Выполняется копирование";	
	
	ИначеЕсли ВыбраннаяКоманда = "Перенести" Тогда
	
		Заголовок = "Выполняется перенос";
	
	ИначеЕсли ВыбраннаяКоманда = "Объединить" Тогда
	
		Заголовок = "Выполняется объединение";	
	
	КонецЕсли;
	
	СписокСсылокИсточников.ЗагрузитьЗначения(Параметры.МассивСсылокИсточников);
	
	Параметры.Свойство("СсылкаПриемник", СсылкаПриемник);
	Параметры.Свойство("СсылкаВладелец", СсылкаВладелец);
	Параметры.Свойство("ВладелецИсточников", ВладелецИсточников);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ВыбраннаяКоманда = "Скопировать" Тогда
	
		ВыполнитьКопирование();	
	
	ИначеЕсли ВыбраннаяКоманда = "Перенести" Тогда
	
		ВыполнитьПеренос();
	
	ИначеЕсли ВыбраннаяКоманда = "Объединить" Тогда
	
		ВыполнитьОбъединение();	
	
	КонецЕсли;
	
	ПоказатьДиалогПередЗакрытием = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьКопирование()

	НачатьКопированиеВФоне();
	
	ПодключитьОбработчикОжидания("ОбновитьПоказателиВыполнения", 5);

КонецПроцедуры // ВыполнитьКопирование()

&НаСервере
Процедура НачатьКопированиеВФоне()
	
	МассивСсылокИсточников = СписокСсылокИсточников.ВыгрузитьЗначения();

	АдресВременногоХранилища = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
	
	МассивПараметров = Новый Массив;
	МассивПараметров.Добавить(МассивСсылокИсточников);
	МассивПараметров.Добавить(ВладелецИсточников);
	МассивПараметров.Добавить(СсылкаПриемник);
	МассивПараметров.Добавить(СсылкаВладелец);
	МассивПараметров.Добавить(АдресВременногоХранилища);

	ФоновоеЗадание = ФоновыеЗадания.Выполнить("СтруктураЗаказаСервер.СкопироватьЭлементыСтруктуры", МассивПараметров, УникальныйИдентификатор, "Копирование элементов структуры кораблей");	

	ИдентификаторФоновогоЗадания = ФоновоеЗадание.УникальныйИдентификатор;	

КонецПроцедуры // НачатьКопированиеВФоне()

&НаКлиенте
Процедура ОбновитьПоказателиВыполнения()

	СообщенияФоновогоЗадания = ПолучитьСообщенияФоновогоЗадания();
	
	Если СообщенияФоновогоЗадания = Неопределено Тогда
	
		ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершениеВыполнения", ЭтотОбъект, Новый Структура("Выполнено", Ложь));
		
		ПоказатьПредупреждение(ОписаниеОповещения, "Фоновое задание не обнаружено. Обратитесь к администратору.",, "Фоновое задание не обнаружено");
		
		Возврат;
	
	КонецЕсли;
	
	КоличествоСообщений = СообщенияФоновогоЗадания.Количество();
	
	Если КоличествоСообщений = 0 Тогда
		
		Возврат;			
		
	КонецЕсли;
	
	ПоследнееСообщение = СообщенияФоновогоЗадания.Получить(КоличествоСообщений - 1).Текст;
	
	ПозицияЗапятой = Найти(ПоследнееСообщение, ",");
	
	Прогресс = Число(Лев(ПоследнееСообщение, ПозицияЗапятой - 1)); 
	
	Пояснение = Прав(ПоследнееСообщение, СтрДлина(ПоследнееСообщение) - ПозицияЗапятой);
	
	Если Прогресс = 100 Тогда
		
		ПоказатьДиалогПередЗакрытием = Ложь;
		
		ОтключитьОбработчикОжидания("ОбновитьПоказателиВыполнения");
		
		РезультатВыполнения = ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершениеВыполнения", ЭтотОбъект, Новый Структура("Выполнено", РезультатВыполнения.Выполнено));
		
		Если РезультатВыполнения.Выполнено Тогда
		
			Если ВыбраннаяКоманда = "Скопировать" Тогда
			
				ТекстСообщения = "Копирование выполнено успешно.";
				ТекстЗаголовка = "Копирование выполнено";		
			
			ИначеЕсли ВыбраннаяКоманда = "Объединить" Тогда
			
				ТекстСообщения = "Объединение выполнено успешно.";
				ТекстЗаголовка = "Объединение выполнено";
				
			КонецЕсли;	
			
			ПоказатьОповещениеПользователя(ТекстЗаголовка,,ТекстСообщения,БиблиотекаКартинок.Информация32);
			ЗавершениеВыполнения(Новый Структура("Выполнено", РезультатВыполнения.Выполнено));

		Иначе
		
			ТекстСообщения = РезультатВыполнения.Описание;
			ТекстЗаголовка = "Ошибка";
			
			ПоказатьПредупреждение(ОписаниеОповещения, ТекстСообщения, , ТекстЗаголовка);

		КонецЕсли;
		
				
	КонецЕсли;	

КонецПроцедуры // ОбновитьПоказателиВыполнения()

&НаКлиенте
Процедура ЗавершениеВыполнения(ДопПараметры) Экспорт

	Закрыть(ДопПараметры.Выполнено);	

КонецПроцедуры // ЗавершениеВыполнения()

&НаСервере
Функция ПолучитьСообщенияФоновогоЗадания()
	
	ФоновоеЗадание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(ИдентификаторФоновогоЗадания);
	
	Если ФоновоеЗадание = Неопределено Тогда
	
		Возврат Неопределено;	
	
	Иначе
	
		Возврат ФоновоеЗадание.ПолучитьСообщенияПользователю(Истина);	
	
	КонецЕсли;

КонецФункции // ПолучитьСообщенияФоновогоЗадания()

&НаКлиенте
Процедура ВыполнитьПеренос()
	
	РезультатВыполнения = СтруктураЗаказаСервер.ПеренестиЭлементыСтруктуры(СписокСсылокИсточников.ВыгрузитьЗначения(), СсылкаПриемник);
	
	Пояснение = "Перенесено элементов - " + Формат(РезультатВыполнения.ЧислоПереносимыхЭлементов, "ЧГ=");
	Прогресс = 100;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершениеВыполнения", ЭтотОбъект, Новый Структура("Выполнено", РезультатВыполнения.Выполнено));
	
	Если РезультатВыполнения.Выполнено Тогда
		
		ТекстСообщения = "Перенос выполнен успешно.";
		ТекстЗаголовка = "Перенос выполнен";		
				
	Иначе
		
		ТекстСообщения = РезультатВыполнения.Описание;
		ТекстЗаголовка = "Ошибка";
		
	КонецЕсли;
	
	ПоказатьПредупреждение(ОписаниеОповещения, ТекстСообщения,, ТекстЗаголовка);	

КонецПроцедуры // ВыполнитьПеренос()

&НаКлиенте
Процедура ВыполнитьОбъединение()

	НачатьОбъединениеВФоне();
	
	ПодключитьОбработчикОжидания("ОбновитьПоказателиВыполнения", 5);	

КонецПроцедуры // ВыполнитьОбъединение()

&НаСервере
Процедура НачатьОбъединениеВФоне()
	
	МассивСсылокИсточников = СписокСсылокИсточников.ВыгрузитьЗначения();

	АдресВременногоХранилища = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
	
	МассивПараметров = Новый Массив;
	МассивПараметров.Добавить(МассивСсылокИсточников);
	МассивПараметров.Добавить(СсылкаПриемник);
	МассивПараметров.Добавить(АдресВременногоХранилища);

	ФоновоеЗадание = ФоновыеЗадания.Выполнить("СтруктураЗаказаСервер.ОбъединитьЭлементыСтруктуры", МассивПараметров, УникальныйИдентификатор, "Объединение элементов структуры кораблей");	

	ИдентификаторФоновогоЗадания = ФоновоеЗадание.УникальныйИдентификатор;	

КонецПроцедуры // НачатьОбъединениеВФоне()

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Не ПоказатьДиалогПередЗакрытием Тогда
		Возврат;
	КонецЕсли;
	
	СообщенияФоновогоЗадания = ПолучитьСообщенияФоновогоЗадания();
	
	Если НЕ СообщенияФоновогоЗадания = Неопределено Тогда
		
		Отказ = Истина;
		Если ЗавершениеРаботы Тогда
			Возврат;
		КонецЕсли;
		
		ТекстВопроса = НСтр("ru = 'Обработка не завершена.
		|Прервать?'");
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.Прервать);
		Кнопки.Добавить(КодВозвратаДиалога.Пропустить, НСтр("ru = 'Не прерывать'"));
		Обработчик = Новый ОписаниеОповещения("ПослеПодтвержденияОтменыЗадания", ЭтотОбъект);
		ПоказатьВопрос(Обработчик, ТекстВопроса, Кнопки, 60, КодВозвратаДиалога.Пропустить);
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПодтвержденияОтменыЗадания(Ответ, ПараметрыВыполнения) Экспорт
	Если Ответ = КодВозвратаДиалога.Прервать Тогда
		ПоказатьДиалогПередЗакрытием = Ложь;
		Закрыть();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	ПриЗакрытииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПриЗакрытииНаСервере()
	
	ФоновоеЗадание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(ИдентификаторФоновогоЗадания);
	
	Если НЕ ФоновоеЗадание = Неопределено Тогда
	
		ФоновоеЗадание.Отменить();	
	
	КонецЕсли;
	
КонецПроцедуры
