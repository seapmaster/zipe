
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Параметры.Свойство("ПредметСнабжения") 
		//Или Не Параметры.Свойство("МассивСтруктурСоставныхЧастей") 
		Тогда
	
		Отказ = Истина;
		Возврат;
	
	КонецЕсли;
	
	Дерево = Новый ДеревоЗначений;
	Дерево.Колонки.Добавить("ПредметСнабжения");
	Дерево.Колонки.Добавить("Тип");
	Дерево.Колонки.Добавить("Количество");
	Дерево.Колонки.Добавить("ПричинаОтбораДляЗИП");
	Дерево.Колонки.Добавить("Взаимозаменяемость");
	
	// Первый уровень
	
	СтрокаПервогоУровня = Дерево.Строки.Добавить();
	СтрокаПервогоУровня.ПредметСнабжения = Параметры.ПредметСнабжения;
	
	// Второй уровень
	
	Цепочка = Новый Массив;
	Цепочка.Добавить(Параметры.ПредметСнабжения);
	
	Если Параметры.Свойство("МассивСтруктурСоставныхЧастей") Тогда
		Для каждого СтруктураСоставнойЧасти Из Параметры.МассивСтруктурСоставныхЧастей Цикл
			
			ПродолжитьЗаполнениеДерева(Цепочка, 
									СтрокаПервогоУровня, 
									СтруктураСоставнойЧасти.СоставнаяЧасть, 
									СтруктураСоставнойЧасти.ЗИП, 
									СтруктураСоставнойЧасти.Тип, 
									СтруктураСоставнойЧасти.Количество);
			
		КонецЦикла;
	Иначе
		
		СтруктураСоставнойЧасти = ВыбратьСоставныеЧасти(СтрокаПервогоУровня.ПредметСнабжения);
		Пока СтруктураСоставнойЧасти.Следующий() Цикл
			ПродолжитьЗаполнениеДерева(Цепочка, 
									СтрокаПервогоУровня, 
									СтруктураСоставнойЧасти.СоставнаяЧасть, 
									СтруктураСоставнойЧасти.ЗИП, 
									СтруктураСоставнойЧасти.Тип, 
									СтруктураСоставнойЧасти.Количество);
		КонецЦикла; 
		
	КонецЕсли;
		
	ЗначениеВРеквизитФормы(Дерево, "СтруктураПредметовСнабжения");
	
КонецПроцедуры

&НаСервере
Процедура ПродолжитьЗаполнениеДерева(Цепочка, Строка, СоставнаяЧасть, ЗИП, Тип, Количество)
	
	СтрокаНовогоУровня = Строка.Строки.Добавить();
	СтрокаНовогоУровня.ПредметСнабжения 	= СоставнаяЧасть;
	СтрокаНовогоУровня.Тип 					= Тип;
	СтрокаНовогоУровня.Количество 			= Количество;
	
	Если Не ЗИП И Цепочка.Найти(СоставнаяЧасть) = Неопределено Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	СпецификацииПредметовСнабжения.СоставляющаяЧасть КАК СоставляющаяЧасть,
		|	СпецификацииПредметовСнабжения.ЗИП КАК ЗИП,
		|	СпецификацииПредметовСнабжения.Количество КАК Количество,
		|	СпецификацииПредметовСнабжения.Тип КАК Тип
		|ИЗ
		|	РегистрСведений.СпецификацииПС КАК СпецификацииПредметовСнабжения
		|ГДЕ
		|	СпецификацииПредметовСнабжения.ПредметСнабжения = &ПредметСнабжения
		|
		|УПОРЯДОЧИТЬ ПО
		|	СпецификацииПредметовСнабжения.НомерПозиции";
		
		Запрос.УстановитьПараметр("ПредметСнабжения", СоставнаяЧасть);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Если РезультатЗапроса.Пустой() Тогда
		
			Возврат;	
		
		КонецЕсли;
		
		Цепочка.Добавить(СоставнаяЧасть);
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			НоваяЦепочка = Новый Массив;
			
			Для каждого Элемент Из Цепочка Цикл
			
				НоваяЦепочка.Добавить(Элемент);	
			
			КонецЦикла;
			
			ПродолжитьЗаполнениеДерева(НоваяЦепочка, 
										СтрокаНовогоУровня, 
										Выборка.СоставляющаяЧасть, 
										Выборка.ЗИП, 
										Выборка.Тип, 
										Выборка.Количество);	
			
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры // ПродолжитьЗаполнениеДерева()

&НаСервере
Функция ВыбратьСоставныеЧасти(ПредметСнабжения)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СпецификацииПредметовСнабжения.СоставляющаяЧасть КАК СоставнаяЧасть,
	|	СпецификацииПредметовСнабжения.ЗИП КАК ЗИП,
	|	СпецификацииПредметовСнабжения.Тип КАК Тип,
	|	СпецификацииПредметовСнабжения.Количество КАК Количество
	|ИЗ
	|	РегистрСведений.СпецификацииПС КАК СпецификацииПредметовСнабжения
	|ГДЕ
	|	СпецификацииПредметовСнабжения.ПредметСнабжения = &ПредметСнабжения
	|
	|УПОРЯДОЧИТЬ ПО
	|	СпецификацииПредметовСнабжения.НомерПозиции";
	
	Запрос.УстановитьПараметр("ПредметСнабжения", ПредметСнабжения);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выбрать();
	
КонецФункции