
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Параметры.Свойство("ПредметСнабжения") Тогда
	
		Отказ = Истина;
		Возврат;	
	
	КонецЕсли;
	
	ПредметСнабжения = Параметры.ПредметСнабжения;
	
	Если Не ЗначениеЗаполнено(ПредметСнабжения) Тогда
	
		Отказ = Истина;
		Возврат;		
	
	КонецЕсли;
	
	ЗаполнитьИсториюСпецификации();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИсториюСпецификации()

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	СпецификацииПредметовСнабженияИстория.Период КАК Версия,
	|	СпецификацииПредметовСнабженияИстория.ПредметСнабжения,
	|	СпецификацииПредметовСнабженияИстория.СоставляющаяЧасть,
	|	СпецификацииПредметовСнабженияИстория.ЗИП,
	|	СпецификацииПредметовСнабженияИстория.Количество,
	|	СпецификацииПредметовСнабженияИстория.НомерПозиции КАК НомерПозиции,
	|	СпецификацииПредметовСнабженияИстория.Тип,
	|	СпецификацииПредметовСнабженияИстория.ОбозначениеСНК,
	|	СпецификацииПредметовСнабженияИстория.ПричинаВыбораДляЗИП,
	|	СпецификацииПредметовСнабженияИстория.УказательНаЭлементСхемы,
	|	СпецификацииПредметовСнабженияИстория.Взаимозаменяемость,
	|	СпецификацииПредметовСнабженияИстория.ПризнакПодбора,
	|	СпецификацииПредметовСнабженияИстория.ОбозначениеЗаготовки,
	|	СпецификацииПредметовСнабженияИстория.СведенияОбИзменениях,
	|	СпецификацииПредметовСнабженияИстория.Автор КАК Автор
	|ИЗ
	|	РегистрСведений.СпецификацииПС КАК СпецификацииПредметовСнабженияИстория
	|ГДЕ
	|	СпецификацииПредметовСнабженияИстория.ПредметСнабжения = &ПредметСнабжения
	|
	|УПОРЯДОЧИТЬ ПО
	|	Версия,
	|	НомерПозиции
	|ИТОГИ
	|	МАКСИМУМ(Автор)
	|ПО
	|	Версия";
	
	Запрос.УстановитьПараметр("ПредметСнабжения", ПредметСнабжения);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
	
		Возврат;	
	
	КонецЕсли;
	
	ЗначениеВРеквизитФормы(РезультатЗапроса.Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам), "ИсторияСпецификации");	

КонецПроцедуры // ЗаполнитьИсториюСпецификации()

