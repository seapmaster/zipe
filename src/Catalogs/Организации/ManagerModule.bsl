//////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

///////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Функция НайтиПоИНН(ИНН) Экспорт
	
	Результат 		= Справочники.Организации.ПустаяСсылка();
	
	Если Не ЗначениеЗаполнено(ИНН) Тогда
		Возврат Результат;
	КонецЕсли; // Если Не ЗначениеЗаполнено(ИНН) Тогда
	
	Запрос 			= Новый Запрос;
	Запрос.Текст 	= "ВЫБРАТЬ
	             	  |	Организации.Ссылка КАК Ссылка
	             	  |ИЗ
	             	  |	Справочник.Организации КАК Организации
	             	  |ГДЕ
	             	  |	Организации.ИНН = &ИНН
	             	  |	И НЕ Организации.ПометкаУдаления";
	Запрос.УстановитьПараметр("ИНН", ИНН);
	
	Выборка 		= Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат 	= Выборка.Ссылка;
	КонецЕсли; // Если Выборка.Следующий() Тогда
	
	Возврат Результат;
	
КонецФункции // НайтиПоИНН

Функция Создать(ИНН, Наименование, Адрес = "") Экспорт
	
	Результат 		= Справочники.Организации.ПустаяСсылка();
	
	Если Не ЗначениеЗаполнено(ИНН) Тогда
		Возврат Результат;
	КонецЕсли; // Если Не ЗначениеЗаполнено(ИНН)
	
	Объект 				= Справочники.Организации.СоздатьЭлемент();
	Объект.ИНН 			= ИНН;
	Объект.Наименование = Наименование;
	Объект.Название 	= Наименование;
	
	Попытка
		Объект.Записать();
		Результат 		= Объект.Ссылка;
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрШаблон("При создании контрагента %1 (ИНН: %2) произошла ошибка: %3", Наименование, ИНН, ОписаниеОшибки()));
	КонецПопытки;
	
	Если Не ПустаяСтрока(Адрес) И ЗначениеЗаполнено(Результат) Тогда
		
		СтруктураКонтактнойИнформации = Новый Структура;
		СтруктураКонтактнойИнформации.Вставить("Представление", Адрес);
		
		Попытка
			УправлениеКонтактнойИнформацией.ЗаполнитьКонтактнуюИнформациюОбъекта(Результат, 
											Справочники.ВидыКонтактнойИнформации.ПочтовыйАдрес,
											СтруктураКонтактнойИнформации);
		Исключение
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрШаблон("При записи адреса контрагенту %1 (ИНН: %2) произошла ошибка: %3", Наименование, ИНН, ОписаниеОшибки()));									
		КонецПопытки;
		
	КонецЕсли; // Если Не ПустаяСтрока(Адрес) И ЗначениеЗаполнено(Результат) Тогда
	
	Возврат Результат;
	
КонецФункции // Создать
