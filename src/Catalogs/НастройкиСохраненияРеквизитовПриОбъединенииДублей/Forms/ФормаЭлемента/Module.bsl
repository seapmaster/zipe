
///////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Объект.ИмяОбъекта) Тогда
	
		ЗаполнитьСписокРеквизитов(Объект.ИмяОбъекта, Элементы.РеквизитыИмя.СписокВыбора);
	
	КонецЕсли; 	
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяОбъектаПриИзменении(Элемент)
	
	ИмяОбъектаПриИзмененииНаСервере();	

КонецПроцедуры

///////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ИмяОбъектаПриИзмененииНаСервере()
	
	Попытка
	
		ЗаполнитьСписокРеквизитов(Объект.ИмяОбъекта, Элементы.РеквизитыИмя.СписокВыбора);

	Исключение
		
		ТекстСообщения = НСтр("ru = 'Ошибка получения списка имен реквизитов! Проверьте имя объекта.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "ИмяОбъекта"); 
		
	КонецПопытки;  	

КонецПроцедуры 

&НаСервере
Процедура ЗаполнитьСписокРеквизитов(ИмяОбъекта, СписокРеквизитов)
	
	СписокРеквизитов.Очистить();
		
	Реквизиты = Неопределено;
	
	Реквизиты = Метаданные.Справочники[ИмяОбъекта].Реквизиты;
	
	Для каждого Реквизит Из Реквизиты Цикл
		
		Если Реквизит.Тип.СодержитТип(Тип("ХранилищеЗначения")) Тогда 
			
			Продолжить;
			
		КонецЕсли;
	
		СписокРеквизитов.Добавить(Реквизит.Имя, Реквизит.Синоним);
		
	КонецЦикла;
	
	СтандартныеРеквизиты = Неопределено;
	
	СтандартныеРеквизиты = Метаданные.Справочники[ИмяОбъекта].СтандартныеРеквизиты;	
	
	Для каждого СтандартныйРеквизит Из СтандартныеРеквизиты Цикл
		
		Если СтандартныйРеквизит.Имя = "Ссылка" Тогда
		
			Продолжить;	
		
		КонецЕсли;
	
		СписокРеквизитов.Добавить(СтандартныйРеквизит.Имя, ?(ЗначениеЗаполнено(СтандартныйРеквизит.Синоним), СтандартныйРеквизит.Синоним, ОбщиеФункцииСервер.ПолучитьСиноним(СтандартныйРеквизит.Имя)));	
		
	КонецЦикла;

	ТабличныеЧасти = Метаданные.Справочники[ИмяОбъекта].ТабличныеЧасти;
	
	Для каждого ТабличнаяЧасть Из ТабличныеЧасти Цикл
		
		СписокРеквизитов.Добавить("ТабличнаяЧасть." + ТабличнаяЧасть.Имя, "Табличная часть: " + ТабличнаяЧасть.Синоним);
		
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура РеквизитыИмяПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Реквизиты.ТекущиеДанные;
	Если НЕ ТекущаяСтрока = Неопределено Тогда
		
		ТекущаяСтрока.Представление = Элементы.РеквизитыИмя.СписокВыбора.НайтиПоЗначению(ТекущаяСтрока.Имя).Представление;		
	
	КонецЕсли; 
	
КонецПроцедуры
