/////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

/////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД

&НаКлиенте
Процедура КомандаПоказатьВложения(Команда)
	ПараметрыФормы 	= Новый Структура;
	ПараметрыФормы.Вставить("ВладелецФайла", 	Объект.Предмет);	
	ПараметрыФормы.Вставить("ТолькоПросмотр", 	Истина);	
	ОткрытьФорму("ОбщаяФорма.ПрисоединенныеФайлы", ПараметрыФормы, ЭтаФорма);
КонецПроцедуры // КомандаПоказатьВложения

/////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ТабличныйДокумент 							= Документы.ЗаявкаФСВТС.СформироватьТабличныйДокументЗаявки(Объект.Предмет);
	РегиональныйРуководитель 					= Объект.БизнесПроцесс.РегиональныйРуководитель;
	Если Не ЗначениеЗаполнено(РегиональныйРуководитель) Тогда
		РегиональныйРуководитель				= РегистрыСведений.РегиональныеРуководителиФСВТС.ПолучитьРуководителя(Объект.Предмет.Заказчик);
		Модифицированность						= Истина;
	КонецЕсли; // Если Не ЗначениеЗаполнено(РегиональныйРуководитель) Тогда	
	Элементы.КомандаПоказатьВложения.Заголовок 	= Справочники.ЗаявкаФСВТСПрисоединенныеФайлы.ПолучитьКоличество(Объект.Предмет);
КонецПроцедуры // ПриСозданииНаСервере

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	БизнесПроцесс 							= ТекущийОбъект.БизнесПроцесс.ПолучитьОбъект();
	БизнесПроцесс.РегиональныйРуководитель 	= РегиональныйРуководитель;
	БизнесПроцесс.Записать();
КонецПроцедуры // ПриЗаписиНаСервере

&НаКлиенте
Процедура РегиональныйРуководительПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры // РегиональныйРуководительПриИзменении

&НаКлиенте
Процедура ПередВыполнением(Отказ)
	Если Модифицированность Тогда
		Попытка
			Записать();
			Модифицированность = Ложь;
		Исключение
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не удалось записать задачу: " + ОписаниеОшибки(),,,,Отказ);
		КонецПопытки;		
	КонецЕсли; // Если Модифицированность Тогда	
КонецПроцедуры // ПередВыполнением


