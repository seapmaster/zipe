/////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

/////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД

&НаКлиенте
Процедура КомандаПоказатьВложения(Команда)
	ПараметрыФормы 	= Новый Структура;
	ПараметрыФормы.Вставить("ВладелецФайла", 	Объект.Предмет);	
	ПараметрыФормы.Вставить("ТолькоПросмотр", 	Истина);	
	ОткрытьФорму("ОбщаяФорма.ПрисоединенныеФайлы", ПараметрыФормы, ЭтаФорма);
КонецПроцедуры // КомандаПоказатьВложения

/////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ТабличныйДокумент 							= Документы.ЗаявкаФСВТС.СформироватьТабличныйДокументЗаявки(Объект.Предмет, Истина);
	ИсторияСогласования 						= Документы.ЗаявкаФСВТС.СформироватьТабличныйДокументИсторииСогласования(Объект.БизнесПроцесс);
	Элементы.КомандаПоказатьВложения.Заголовок 	= Справочники.ЗаявкаФСВТСПрисоединенныеФайлы.ПолучитьКоличество(Объект.Предмет);
	НайденныеВизы 								= Объект.БизнесПроцесс.УстановленныеВизы.НайтиСтроки(Новый Структура("Исполнитель", Объект.Исполнитель));
	Если НайденныеВизы.Количество() > 0 Тогда
		Виза 		= НайденныеВизы[0].Виза;
		Комментарий = НайденныеВизы[0].Комментарий;
	КонецЕсли; // Если НайденныеВизы.Количество() > 0 Тогда	
КонецПроцедуры // ПриСозданииНаСервере

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	БизнесПроцесс 		= ТекущийОбъект.БизнесПроцесс.ПолучитьОбъект();
	НайденныеВизы 		= БизнесПроцесс.УстановленныеВизы.НайтиСтроки(Новый Структура("Исполнитель", Объект.Исполнитель));
	Если НайденныеВизы.Количество() = 0 Тогда
		СтрокаВизы 				= БизнесПроцесс.УстановленныеВизы.Добавить();
		СтрокаВизы.Исполнитель 	= Объект.Исполнитель;
	Иначе
		СтрокаВизы 				= НайденныеВизы[0];
	КонецЕсли; // Если НайденныеВизы.Количество() = 0 Тогда
	
	СтрокаВизы.Виза 		= Виза;
	СтрокаВизы.Дата 		= ТекущаяДата();
	СтрокаВизы.Комментарий 	= Комментарий;
	БизнесПроцесс.Записать();
КонецПроцедуры // ПриЗаписиНаСервере

&НаКлиенте
Процедура ВизаПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры // ВизаПриИзменении

&НаКлиенте
Процедура КомментарийПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры // КомментарийПриИзменении

&НаКлиенте
Процедура ПередВыполнением(Отказ)
	Если Модифицированность Тогда
		Попытка
			Записать();
			Модифицированность = Ложь;
		Исключение
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не удалось записать задачу: " + ОписаниеОшибки(),,,,Отказ);
		КонецПопытки;		
	КонецЕсли; // Если Модифицированность Тогда	
КонецПроцедуры // ПередВыполнением


