////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТОЧЕК МАРШРУТА

#Область УстановкаВизы

Процедура УстановкаВизыПриСозданииЗадач(ТочкаМаршрутаБизнесПроцесса, ФормируемыеЗадачи, Отказ)
	Для Каждого Задача Из ФормируемыеЗадачи Цикл
		Задача.Автор 		= Автор;
		Задача.Важность 	= Перечисления.ВариантыВажностиЗадачи.Обычная;
		Задача.Предмет 		= Заявка;
		Задача.ДатаНачала	= ТекущаяДата();
		Задача.Описание		= "Необходимо согласовать распределение заявки";
	КонецЦикла; // Для Каждого Задача Из ФормируемыеЗадачи Цикл	
КонецПроцедуры // УстановкаВизыПриСозданииЗадач

Процедура УстановкаВизыПередВыполнением(ТочкаМаршрутаБизнесПроцесса, Задача, Отказ)
	НайденныеВизы 	= УстановленныеВизы.НайтиСтроки(Новый Структура("Исполнитель", Задача.Исполнитель));
	Если НайденныеВизы.Количество() = 0 
		Или Не ЗначениеЗаполнено(НайденныеВизы[0].Виза) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не установлена виза!",,,"Виза", Отказ);
	ИначеЕсли Не НайденныеВизы[0].Виза = Перечисления.ВизыСогласования.Согласовано
		И Не ЗначениеЗаполнено(НайденныеВизы[0].Комментарий) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Для данной визы должен быть установлен комментарий!",,,"Комментарий", Отказ);
	КонецЕсли; // Если НайденныеВизы.Количество() = 0 Тогда
		
КонецПроцедуры // УстановкаВизыПередВыполнением

Процедура УстановкаВизыПриВыполнении(ТочкаМаршрутаБизнесПроцесса, Задача, Отказ)
	Согласовано = Истина;
	Для Каждого Строка Из УстановленныеВизы Цикл
		Если Строка.Виза = Перечисления.ВизыСогласования.НеСогласовано Тогда
			Согласовано = Ложь;
		КонецЕсли; // Если Строка.Виза = Перечисления.ВизыСогласования.НеСогласовано Тогда		
	КонецЦикла; // Для Каждого Стока Из УстановленныеВизы Цикл	
КонецПроцедуры // УстановкаВизыПриВыполнении

#КонецОбласти

#Область Завершение

Процедура ЗавершениеПриЗавершении(ТочкаМаршрутаБизнесПроцесса, Отказ)
	БизнесПроцесс 							= Основание.ПолучитьОбъект();
	БизнесПроцесс.РаспределениеСогласовано 	= Согласовано;
	БизнесПроцесс.Записать();
КонецПроцедуры // ЗавершениеПриЗавершении

#КонецОбласти

////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ БИЗНЕС ПРОЦЕССА

Процедура ПередЗаписью(Отказ)
	Если Не ЗначениеЗаполнено(Автор) Тогда
		Автор = Пользователи.АвторизованныйПользователь();
	КонецЕсли; // Если Не ЗначениеЗаполнено(Автор) Тогда
	
	Если Не ЗначениеЗаполнено(Наименование) Тогда
		Наименование = "Согласование распределения заявки №" + Заявка.Номер 
												+ " от " 
												+ Формат(Заявка.Дата, "ДЛФ=DD")	
												+ "/"
												+ Заявка.Заказчик
												+ "/"
												+ Заявка.НомерЗаказчика;
	КонецЕсли; // Если Не ЗначениеЗаполнено(Наименование) Тогда	
КонецПроцедуры // ПередЗаписью






