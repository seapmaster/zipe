
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("АдресНастроекОтбораПоЗаказу", АдресНастроекОтбораПоЗаказу);
	
	Если ЗначениеЗаполнено(АдресНастроекОтбораПоЗаказу) Тогда
	
		СтруктураОтбораПоЗаказу = ПолучитьИзВременногоХранилища(АдресНастроекОтбораПоЗаказу);
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтруктураОтбораПоЗаказу, "Применить,ВидСравнения");
		
		Если ВидСравнения = "ВСписке" Или ВидСравнения = "НеВСписке" Тогда
		
			СписокЗначенийОтбора.ЗагрузитьЗначения(СтруктураОтбораПоЗаказу.МассивЗначенийОтбора);
			
		Иначе
			
			ЗначениеОтбора = СтруктураОтбораПоЗаказу.ЗначениеОтбора;	
		
		КонецЕсли;
		
	Иначе
		
		Применить = Истина;
		ВидСравнения = "Равно";
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ВидСравненияПриИзменении();	
	
КонецПроцедуры

&НаКлиенте
Процедура ВидСравненияПриИзменении(Элемент = Неопределено)
	
	Если ВидСравнения = "ВСписке" Или ВидСравнения = "НеВСписке" Тогда
	
		Элементы.ЗначениеОтбора.Видимость = Ложь;
		Элементы.СписокЗначенийОтбора.Видимость = Истина;
	
	Иначе
	
		Элементы.ЗначениеОтбора.Видимость = Истина;
		Элементы.СписокЗначенийОтбора.Видимость = Ложь;	
	
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	Если ЗначениеЗаполнено(АдресНастроекОтбораПоЗаказу) Тогда
	
		УдалитьИзВременногоХранилища(АдресНастроекОтбораПоЗаказу);	
	
	КонецЕсли;
	
	СтруктураОтбораПоЗаказу = Новый Структура;
	СтруктураОтбораПоЗаказу.Вставить("Применить", Применить);
	СтруктураОтбораПоЗаказу.Вставить("ВидСравнения", ВидСравнения);
	
	Если ВидСравнения = "ВСписке" Или ВидСравнения = "НеВСписке" Тогда
	
		СтруктураОтбораПоЗаказу.Вставить("МассивЗначенийОтбора", СписокЗначенийОтбора.ВыгрузитьЗначения());		
	
	Иначе
	
		СтруктураОтбораПоЗаказу.Вставить("ЗначениеОтбора", ЗначениеОтбора);	
	
	КонецЕсли;
	
	Закрыть(ПоместитьВоВременноеХранилище(СтруктураОтбораПоЗаказу, Новый УникальныйИдентификатор));
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры
