&НаСервере
Процедура СкопироватьСтруктуруНаСервере(Родитель,СоответствиеЭлементов = Неопределено)
	
	Если СоответствиеЭлементов = Неопределено Тогда
		СоответствиеЭлементов = Новый Соответствие();
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СтруктураЗаказаПоКомплектующимИзделиямИЗИП.Ссылка КАК Ссылка,
		|	СтруктураЗаказаПоКомплектующимИзделиямИЗИП.Родитель КАК Родитель,
		|	ДополнительныеНаименования.ИмяРеквизита КАК ИмяРеквизита,
		|	ДополнительныеНаименования.Язык КАК Язык,
		|	ДополнительныеНаименования.Перевод КАК Перевод
		|ИЗ
		|	Справочник.СтруктураЗаказаПоКомплектующимИзделиямИЗИП КАК СтруктураЗаказаПоКомплектующимИзделиямИЗИП
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеНаименования КАК ДополнительныеНаименования
		|		ПО (ДополнительныеНаименования.Владелец = СтруктураЗаказаПоКомплектующимИзделиямИЗИП.Ссылка)
		|			И (ДополнительныеНаименования.ИмяРеквизита = ""Наименование"")
		|ГДЕ
		|	СтруктураЗаказаПоКомплектующимИзделиямИЗИП.Владелец = &Корабль
		|	И СтруктураЗаказаПоКомплектующимИзделиямИЗИП.Ссылка В ИЕРАРХИИ(&Родитель)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка
		|ИТОГИ ПО
		|	Ссылка ИЕРАРХИЯ";
	
	Запрос.УстановитьПараметр("Корабль",ЗаказИсточник);
	Запрос.УстановитьПараметр("Родитель",Родитель);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаЭлемент = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Сч = 0;
	НачатьТранзакцию();
	
	Пока ВыборкаЭлемент.Следующий() Цикл
		
		Сч = Сч + 1;
		
		Если СоответствиеЭлементов[ВыборкаЭлемент.Ссылка] = Неопределено Тогда
			
			НовыйЭлемент = ВыборкаЭлемент.Ссылка.Скопировать();
			НовыйЭлемент.Владелец = ЗаказПолучатель;
			НовыйЭлемент.Родитель = СоответствиеЭлементов[ВыборкаЭлемент.Родитель];
			НовыйЭлемент.Записать();
			
			СоответствиеЭлементов[ВыборкаЭлемент.Ссылка] = НовыйЭлемент.Ссылка;
			
			ВыборкаДетальныеЗаписи = ВыборкаЭлемент.Выбрать();
			
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				
				Если Не ВыборкаДетальныеЗаписи.Язык = NULL Тогда
					
					МЗ = РегистрыСведений.ДополнительныеНаименования.СоздатьМенеджерЗаписи();
					ЗаполнитьЗначенияСвойств(МЗ, ВыборкаДетальныеЗаписи);
					МЗ.Владелец = НовыйЭлемент.Ссылка;
					МЗ.Записать();	
					
				КонецЕсли;
				
			КонецЦикла;
		КонецЕсли; 
		
		Если Сч % 100 = 0 ИЛИ Сч = ВыборкаЭлемент.Количество() Тогда
			
			ЗафиксироватьТранзакцию();
			
			Если НЕ Сч = ВыборкаЭлемент.Количество() Тогда
				
				НачатьТранзакцию();
				
			КонецЕсли; 
			
		КонецЕсли; 
		
		
	КонецЦикла;
	
	
КонецПроцедуры

&НаСервере
Функция ПолучитьОбщееКоличество()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СтруктураЗаказаПоКомплектующимИзделиямИЗИП.Ссылка КАК Родитель,
		|	0 КАК Количество
		|ИЗ
		|	Справочник.СтруктураЗаказаПоКомплектующимИзделиямИЗИП КАК СтруктураЗаказаПоКомплектующимИзделиямИЗИП
		|ГДЕ
		|	СтруктураЗаказаПоКомплектующимИзделиямИЗИП.Владелец = &Корабль
		|	И СтруктураЗаказаПоКомплектующимИзделиямИЗИП.Родитель = ЗНАЧЕНИЕ(Справочник.СтруктураЗаказаПоКомплектующимИзделиямИЗИП.ПустаяСсылка)
		|";
	
	Запрос.УстановитьПараметр("Корабль",ЗаказИсточник);
	
	ТаблицаРодителейВрем = Запрос.Выполнить().Выгрузить();
	
	ОбщееКоличество = 0;
	
	Для каждого Стр Из ТаблицаРодителейВрем Цикл
		
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	Количество(СтруктураЗаказаПоКомплектующимИзделиямИЗИП.Ссылка) КАК Количество
			|ИЗ
			|	Справочник.СтруктураЗаказаПоКомплектующимИзделиямИЗИП КАК СтруктураЗаказаПоКомплектующимИзделиямИЗИП
			|	
			|ГДЕ
			|	СтруктураЗаказаПоКомплектующимИзделиямИЗИП.Ссылка В ИЕРАРХИИ (&Родитель)
			|	И СтруктураЗаказаПоКомплектующимИзделиямИЗИП.Владелец = &Корабль
			|";
		Запрос.УстановитьПараметр("Родитель",Стр.Родитель);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда
			Стр.Количество = Выборка.Количество;
		КонецЕсли; 
		
	КонецЦикла; 
	
	ЗначениеВРеквизитФормы(ТаблицаРодителейВрем,"ТаблицаРодителей");
	
	Возврат ТаблицаРодителейВрем.Итог("Количество");
	
КонецФункции


&НаКлиенте
Процедура Скопировать(Команда)
	
	Сообщить("" + ТекущаяДата() + ": Начало копирования " + ЗаказИсточник);
	
	ОбщееКоличество = ПолучитьОбщееКоличество();
	
	ВремяНачалаОбработки = ТекущаяДата(); 
	
	ТекСчетчик = 0;
	
	Для каждого Стр Из ТаблицаРодителей Цикл
		
		Порция = Стр.Количество;
		
		УправлениеИнтерфейсом.ВывестиТекущееСостояние("Копирование структуры корабля",ВремяНачалаОбработки,ТекСчетчик,ОбщееКоличество);
		
		СкопироватьСтруктуруНаСервере(Стр.Родитель);
		
		ТекСчетчик = МИН(ТекСчетчик + Порция,ОбщееКоличество);
		
	КонецЦикла; 
	
	СтрПрошло = "";
	Если ТекСчетчик Тогда
		УправлениеИнтерфейсом.ВывестиТекущееСостояние("Копирование структуры корабля",ВремяНачалаОбработки,ОбщееКоличество,ОбщееКоличество,СтрПрошло);
	КонецЕсли; 

	Сообщить("" + ТекущаяДата() + ": Окончание копирования " + ЗаказИсточник + "(затрачено: " + СтрПрошло + ")");
	
КонецПроцедуры
