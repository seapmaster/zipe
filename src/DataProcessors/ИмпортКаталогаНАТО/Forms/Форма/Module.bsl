
#Область СобытияФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьКнопкиКоманднойПанели();
	
	Элементы.ГруппаЗагруженныеДанные.Видимость = ПоказатьЗагруженныеДанные;
			
	ВариантЗагрузки = Элементы.ВариантЗагрузки.СписокВыбора[0];
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьДанныеСразуПослеЧтенияПриИзменении(Элемент)
	
	УстановитьКнопкиКоманднойПанели();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьЗагруженныеДанныеПриИзменении(Элемент)
	
	Элементы.ГруппаЗагруженныеДанные.Видимость = ПоказатьЗагруженныеДанные;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолноеИмяФайлаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ВыборФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	
	ВыборФайла.Показать(Новый ОписаниеОповещения("ПолноеИмяФайлаНачалоВыбораЗавершение", ЭтаФорма));
	
КонецПроцедуры

&НаКлиенте
Процедура ПолноеИмяФайлаНачалоВыбораЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если Не ВыбранныеФайлы = Неопределено Тогда
		
		ПолноеИмяФайла = ВыбранныеФайлы[0];
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура УстановитьКнопкиКоманднойПанели()
	
	Если ЗаписатьДанныеСразуПослеЧтения Тогда
		
		КоманднаяПанель.ПодчиненныеЭлементы.ФормаПрочитатьДанные.Заголовок = "Импортировать данные";
		КоманднаяПанель.ПодчиненныеЭлементы.ФормаЗаписатьДанные.Видимость = Ложь;
		
	Иначе
		
		КоманднаяПанель.ПодчиненныеЭлементы.ФормаПрочитатьДанные.Заголовок = "Прочитать данные";
		КоманднаяПанель.ПодчиненныеЭлементы.ФормаЗаписатьДанные.Видимость = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

//ПРОЦЕДУРЫ И ФУНКЦИИ ЧТЕНИЯ ДАННЫХ ИЗ ФАЙЛА
#Область ЧтениеДанных

&НаКлиенте
Процедура ПрочитатьДанные(Команда)
	
	Если Не ЗначениеЗаполнено(ПолноеИмяФайла) Тогда
		
		ПоказатьПредупреждение(, "Выберите файл с данными!");
		Возврат;
		
	КонецЕсли;
	
	Файл = Новый Файл(ПолноеИмяФайла);
	
	ИмяФайла = Файл.Имя;
	ПутьКФайлу = Файл.Путь;
	
	Файл.НачатьПроверкуСуществования(Новый ОписаниеОповещения("ПрочитатьДанныеПродолжение", ЭтаФорма));
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьДанныеПродолжение(Существует, ДополнительныеПараметры) Экспорт
	
	Если Существует Тогда
		
		ТекстВопроса = ?(ЗаписатьДанныеСразуПослеЧтения, "Данные в базе данных будут изменены. Продолжить?", "Результат предыдущего чтения будет очищен. Продолжить?");
	
		ПоказатьВопрос(Новый ОписаниеОповещения("ПрочитатьДанныеЗавершение", ЭтаФорма), ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	Иначе
		
		ПоказатьПредупреждение(, "Файл не обнаружен!");
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьДанныеЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		ВыполнитьЧтениеДанных();
		
		Если ЗаписатьДанныеСразуПослеЧтения Тогда
			
			ВыполнитьЗаписьДанных();
			
		КонецЕсли;
		
	Иначе
		
		Сообщить("Действие отменено!");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьЧтениеДанных()
	
	Сообщить("Чтение данных начато в " + ТекущаяДата());
	
	ЗагруженныеДанные.Очистить();
	
	Если ВариантЗагрузки = "1С" Тогда //читаем через 1С
		
		ПараметрыФайла = РаботаСФайламиOfficeКлиент.ПодготовитьФайлКЧтению(ПолноеИмяФайла, "ТабличныйДокумент");
		Если ПараметрыФайла = Неопределено Тогда
			Возврат;
		КонецЕсли; // Если ПараметрыФайла = Неопределено Тогда
		
		ЛистыДокумента = РаботаСФайламиOffice.ПрочитатьВсеЛистыФайла(ПараметрыФайла);
		Если ЛистыДокумента = Неопределено Тогда
			Возврат;
		КонецЕсли; // Если ЛистыДокумента = Неопределено Тогда
		
		Для Каждого ОписаниеЛиста Из ЛистыДокумента Цикл
			Лист = ОписаниеЛиста.Значение;
			Если ЗначениеЗаполнено(СокрЛП(Строка(Лист.Область(1,1).Текст))) Тогда
				ВызватьЧтениеДанных(Лист);
				Прервать;
			КонецЕсли; // Если ЗначениеЗаполнено(СокрЛП(Строка(Лист.Область(1,1).Текст))) Тогда
		КонецЦикла; // Для Каждого ОписаниеЛиста Из ЛистыДокумента Цикл
				
	Иначе //читаем через ODBC-драйвер 
#Область ODBC
		СтрокаПодключения = "Provider=MSDASQL.1;Persist Security Info=False;Extended Properties=""DSN=Excel Files;DBQ=&Filename;DefaultDir=&Foldername;DriverId=1046;MaxBufferSize=2048;PageTimeout=5;""";
		СтрокаПодключения = СтрЗаменить(СтрокаПодключения, "&Filename", ПолноеИмяФайла);
		СтрокаПодключения = СтрЗаменить(СтрокаПодключения, "&Foldername", ПутьКФайлу);
		
		Соединение = Новый COMОбъект("ADODB.CONNECTION");
		
		Попытка
			
			Соединение.Open(СтрокаПодключения);
			
		Исключение
			
			Сообщить("Ошибка открытия Excel " + Символы.ПС + ОписаниеОшибки());
			Возврат;
			
		КонецПопытки;
		
		БД = Новый COMОбъект("ADOX.Catalog");
		БД.ActiveConnection = Соединение;
		
		ТекстЗапроса =
		"select *
		|from [&ИмяТаблицы]";
		
		ТаблицыБД = БД.Tables;
		
		Для каждого ТаблицаБД из ТаблицыБД Цикл 
			
			НаборЗаписей = ОткрытьТаблицу(ТаблицаБД.Name, ТекстЗапроса, Соединение);
			
			Если Не НаборЗаписей = Неопределено Тогда
				
				ВызватьЧтениеДанных(НаборЗаписей);
				
				Прервать;
				
			КонецЕсли; 
			
		КонецЦикла;
		
		НаборЗаписей = Неопределено;
		БД = Неопределено;
		Соединение = Неопределено;
#КонецОбласти
	КонецЕсли;
	
	Сообщить("Чтение данных окончено в " + ТекущаяДата());
	
КонецПроцедуры

&НаКлиенте
Процедура ВызватьЧтениеДанных(Источник)
	
	ЗагрузкаЧерез1С = (ВариантЗагрузки = "1С");
	
	ДатаНачала 		= ТекущаяДата();
	Сч 				= 2;		
	ВсегоСтрок 		= ?(ЗагрузкаЧерез1С, Источник.ВысотаТаблицы, Источник.RecordCount);
	
	Пока ?(ЗагрузкаЧерез1С, Сч <= ВсегоСтрок, Источник.EOF() = 0) Цикл
		
		КонтрольноеЧисло = ((Сч - 1) / 500);
		
		Если Сч = 2 Или КонтрольноеЧисло = Цел(КонтрольноеЧисло) Тогда
			УправлениеИнтерфейсом.ВывестиТекущееСостояние("Чтение данных...", ДатаНачала, Сч - 1, ВсегоСтрок);
		КонецЕсли; // Если Сч = 2 Или КонтрольноеЧисло = Цел(КонтрольноеЧисло) Тогда
		
		NSN = ПолучитьЗначениеВЯчейке(Источник, Сч, 0, ЗагрузкаЧерез1С);
		Если Не ЗначениеЗаполнено(NSN) Тогда
			Продолжить;
			Если Не ЗагрузкаЧерез1С Тогда
				Источник.MoveNext();
			КонецЕсли; // Если Не ЗагрузкаЧерез1С Тогда
			Сч = Сч + 1;
		КонецЕсли; // Если Не ЗначениеЗаполнено(NSN) Тогда
		
		СтрокаДанных = ЗагруженныеДанные.Добавить();
		СтрокаДанных.NSN 				= NSN;
		СтрокаДанных.RN 				= ПолучитьЗначениеВЯчейке(Источник, Сч, 1, ЗагрузкаЧерез1С);
		СтрокаДанных.RN_TRANSLIT 		= ПолучитьЗначениеВЯчейке(Источник, Сч, 2, ЗагрузкаЧерез1С);
		СтрокаДанных.ORIG_NAME_RU 		= ПолучитьЗначениеВЯчейке(Источник, Сч, 3, ЗагрузкаЧерез1С);
		СтрокаДанных.ORIG_NAME_ENG 		= ПолучитьЗначениеВЯчейке(Источник, Сч, 4, ЗагрузкаЧерез1С);
		СтрокаДанных.ORIG_NAME_TRANSLIT = ПолучитьЗначениеВЯчейке(Источник, Сч, 5, ЗагрузкаЧерез1С);
		СтрокаДанных.INC 				= ПолучитьЗначениеВЯчейке(Источник, Сч, 6, ЗагрузкаЧерез1С);
		СтрокаДанных.NAME_EN 			= ПолучитьЗначениеВЯчейке(Источник, Сч, 7, ЗагрузкаЧерез1С);
		СтрокаДанных.NAME_RU 			= ПолучитьЗначениеВЯчейке(Источник, Сч, 8, ЗагрузкаЧерез1С);
		СтрокаДанных.NSG 				= ПолучитьЗначениеВЯчейке(Источник, Сч, 9, ЗагрузкаЧерез1С);
		СтрокаДанных.NSC 				= ПолучитьЗначениеВЯчейке(Источник, Сч, 10, ЗагрузкаЧерез1С);
		СтрокаДанных.NCAGE 				= ПолучитьЗначениеВЯчейке(Источник, Сч, 11, ЗагрузкаЧерез1С);
		СтрокаДанных.CODIFIED_BY 		= ПолучитьЗначениеВЯчейке(Источник, Сч, 12, ЗагрузкаЧерез1С);
		СтрокаДанных.NAME_ORG 			= ПолучитьЗначениеВЯчейке(Источник, Сч, 13, ЗагрузкаЧерез1С);
		СтрокаДанных.NAME_ORG_TRANSLIT 	= ПолучитьЗначениеВЯчейке(Источник, Сч, 14, ЗагрузкаЧерез1С);
		СтрокаДанных.CIN 				= ПолучитьЗначениеВЯчейке(Источник, Сч, 15, ЗагрузкаЧерез1С);
		СтрокаДанных.DATE_CREA 			= ПолучитьДату(ПолучитьЗначениеВЯчейке(Источник, Сч, 16, ЗагрузкаЧерез1С), Сч, "DATE_CREA");
		СтрокаДанных.DATE_ASSI 			= ПолучитьДату(ПолучитьЗначениеВЯчейке(Источник, Сч, 17, ЗагрузкаЧерез1С), Сч, "DATE_ASSI");
		СтрокаДанных.DATE_CANC 			= ПолучитьДату(ПолучитьЗначениеВЯчейке(Источник, Сч, 18, ЗагрузкаЧерез1С), Сч, "DATE_CANC");
		СтрокаДанных.TIIC 				= ПолучитьЗначениеВЯчейке(Источник, Сч, 19, ЗагрузкаЧерез1С);
		СтрокаДанных.RPDMRC 			= ПолучитьЗначениеВЯчейке(Источник, Сч, 20, ЗагрузкаЧерез1С);
		СтрокаДанных.KIDN 				= ПолучитьЗначениеВЯчейке(Источник, Сч, 21, ЗагрузкаЧерез1С);
		СтрокаДанных.RNCC 				= ПолучитьЗначениеВЯчейке(Источник, Сч, 22, ЗагрузкаЧерез1С);
		СтрокаДанных.RNVC 				= ПолучитьЗначениеВЯчейке(Источник, Сч, 23, ЗагрузкаЧерез1С);
		СтрокаДанных.DAC 				= ПолучитьЗначениеВЯчейке(Источник, Сч, 24, ЗагрузкаЧерез1С);
		СтрокаДанных.RNJC 				= ПолучитьЗначениеВЯчейке(Источник, Сч, 25, ЗагрузкаЧерез1С);
		СтрокаДанных.RNSC 				= ПолучитьЗначениеВЯчейке(Источник, Сч, 26, ЗагрузкаЧерез1С);
		СтрокаДанных.RNFC 				= ПолучитьЗначениеВЯчейке(Источник, Сч, 27, ЗагрузкаЧерез1С);
		СтрокаДанных.RNAAC 				= ПолучитьЗначениеВЯчейке(Источник, Сч, 28, ЗагрузкаЧерез1С);
		СтрокаДанных.НомерСтроки 		= Сч;
		
		Если Не ЗагрузкаЧерез1С Тогда
			Источник.MoveNext();
		КонецЕсли; // Если Не ЗагрузкаЧерез1С Тогда
		
		Сч = Сч + 1;
		
	КонецЦикла; // Пока ?(ЗагрузкаЧерез1С, Сч <= ВсегоСтрок, Источник.EOF() = 0) Цикл
	
КонецПроцедуры // ВызватьЧтениеДанных

&НаКлиенте
Функция ОткрытьТаблицу(ИмяТаблицы, ТекстЗапроса, Соединение)
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяТаблицы", ИмяТаблицы);
	
	НаборЗаписей = Новый COMОбъект("ADODB.RecordSet");
	
	Попытка
		
		НаборЗаписей.CursorType = 3;
		НаборЗаписей.Open(ТекстЗапроса, Соединение);
		
	Исключение
		
		Сообщить("Ошибка взаимодействия с Excel " + Символы.ПС + ОписаниеОшибки());
		Возврат Неопределено;
		
	КонецПопытки;
	
	Если НаборЗаписей.RecordCount = 0 Тогда
		
		Возврат Неопределено;
		
	КонецЕсли;
	
	Возврат НаборЗаписей;
		
КонецФункции

&НаКлиенте
Функция ПолучитьЗначениеВЯчейке(Источник, НомерСтроки, ИндексКолонки, ЗагрузкаЧерез1С)
	Возврат ?(ЗагрузкаЧерез1С, Источник.Область(НомерСтроки, ИндексКолонки + 1).Текст, Источник.Fields(ИндексКолонки).Value);
КонецФункции // ПолучитьЗначениеВЯчейке

&НаКлиенте
Функция ПолучитьДату(ДатаСтрокой, НомерСтроки, ИмяКолонки)
	
	Если Не ЗначениеЗаполнено(ДатаСтрокой) Тогда
		
		Возврат Неопределено;
		
	КонецЕсли;
	
	ПромежуточнаяСтрока = СтрЗаменить(СтрЗаменить(СтрЗаменить(ДатаСтрокой, "-", ""), ":", ""), " ", "");
	ПромежуточнаяСтрока = Лев(ПромежуточнаяСтрока, СтрДлина(ПромежуточнаяСтрока) - 2);
	
	Попытка
		
		Возврат Дата(ПромежуточнаяСтрока);
		
	Исключение
		
		Сообщить("Некорретный формат даты " + ДатаСтрокой + " в строке " + НомерСтроки + " колонка " + ИмяКолонки);
		Возврат Неопределено;
		
	КонецПопытки;
	
КонецФункции

#КонецОбласти

//ПРОЦЕДУРЫ И ФУНКЦИИ ЗАПИСИ ДАННЫХ В БАЗУ ДАННЫХ
#Область ЗаписьДанных

&НаКлиенте
Процедура ЗаписатьДанные(Команда)
	
	Если ЗагруженныеДанные.Количество() = 0 Тогда
		
		ПоказатьПредупреждение(, "Нет данных для загрузки!");
		Возврат;
		
	КонецЕсли;
	
	ПоказатьВопрос(Новый ОписаниеОповещения("ЗаписатьДанныеЗавершение", ЭтаФорма), "Данные в базе данных будут изменены. Продолжить?", РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьДанныеЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		ВыполнитьЗаписьДанных();
		
	Иначе
		
		Сообщить("Действие отменено!");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьЗаписьДанных()
	
	Сообщить("Запись данных начата в " + ТекущаяДата());
	Состояние("Запись данных в базу данных...", , , БиблиотекаКартинок.ДлительнаяОперация48);
	ВыполнитьЗаписьДанныхНаСервере();
	Сообщить("Запись данных окончена в " + ТекущаяДата());
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьЗаписьДанныхНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗагруженныеДанные.NSN КАК NSN,
	|	ЗагруженныеДанные.RN КАК RN,
	|	ЗагруженныеДанные.RN_TRANSLIT КАК RN_TRANSLIT,
	|	ЗагруженныеДанные.ORIG_NAME_RU КАК ORIG_NAME_RU,
	|	ЗагруженныеДанные.ORIG_NAME_ENG КАК ORIG_NAME_ENG,
	|	ЗагруженныеДанные.ORIG_NAME_TRANSLIT КАК ORIG_NAME_TRANSLIT,
	|	ЗагруженныеДанные.INC КАК INC,
	|	ЗагруженныеДанные.NAME_EN КАК NAME_EN,
	|	ЗагруженныеДанные.NAME_RU КАК NAME_RU,
	|	ЗагруженныеДанные.NSG КАК NSG,
	|	ЗагруженныеДанные.NSC КАК NSC,
	|	ЗагруженныеДанные.NCAGE КАК NCAGE,
	|	ЗагруженныеДанные.CODIFIED_BY КАК CODIFIED_BY,
	|	ЗагруженныеДанные.NAME_ORG КАК NAME_ORG,
	|	ЗагруженныеДанные.NAME_ORG_TRANSLIT КАК NAME_ORG_TRANSLIT,
	|	ЗагруженныеДанные.CIN КАК CIN,
	|	ЗагруженныеДанные.DATE_CREA КАК DATE_CREA,
	|	ЗагруженныеДанные.DATE_ASSI КАК DATE_ASSI,
	|	ЗагруженныеДанные.DATE_CANC КАК DATE_CANC,
	|	ЗагруженныеДанные.TIIC КАК TIIC,
	|	ЗагруженныеДанные.RPDMRC КАК RPDMRC,
	|	ЗагруженныеДанные.KIDN КАК KIDN,
	|	ЗагруженныеДанные.RNCC КАК RNCC,
	|	ЗагруженныеДанные.RNVC КАК RNVC,
	|	ЗагруженныеДанные.DAC КАК DAC,
	|	ЗагруженныеДанные.RNJC КАК RNJC,
	|	ЗагруженныеДанные.RNSC КАК RNSC,
	|	ЗагруженныеДанные.RNFC КАК RNFC,
	|	ЗагруженныеДанные.RNAAC КАК RNAAC,
	|	ЗагруженныеДанные.НомерСтроки КАК НомерСтроки
	|ПОМЕСТИТЬ втЗагруженныеДанные
	|ИЗ
	|	&ЗагруженныеДанные КАК ЗагруженныеДанные
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	NSN,
	|	RN,
	|	NCAGE
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ПредметыСнабженияНАТО.Ссылка, ЗНАЧЕНИЕ(Справочник.ПредметыСнабженияНАТО.ПустаяСсылка)) КАК ПредметСнабженияНАТО,
	|	втЗагруженныеДанные.NSN КАК NSN,
	|	втЗагруженныеДанные.RN КАК RN,
	|	втЗагруженныеДанные.RN_TRANSLIT КАК RN_TRANSLIT,
	|	втЗагруженныеДанные.ORIG_NAME_RU КАК ORIG_NAME_RU,
	|	втЗагруженныеДанные.ORIG_NAME_ENG КАК ORIG_NAME_ENG,
	|	втЗагруженныеДанные.ORIG_NAME_TRANSLIT КАК ORIG_NAME_TRANSLIT,
	|	втЗагруженныеДанные.INC КАК INC,
	|	втЗагруженныеДанные.NAME_EN КАК NAME_EN,
	|	втЗагруженныеДанные.NAME_RU КАК NAME_RU,
	|	втЗагруженныеДанные.NSG КАК NSG,
	|	втЗагруженныеДанные.NSC КАК NSC,
	|	втЗагруженныеДанные.NCAGE КАК NCAGE,
	|	втЗагруженныеДанные.CODIFIED_BY КАК CODIFIED_BY,
	|	втЗагруженныеДанные.NAME_ORG КАК NAME_ORG,
	|	втЗагруженныеДанные.NAME_ORG_TRANSLIT КАК NAME_ORG_TRANSLIT,
	|	втЗагруженныеДанные.CIN КАК CIN,
	|	втЗагруженныеДанные.DATE_CREA КАК DATE_CREA,
	|	втЗагруженныеДанные.DATE_ASSI КАК DATE_ASSI,
	|	втЗагруженныеДанные.DATE_CANC КАК DATE_CANC,
	|	втЗагруженныеДанные.TIIC КАК TIIC,
	|	втЗагруженныеДанные.RPDMRC КАК RPDMRC,
	|	втЗагруженныеДанные.KIDN КАК KIDN,
	|	втЗагруженныеДанные.RNCC КАК RNCC,
	|	втЗагруженныеДанные.RNVC КАК RNVC,
	|	втЗагруженныеДанные.DAC КАК DAC,
	|	втЗагруженныеДанные.RNJC КАК RNJC,
	|	втЗагруженныеДанные.RNSC КАК RNSC,
	|	втЗагруженныеДанные.RNFC КАК RNFC,
	|	втЗагруженныеДанные.RNAAC КАК RNAAC,
	|	втЗагруженныеДанные.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	втЗагруженныеДанные КАК втЗагруженныеДанные
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПредметыСнабженияНАТО КАК ПредметыСнабженияНАТО
	|		ПО втЗагруженныеДанные.NSN = ПредметыСнабженияНАТО.КодNSN
	|			И втЗагруженныеДанные.RN = ПредметыСнабженияНАТО.RN
	|			И втЗагруженныеДанные.NCAGE = ПредметыСнабженияНАТО.NCAGE
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	Запрос.УстановитьПараметр("ЗагруженныеДанные", ЗагруженныеДанные.Выгрузить());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ТаблицаЗапроса = РезультатЗапроса.Выгрузить();
	
	ПредметыСнабженияНАТОМенеджер = Справочники.ПредметыСнабженияНАТО;
	
	СозданныеЭлементы = Новый Массив;
	
	Для каждого СтрокаТаблицы Из ТаблицаЗапроса Цикл
		
		Ключ = СтрокаТаблицы.NSN + "|" + СтрокаТаблицы.RN + "" + СтрокаТаблицы.NCAGE;
			
		Если ЗначениеЗаполнено(СтрокаТаблицы.ПредметСнабженияНАТО) Или Не СозданныеЭлементы.Найти(Ключ) = Неопределено Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		ПредметСнабженияНАТООбъект = ПредметыСнабженияНАТОМенеджер.СоздатьЭлемент();
		ЗаполнитьЗначенияСвойств(ПредметСнабженияНАТООбъект, СтрокаТаблицы);
		ПредметСнабженияНАТООбъект.Наименование = СтрокаТаблицы.INC;
		ПредметСнабженияНАТООбъект.КодNSN = СтрокаТаблицы.NSN;
		
		Попытка
			
			 ПредметСнабженияНАТООбъект.Записать();
			 СтрокаТаблицы.ПредметСнабженияНАТО = ПредметСнабженияНАТООбъект.Ссылка;
			 ЗаписатьВЛог(СтрокаТаблицы.ПредметСнабженияНАТО, "Создан предмет снабжения НАТО", Формат(СтрокаТаблицы.НомерСтроки, "ЧГ=0"));
			 СОзданныеЭлементы.Добавить(Ключ);
			
		 Исключение
			 
			 Описание = "Не удалось создать предмет снабжения " + СтрокаТаблицы.NSN + " " +СтрокаТаблицы.NAME_RU + " " +СтрокаТаблицы.RN + ":" +
			 	Символы.ПС + ОписаниеОшибки();
				
			 ЗаписатьВЛог(, Описание, Формат(СтрокаТаблицы.НомерСтроки, "ЧГ=0"));
			 Сообщить(Описание, СтатусСообщения.Важное);
			
		КонецПопытки;
		
	КонецЦикла;
	
	ЗагруженныеДанные.Загрузить(ТаблицаЗапроса);
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьВЛог(Объект1С = Неопределено, Описание, ИдентификаторСтроки)
			
	ЛогЗагрузкиМенеджер = РегистрыСведений.ЛогЗагрузки.СоздатьМенеджерЗаписи();
	ЛогЗагрузкиМенеджер.Период = ТекущаяДата();
	ЛогЗагрузкиМенеджер.ГУИД = Строка(Новый УникальныйИдентификатор);
	ЛогЗагрузкиМенеджер.Файл = ИмяФайла;
	ЛогЗагрузкиМенеджер.Объект1С = Объект1С;
	ЛогЗагрузкиМенеджер.ИдентификаторСтроки = ИдентификаторСтроки;
	ЛогЗагрузкиМенеджер.Описание = Описание;
	ЛогЗагрузкиМенеджер.ПутьКФайлу = ПолноеИмяФайла;		
	ЛогЗагрузкиМенеджер.Записать(); 
	
КонецПроцедуры

#КонецОбласти