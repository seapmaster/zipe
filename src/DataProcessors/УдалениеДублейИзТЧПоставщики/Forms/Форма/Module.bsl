
//////////////////////////// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ //////////////
#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция УдалитьНаСервере()
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
 	Возврат ОбработкаОбъект.УдалитьДублиНаСервере(Истина)
КонецФункции

&НаСервере
Процедура СформироватьНеобработанныеПС()
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КаталогПредметовСнабженияИзготовителиИПоставщики.Ссылка КАК ПС
		|ИЗ
		|	Справочник.КаталогПредметовСнабжения.ИзготовителиИПоставщики КАК КаталогПредметовСнабженияИзготовителиИПоставщики
		|ГДЕ
		|	НЕ КаталогПредметовСнабженияИзготовителиИПоставщики.Ссылка.ПометкаУдаления
		|
		|СГРУППИРОВАТЬ ПО
		|	КаталогПредметовСнабженияИзготовителиИПоставщики.Ссылка
		|
		|ИМЕЮЩИЕ
		|	КОЛИЧЕСТВО(КаталогПредметовСнабженияИзготовителиИПоставщики.Контрагент) <> КОЛИЧЕСТВО(РАЗЛИЧНЫЕ КаталогПредметовСнабженияИзготовителиИПоставщики.Контрагент)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПС";
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	КоличествоВсего = РезультатЗапроса.Количество();
	
	СписокПС.Загрузить(РезультатЗапроса);
	
КонецПроцедуры

&НаСервере
Процедура СписокПСПриАктивизацииСтрокиНаСервере(ТекущийПС)
	ТЧПоставщики.Загрузить(ТекущийПС.ИзготовителиИПоставщики.Выгрузить());	
КонецПроцедуры

&НаСервере
Процедура СохранитьИзмененияНаСервере(ТекущийПС)
	ПСОбъект = ТекущийПС.ПолучитьОбъект();
	ПСОбъект.ИзготовителиИПоставщики.Загрузить(ТЧПоставщики.Выгрузить());
	ПСОбъект.Записать();
КонецПроцедуры

#КонецОбласти
/////////////////////////////////////////////////////////////////////////

//////////////////////////// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ //////////////////
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура СписокПСПриАктивизацииСтроки(Элемент)
	Если СписокПС.Количество() > 0 Тогда
		ТекущийПС = Элементы.СписокПС.ТекущиеДанные.ПС;
		СписокПСПриАктивизацииСтрокиНаСервере(ТекущийПС);
	КонецЕсли;
КонецПроцедуры


#КонецОбласти
/////////////////////////////////////////////////////////////////////////

/////////////////////////////////////// КОМАНДЫ ФОРМЫ ///////////////////
#Область КомандыФормы

&НаКлиенте
Процедура СформиватьСписокПС(Команда)
	СформироватьНеобработанныеПС();
КонецПроцедуры

&НаКлиенте
Процедура УдалитьДубли(Команда)
	Структура = УдалитьНаСервере();
	Если Структура.Свойство("ФоновоеЗаданиеУжеЗапущено") Тогда
		Сообщить("Запущено фоновое задание по удалению дублей");
		Возврат;
	КонецЕсли;
	Сообщить("Обработано элементов " + Структура.КоличествоОбработанныхЭлементов);
	СформироватьНеобработанныеПС();
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИзменения(Команда)
	ТекущийПС = Элементы.СписокПС.ТекущиеДанные.ПС;
	СохранитьИзмененияНаСервере(ТекущийПС);
КонецПроцедуры

#КонецОбласти
/////////////////////////////////////////////////////////////////////////