//ПРОЦЕДУРЫ И ФУНКЦИИ ЧТЕНИЯ ФАЙЛА
#Область ЧтениеФайла

&НаКлиенте
Процедура ЗагрузитьДанные(Команда)
	
	Если Не ЗначениеЗаполнено(ПутьКФайлу) Тогда
		Сообщить("Не выбран файл!");
		Возврат;
	КонецЕсли; 
	
	ПоказатьВопрос(Новый ОписаниеОповещения("ЗагрузитьДанныеЗавершение", ЭтаФорма), "Результат предыдущей загрузки или сопоставления будет очищен. Продолжить?", РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьДанныеЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ВыполнитьЗагрузкуДанных();
	Иначе
		Сообщить("Прервано пользователем!");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьЗагрузкуДанных()
	
	Состояние("Чтение файла Excel ",,,БиблиотекаКартинок.ДлительнаяОперация48);
	
	СоответствиеПредметовСнабжения = ПолучитьСоответствиеПредметовСнабжения();
	
	КартинкиПредметовСнабжения.Очистить();
	
	Сообщить("Загрузка данных начата в "+ТекущаяДата());
	
	Файл = Новый Файл(ПутьКФайлу);
		
	СтрокаПодключения = "Provider=MSDASQL.1;Persist Security Info=False;Extended Properties=""DSN=Excel Files;DBQ=&Filename;DefaultDir=&Foldername;DriverId=1046;MaxBufferSize=2048;PageTimeout=5;""";
	СтрокаПодключения = СтрЗаменить(СтрокаПодключения,"&Filename",Файл.ПолноеИмя);
	СтрокаПодключения = СтрЗаменить(СтрокаПодключения,"&Foldername",Файл.Путь);
	
	Соединение = Новый COMОбъект("ADODB.CONNECTION");
	
	Попытка
		Соединение.Open(СтрокаПодключения);
	Исключение
		ТекИнформацияОбОшибке = "Произошла ошибка: " + ОписаниеОшибки() + ", не подключиться к источнику. Обработка прервана!";
		Сообщить(ТекИнформацияОбОшибке,СтатусСообщения.Важное);
		Возврат;
	КонецПопытки;
	
	БД = Новый COMОбъект("ADOX.Catalog");
	БД.ActiveConnection = Соединение;
	
	ТекстЗапроса =
	"select obozn, nameSys, Znach, ris
    |from [kat_print_img$]
    |where Har = 2";
	
	НаборЗаписей = Новый COMОбъект("ADODB.RecordSet");
	
	Попытка
		НаборЗаписей.CursorType = 3;
		НаборЗаписей.Open(ТекстЗапроса,Соединение);
	Исключение
		ТекИнформацияОбОшибке = "Не получены данные таблицы файла по причине "+ОписаниеОшибки();
		Сообщить(ТекИнформацияОбОшибке,СтатусСообщения.Важное);
		Возврат;
	КонецПопытки;
	
	Сч = 1;
	ВсегоСтрок = НаборЗаписей.RecordCount;
	
	ДатаНачала = ТекущаяДата();
	
	Пока НаборЗаписей.EOF()=0 Цикл
		
		УправлениеИнтерфейсом.ВывестиТекущееСостояние("Чтение данных Мурена",ДатаНачала,Сч,ВсегоСтрок);
		
		СтрокаТаблицыКартинок = КартинкиПредметовСнабжения.Добавить();
		СтрокаТаблицыКартинок.Наименование = СокрЛП(НаборЗаписей.Fields(1).Value);
		СтрокаТаблицыКартинок.Обозначение = СокрЛП(НаборЗаписей.Fields(1).Value);
		ФНН = СокрЛП(НаборЗаписей.Fields(2).Value); 
		СтрокаТаблицыКартинок.ФНН = Лев(ФНН,11)+Прав(ФНН,4);
		ПредметСнабжения = СоответствиеПредметовСнабжения.Получить(СтрокаТаблицыКартинок.ФНН);
		Если ПредметСнабжения <> Неопределено Тогда
			СтрокаТаблицыКартинок.ПредметСнабжения = ПредметСнабжения;
		Иначе
			ЗаписатьВЛог(Файл.Имя,Файл.Путь,Строка(Сч),,"Предмет снабжения "+СтрокаТаблицыКартинок.ФНН+" не найден!");
		КонецЕсли;
		СтрокаТаблицыКартинок.ПутьКФайлуКартинки = СокрЛП(НаборЗаписей.Fields(3).Value);
		Если Не ЗначениеЗаполнено(СтрокаТаблицыКартинок.ПутьКФайлуКартинки) Тогда
			ЗаписатьВЛог(Файл.Имя,Файл.Путь,Строка(Сч),,"Не указана картинка предмета снабжения "+СтрокаТаблицыКартинок.ФНН);
			СтрокаТаблицыКартинок.ФайлСуществует = Ложь;
		Иначе
			ФайлКартинки = Новый Файл(СтрокаТаблицыКартинок.ПутьКФайлуКартинки);
			СтрокаТаблицыКартинок.ФайлСуществует = ФайлКартинки.Существует();
			Если Не СтрокаТаблицыКартинок.ФайлСуществует Тогда
				ЗаписатьВЛог(ФайлКартинки.Имя,ФайлКартинки.Путь,Строка(Сч),ПредметСнабжения,"Не найден файл картинки!");
			КонецЕсли;
		КонецЕсли;
		
		Сч = Сч + 1;
		
		НаборЗаписей.MoveNext();
		
	КонецЦикла;
	
	СгруппироватьТаблицу();
	
	Сообщить("Загрузка данных окончена в "+ТекущаяДата());
	
КонецПроцедуры

&НаСервере
Процедура СгруппироватьТаблицу()
	
	тзКартинкиПредметовСнабжения = РеквизитФормыВЗначение("КартинкиПредметовСнабжения",Тип("ТаблицаЗначений"));
	тзКартинкиПредметовСнабжения.Свернуть("Наименование,Обозначение,ФНН,ПредметСнабжения,ПутьКФайлуКартинки,ФайлСуществует");
	КартинкиПредметовСнабжения.Загрузить(тзКартинкиПредметовСнабжения);
	
КонецПроцедуры
 
&НаСервере
Функция ПолучитьСоответствиеПредметовСнабжения()
	
	СоответствиеПредметовСнабжения = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КаталогПредметовСнабжения.ФНН КАК ФНН,
	|	КаталогПредметовСнабжения.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.КаталогПредметовСнабжения КАК КаталогПредметовСнабжения";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СоответствиеПредметовСнабжения.Вставить(Выборка.ФНН,Выборка.Ссылка);
	КонецЦикла;
	
	Возврат СоответствиеПредметовСнабжения;
	
КонецФункции 
 
#КонецОбласти

//ПРОЦЕДУРЫ И ФУНКЦИИ ЗАПИСИ ДАННЫХ В БД
#Область ЗаписьДанных

&НаСервере
Процедура ЗаписатьВЛог(ИмяФайла,ПутьКФайлу,ИдентификаторСтроки,Объект1С=Неопределено,Описание)
	
	ЛогЗагрузкиМенеджерЗаписи = РегистрыСведений.ЛогЗагрузки.СоздатьМенеджерЗаписи();
	ЛогЗагрузкиМенеджерЗаписи.Период = ТекущаяДата();
	ЛогЗагрузкиМенеджерЗаписи.ГУИД = Строка(Новый УникальныйИдентификатор);
	ЛогЗагрузкиМенеджерЗаписи.Файл = ИмяФайла;
	ЛогЗагрузкиМенеджерЗаписи.ИдентификаторСтроки = ИдентификаторСтроки;
	Если Объект1С <> Неопределено Тогда
		ЛогЗагрузкиМенеджерЗаписи.Объект1С = Объект1С;
	КонецЕсли; 
	ЛогЗагрузкиМенеджерЗаписи.Описание = Описание;
	ЛогЗагрузкиМенеджерЗаписи.ПутьКФайлу = ПутьКФайлу;
	ЛогЗагрузкиМенеджерЗаписи.Записать();
	
	Сообщить(ИмяФайла+" "+ИдентификаторСтроки+" "+Строка(Объект1С)+" "+Описание,СтатусСообщения.Важное);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьДанные(Команда)
	
	Если КартинкиПредметовСнабжения.Количество() = 0 Тогда
		Сообщить("Нет данных для записи!");
		Возврат;
	КонецЕсли;
	
	ПоказатьВопрос(Новый ОписаниеОповещения("ЗаписатьДанныеЗавершение", ЭтаФорма), "Данные в БД будут изменены. Продолжить?", РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьДанныеЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Сообщить("Запись данных начата в "+ТекущаяДата());
		Состояние("Запись данных в БД",,,БиблиотекаКартинок.ДлительнаяОперация48);
		ВыполнитьЗаписьДанных();
		Сообщить("Запись данных окончена в "+ТекущаяДата());
	Иначе
		Сообщить("Прервано пользователем!");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьЗаписьДанных()
	
	Сч = 1;
	ВсегоСтрок = КартинкиПредметовСнабжения.Количество();
	ДатаНачала = ТекущаяДата();
	
	Для каждого СтрокаКартинка Из КартинкиПредметовСнабжения Цикл
		
		Если СтрокаКартинка.ФайлСуществует Тогда
			
			УправлениеИнтерфейсом.ВывестиТекущееСостояние("Загрузка картинок",ДатаНачала,Сч,ВсегоСтрок);
			
			ФайлКартинки = Новый Файл(СтрокаКартинка.ПутьКФайлуКартинки);
			
			ПомещаемыеФайлы = Новый Массив;
			ПомещаемыеФайлы.Добавить(Новый ОписаниеПередаваемогоФайла(ФайлКартинки.ПолноеИмя));
			
			ПомещенныеФайлы = Новый Массив;
			
			Если Не ПоместитьФайлы(ПомещаемыеФайлы, ПомещенныеФайлы,, Ложь) Тогда
				
				ЗаписатьВЛог(ФайлКартинки.Имя,ФайлКартинки.Путь,Строка(Сч),СтрокаКартинка.ПредметСнабжения,"Не удалось поместить файл во временное хранилище");
				Продолжить;
				
			КонецЕсли;
			
			АдресВременногоХранилищаФайла = ПомещенныеФайлы.Получить(0).Хранение;
			
			ПрисоединенныйФайл = ПрисоединенныеФайлыСлужебныйВызовСервера.ДобавитьФайл(СтрокаКартинка.ПредметСнабжения,
			ФайлКартинки.ИмяБезРасширения,
			ОбщегоНазначенияКлиентСервер.РасширениеБезТочки(ФайлКартинки.Расширение),
			,
			ФайлКартинки.ПолучитьУниверсальноеВремяИзменения(),
			АдресВременногоХранилищаФайла);
			
			Если ПрисоединенныйФайл = Неопределено Тогда
				
				ЗаписатьВЛог(ФайлКартинки.Имя,ФайлКартинки.Путь,Строка(Сч),СтрокаКартинка.ПредметСнабжения,"Не присвоить файл предмету снабжения");	
				
			КонецЕсли;
			
		КонецЕсли; 
		
		Сч = Сч + 1;
		
	КонецЦикла; 
	
	Сообщить("Всего обновлено "+Строка(Сч)+" элементов");
	
КонецПроцедуры

#КонецОбласти 

//ПРОЦЕДУРЫ И ФУНКЦИИ СОБЫТИЙ ФОРМЫ
#Область СобытияФормы

&НаКлиенте
Процедура ПутьКФайлуНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ВыборФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	
	ВыборФайла.Показать(Новый ОписаниеОповещения("ПутьКФайлуНачалоВыбораЗавершение", ЭтаФорма));
	
КонецПроцедуры

&НаКлиенте
Процедура ПутьКФайлуНачалоВыбораЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если (ВыбранныеФайлы <> Неопределено) Тогда
		ПутьКФайлу = ВыбранныеФайлы[0];
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
